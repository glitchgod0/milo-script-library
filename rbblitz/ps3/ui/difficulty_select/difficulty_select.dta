{new UIPanel
   difficulty_select_panel
   (file "difficulty_select.milo")
   (focus difficulties.lst)
   (last_difficulty difficulty_select_medium)
   (enter
      {difficulties.lst set_data
         (difficulty_select_medium difficulty_select_expert)
      }
      {difficulties.lst set_selected [last_difficulty]}
   )
   (select_difficulty
      ($user)
      {set [last_difficulty] {difficulties.lst selected_sym}}
      {$user set_difficulty
         {switch {difficulties.lst selected_sym}
            (difficulty_select_medium kDifficultyMedium)
            (difficulty_select_expert kDifficultyExpert)
         }
      }
   )
   (SELECT_MSG
      {$this select_difficulty $user}
      {demo_on_select_difficulty}
      {{ui current_screen} difficulty_chosen}
   )
   (BUTTON_DOWN_MSG
      {switch $action
         (kAction_Cancel {ui goto_screen powerup_select_screen})
      }
      kDataUnhandled
   )
}
{new UIScreen
   difficulty_select_screen
   (panels difficulty_select_panel shell_music_panel)
   (focus difficulty_select_panel)
   (enter
      {shell_sfx set_interceptor $this}
      {difficulty_select_panel set_showing TRUE}
   )
   (difficulty_chosen
      {shell_sfx play_into_game}
      {loading_screen set wait_for_button_press TRUE}
      {ui goto_screen loading_screen}
   )
}