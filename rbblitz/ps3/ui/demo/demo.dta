{func enable_demo_mode
   {set $demo_state kDemoState_PreGame}
   {set $demo_tsl_state 1}
   #ifdef BRAKELEY_DEBUG
   {print "set to " $demo_tsl_state "\n"}
   {print "entering demo state PRE GAME\n"}
   #endif
}
{func disable_demo_mode
   {set $demo_state kDemoState_Off}
   #ifdef BRAKELEY_DEBUG
   {print "entering demo state OFF\n"}
   #endif
}
{func reset_demo_mode
   {disable_demo_mode}
   {enable_demo_mode}
}
{func is_in_demo_mode
   {!= $demo_state kDemoState_Off}
}
{func is_in_demo_pre_game_state
   {== $demo_state kDemoState_PreGame}
}
{func is_in_demo_game_state
   {== $demo_state kDemoState_Game}
}
{func is_in_demo_post_game_state
   {== $demo_state kDemoState_PostGame}
}
{func get_demo_state
   $demo_state
}
{func get_demo_config
   {find $syscfg demo}
}
{func demo_enter_song_select
   {if {is_in_demo_game_state}
      {reset_demo_mode}
   }
   {set $demo_tsl_state
      {cond ({>= $demo_tsl_state 32} 32) ({is_in_demo_post_game_state} 30) 1}
   }
   #ifdef BRAKELEY_DEBUG
   {print "set to " $demo_tsl_state "\n"}
   #endif
   {song_select_panel set_demo_state $demo_tsl_state}
}
{func demo_tsl_is_active
   {&& {> $demo_tsl_state 1} {< $demo_tsl_state 30}}
}
{func demo_tsl_is_choose_diff_a
   {== $demo_tsl_state 10}
}
{func demo_tsl_is_choose_diff_b
   {== $demo_tsl_state 13}
}
{func demo_start_tsl_slides
   {set $demo_tsl_state 2}
   #ifdef BRAKELEY_DEBUG
   {print "set to " $demo_tsl_state "\n"}
   #endif
   {demo_private_advance_forward_to_state}
}
{func demo_next_tsl_state
   {'++' $demo_tsl_state}
   #ifdef BRAKELEY_DEBUG
   {print "advance to " $demo_tsl_state "\n"}
   #endif
   {demo_private_advance_forward_to_state}
}
{func demo_prev_tsl_state
   {-- $demo_tsl_state}
   #ifdef BRAKELEY_DEBUG
   {print "back up to " $demo_tsl_state "\n"}
   #endif
   {demo_private_advance_backward_to_state}
}
{func demo_on_select_difficulty
   {cond
      ({&&
            {is_in_demo_pre_game_state}
            {demo_tsl_is_choose_diff_b}}
         {cheats_mgr set_unsafe_cheat_used}
         {set $demo_state kDemoState_Game}
         #ifdef BRAKELEY_DEBUG
         {print "entering demo state GAME\n"}
         #endif
      )
   }
}
{func demo_win_cheat
   {game_cheats win_cheat}
}
{func demo_enter_results_screen
   {if {== $demo_state kDemoState_Game}
      {set $demo_state kDemoState_PostGame}
      {set $demo_tsl_state 30}
      #ifdef BRAKELEY_DEBUG
      {print "entering demo state POST GAME\n"}
      #endif
   }
}
{func demo_post_game_is_active
   {'||'
      {&& {>= $demo_tsl_state 30} {< $demo_tsl_state 32}}
      {&& {>= $demo_tsl_state 40} {< $demo_tsl_state 41}}
   }
}
{func demo_show_post_game_tsl
   {set $demo_tsl_state 40}
   #ifdef BRAKELEY_DEBUG
   {print "set to " $demo_tsl_state "\n"}
   #endif
   {demo_private_advance_forward_to_state}
}
{func demo_private_advance_forward_to_state
   {song_select_panel set_demo_state $demo_tsl_state}
   {switch $demo_tsl_state
      (8
         {do
            ($song_id {song_mgr get_song_id_from_short_name feelgoodinc})
            ($song_data {song_mgr data $song_id})
            {{song_select_panel find songs.lst} set_selected
               {{song_select_panel get_songs_provider} get_position $song_data}
            }
            {song_select_panel update_current_song}
            {loading_screen set_song $song_data}
         }
      )
      (10 {song_select_screen song_chosen})
   }
}
{func demo_private_advance_backward_to_state
   {switch $demo_tsl_state
      (29 {set $demo_tsl_state 30})
   }
   {song_select_panel set_demo_state $demo_tsl_state}
}
{new UIPanel demo_player_profile_panel (file "player_profile.milo")}
{new UIPanel demo_deckbuilding_panel (file "demo_deckbuilding.milo")}
{new UIPanel demo_trybuy_panel (file "demo_trybuy.milo")}
{new UIPanel demo_results_panel (file "demo_results_purchase.milo")}