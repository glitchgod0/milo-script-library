{new UIPanel
   splash_panel
   (file "splash.milo")
   (state_on_enter start)
   (enter
      {{$this loaded_dir} set state [state_on_enter]}
      {shell_sfx set_interceptor $this}
   )
   (exit
      {set [state_on_enter] {{$this loaded_dir} get state}}
      {shell_sfx clear_interceptor}
   )
   (check_trial
      {if {== {{$this loaded_dir} get state} main_menu}
         {do
            ($options_array
               {array
                  (splash_start
                     splash_leaderboard
                     #ifdef HX_XBOX
                     splash_achievements
                     #endif
                     splash_options
                     splash_upgrade
                     splash_dlc
                     #ifdef HX_XBOX
                     splash_exit
                     #endif
                  )
               }
            )
            {if_else {trial_mgr get_trial_mode}
               {do
                  {remove_elem $options_array splash_dlc}
                  {start.lst set_data $options_array}
                  {achievements set_allow_achievements FALSE}
               }
               {do
                  {remove_elem $options_array splash_upgrade}
                  {start.lst set_data $options_array}
                  {if {&& {platform_mgr is_connected} {trial_mgr get_needs_free_song_pack}}
                     {splash_screen show_upsell}
                  }
               }
            }
         }
      }
   )
   (on_select_msg {shell_sfx play_select})
   (goto_main_hub
      {if_else
         {'||'
            {rock_central is_login_blocked}
            {user_mgr is_participating_user_online}
         }
         {ui goto_screen recommendation_screen}
         {ui goto_screen song_select_screen}
      }
   )
   (SELECT_MSG
      {if_else {== $component start.but}
         {do
            {ui goto_screen server_connect_screen}
            {user_mgr set_participating_user $user}
         }
         {if {== {{$this loaded_dir} get state} main_menu}
            {switch {start.lst selected_sym}
               (splash_start {$this goto_main_hub})
               (splash_leaderboard {ui goto_screen career_leaderboard_screen})
               (splash_achievements
                  {data_mine_mgr add_event view_achievements (("from_screen" "splash_menu"))}
                  {do
                     ($successfully_displayed
                        {display_achievements {user_mgr get_participating_user}}
                     )
                     {if {! $successfully_displayed}
                        {error_popup_panel show_popup achievements_unavailable splash_panel}
                     }
                  }
               )
               (splash_options
                  {options_screen set back_screen splash_screen}
                  {ui goto_screen options_screen}
               )
               (splash_dlc
                  {if_else {user_mgr is_participating_user_online}
                     {do
                        {data_mine_mgr add_event enter_store (("from_screen" "splash_menu"))}
                        {ui goto_screen store_menu_screen}
                     }
                     {error_popup_panel show_popup store_unavailable_not_online splash_panel}
                  }
               )
               (splash_upgrade
                  {if_else {platform_mgr is_connected}
                     {trial_mgr offer_trial_upgrade $user}
                     #ifdef HX_PS3
                     {if_else {! {platform_mgr has_online_privilege -1}}
                        {error_popup_panel show_popup error_no_purchase_permission splash_panel}
                        {error_popup_panel show_popup error_unlock_full_game splash_panel}
                     }
                     #else
                     {error_popup_panel show_popup error_unlock_full_game splash_panel}
                     #endif
                  }
               )
               (splash_exit
                  {pause_exit_game_panel set display_msg exit_game_confirmation}
                  {ui goto_screen confirm_exit_game_screen}
               )
            }
         }
      }
   )
   (BUTTON_DOWN_MSG
      {switch {{$this loaded_dir} get state}
         (main_menu
            {switch $action
               (kAction_Start
                  {handle
                     ($this button_down $user $raw_button kAction_Confirm $pad_num)
                  }
               )
               (kAction_ViewModify
                  {handle
                     ($this button_down $user $raw_button kAction_Cancel $pad_num)
                  }
               )
               (kAction_Cancel
                  {shell_sfx play_back}
                  {{$this loaded_dir} set state start}
                  {user_mgr set_participating_user ''}
               )
               kDataUnhandled
            }
         )
         (start
            {switch $action
               (kAction_Start
                  {user_mgr set_participating_user $user}
                  {ui goto_screen server_connect_screen}
               )
               kDataUnhandled
            }
         )
         kDataUnhandled
      }
   )
}
{new UIScreen
   splash_screen
   (panels shell_music_panel splash_panel upsell_panel error_popup_panel)
   (focus splash_panel)
   (enter
      {splash_panel check_trial}
      {do
         ($part_usr {user_mgr get_participating_user})
         {if $part_usr
            {update_participating_user_presence CONTEXT_PRESENCE_RP_EXPLORING_MENU}
         }
      }
      {platform_mgr add_sink $this}
   )
   (exit {platform_mgr remove_sink $this})
   (show_upsell
      {upsell_panel set_options
         upsell_title_rbpack
         upsell_text_rbpack
         offer_pack_upgrade
      }
      {upsell_panel update_confirm inline_help_download}
      {upsell_panel set_showing TRUE}
      {$this set_focus_panel upsell_panel}
   )
   (hide_upsell
      {upsell_panel set_showing FALSE}
      {$this set_focus_panel splash_panel}
   )
   (SIGNIN_CHANGED_MSG
      {if {upsell_panel showing}
         {$this hide_upsell}
      }
   )
}
{new UIScreen game_exit_screen (panels) (enter {exit})}
{new UIScreen
   confirm_exit_game_screen
   (panels
      shell_music_panel
      splash_panel
      upsell_panel
      error_popup_panel
      pause_exit_game_panel
   )
   (focus pause_exit_game_panel)
   (back_screen splash_screen)
   (confirm_exit
      {if_else {trial_mgr get_trial_mode}
         {ui goto_screen upsell_and_exit_screen}
         {ui goto_screen game_exit_screen}
      }
   )
}