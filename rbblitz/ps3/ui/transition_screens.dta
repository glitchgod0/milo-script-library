{func create_one_way_transition_screen
   ($screen_one $screen_two)
   {new TransitionScreen
      {symbol
         {sprintf "trans_%s_%s" $screen_one $screen_two}
      }
      (screens
         {object $screen_one}
         {object $screen_two}
      )
      (stored_parameter_for_going_left FALSE)
      (safe_to_transition FALSE)
      (is_transitioning FALSE)
      (enter
         {$this set safe_to_transition FALSE}
         {$this set is_transitioning FALSE}
      )
      (TRANSITION_COMPLETE_MSG
         {do
            ($target_header_num
               {if_else {[next_screen] has (header_index)}
                  {[next_screen] get header_index}
                  -1
               }
            )
            ($start_header_num
               {if_else {[prev_screen] has (header_index)}
                  {[prev_screen] get header_index}
                  -1
               }
            )
            ($start_group_num {floor {/ $start_header_num 10}})
            ($target_group_num {floor {/ $target_header_num 10}})
            ($start_index_num {mod $start_header_num 10})
            ($target_index_num {mod $target_header_num 10})
            ($did_jump {!= $start_group_num $target_group_num})
            ($did_loop
               {&&
                  {! $did_jump}
                  {&&
                     {!= {abs {- $start_index_num $target_index_num}} 1}
                     {== $start_group_num 0}
                  }
               }
            )
            ($anim_type
               {if_else $did_jump
                  {if_else {> $start_header_num $target_header_num}
                     shift_up
                     shift_down
                  }
                  {if_else {'^^' {> $start_header_num $target_header_num} $did_loop}
                     shift_left
                     shift_right
                  }
               }
            )
            {$this transition_panels
               $anim_type
               $start_group_num
               $target_group_num
               $start_index_num
               $target_index_num
               {$this get_prev_panels}
               "transition_out.flow"
            }
            {$this transition_panels
               $anim_type
               $start_group_num
               $target_group_num
               $start_index_num
               $target_index_num
               {$this get_next_panels}
               "transition_in.flow"
            }
         }
         {$this set safe_to_transition TRUE}
      )
      (transition_panels
         ($anim_type
            $start_group_num
            $target_group_num
            $start_index_num
            $target_index_num
            $panel_list
            $flow_name
         )
         {do
            ($cur_panel)
            {foreach $cur_panel $panel_list
               {if
                  {&&
                     {$cur_panel loaded_dir}
                     {== {{$cur_panel loaded_dir} get_type} transitioning_panel}
                  }
                  {do
                     ($transition_flow {$cur_panel find $flow_name})
                     {{$cur_panel loaded_dir} set anim_type $anim_type}
                     {{$cur_panel loaded_dir} set start_group $start_group_num}
                     {{$cur_panel loaded_dir} set target_group $target_group_num}
                     {{$cur_panel loaded_dir} set start_header $start_index_num}
                     {{$cur_panel loaded_dir} set target_header $target_index_num}
                     {if $transition_flow
                        #ifdef TRANSITION_SPEW
                        {print
                           "Running "
                           $flow_name
                           " for "
                           {$cur_panel name}
                           "with "
                           $anim_type
                           "\n"
                        }
                        #endif
                        {$transition_flow activate}
                     }
                  }
               }
            }
         }
      )
      (poll
         {if {&& [safe_to_transition] {! [is_transitioning]}}
            {$this set is_transitioning TRUE}
            {foreach $cur_panel {$this get_prev_panels}
               {do
                  ($cur_panel_dir {$cur_panel loaded_dir})
                  {if {&& $cur_panel_dir {== {$cur_panel_dir get_type} transitioning_panel}}
                     {$this set
                        is_transitioning
                        {&& [is_transitioning] {! {$cur_panel_dir get in_transition}}}
                     }
                  }
               }
            }
            {foreach $cur_panel {$this get_next_panels}
               {do
                  ($cur_panel_dir {$cur_panel loaded_dir})
                  {if {&& $cur_panel_dir {== {$cur_panel_dir get_type} transitioning_panel}}
                     {$this set
                        is_transitioning
                        {&& [is_transitioning] {! {$cur_panel_dir get in_transition}}}
                     }
                  }
               }
            }
            {if [is_transitioning]
               {ui goto_screen [next_screen]}
            }
         }
      )
      (refresh_content
         {handle ([prev_screen] refresh_content) ([next_screen] refresh_content)}
      )
      (is_content_refresh_done
         {do
            ($prev_screen_handled {handle_ret ([prev_screen] is_content_refresh_done)})
            ($next_screen_handled {handle_ret ([next_screen] is_content_refresh_done)})
            {&&
               {'||'
                  {== {type $prev_screen_handled} {type kDataUnhandled}}
                  $prev_screen_handled
               }
               {'||'
                  {== {type $next_screen_handled} {type kDataUnhandled}}
                  $next_screen_handled
               }
            }
         }
      )
   }
}
{new Object ui_init_anchor}
{func transition_between
   ($screen_a $screen_b)
   {do
      ($screen_name
         {symbol
            {sprintf "trans_%s_%s" {$screen_a name} {$screen_b name}}
         }
      )
      {set_this ui_init_anchor}
      {if {! {exists $screen_name}}
         {create_one_way_transition_screen {$screen_a name} {$screen_b name}}
      }
      {ui goto_screen {object $screen_name}}
   }
}
{func transition_to
   ($screen_b)
   {transition_between {ui current_screen} $screen_b}
}