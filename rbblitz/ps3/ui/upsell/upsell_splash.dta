{new UIPanel
   upsell_splash_panel
   (file "upsell_splash.milo")
   (screen_num 0)
   (max_screens 2)
   (exit_afterward FALSE)
   (enter
      {$this set screen_num 0}
      {$this show_group}
   )
   (show_group
      {switch [screen_num]
         (0
            {splash1.grp set_showing TRUE}
            {splash2.grp set_showing FALSE}
            {InlineHelp.ihp set_action_token kAction_Confirm continue}
            {back_to_game.ihp set_showing FALSE}
         )
         (1
            {splash1.grp set_showing FALSE}
            {splash2.grp set_showing TRUE}
            {if [exit_afterward]
               {InlineHelp.ihp set_action_token kAction_Confirm upsell_quit}
            }
            {back_to_game.ihp set_showing [exit_afterward]}
         )
      }
   )
   (BUTTON_DOWN_MSG
      {switch $action
         (kAction_Confirm
            {'++' [screen_num]}
            {if_else {>= [screen_num] [max_screens]}
               {do
                  {if_else [exit_afterward]
                     {ui goto_screen game_exit_screen}
                     {ui goto_screen {results_shared_panel get next_screen}}
                  }
               }
               {do
                  {$this show_group}
               }
            }
         )
         (kAction_ShellOption
            {if_else {platform_mgr is_connected}
               {trial_mgr offer_trial_upgrade $user}
               {error_popup_panel show_popup error_unlock_full_game $this}
            }
         )
         (kAction_Cancel
            {if {back_to_game.ihp showing}
               {ui goto_screen splash_screen}
            }
         )
      }
   )
}
{new UIScreen
   upsell_splash_screen
   (panels shell_music_panel upsell_splash_panel error_popup_panel)
   (focus upsell_splash_panel)
   (enter {upsell_splash_panel set exit_afterward FALSE})
}
{new UIScreen
   upsell_and_exit_screen
   (panels shell_music_panel upsell_splash_panel error_popup_panel)
   (focus upsell_splash_panel)
   (enter {upsell_splash_panel set exit_afterward TRUE})
}