{new UIPanel
   challenge_request_panel
   (file "challenge_request.milo")
   (rec_idx -1)
   (rec_metadata NULL)
   (enter {$this hide_popup_immediate})
   (exit)
   (prepare_popup
      ($player_name $song_name $rec_index $rec_metadata)
      {set [rec_idx] $rec_index}
      {set [rec_metadata] $rec_metadata}
      {challenge_confirm.lbl set_token_fmt
         challenge_request
         $player_name
         $song_name
      }
      {if_else [rec_metadata]
         {challenge_help.ihp set_action_token kAction_Confirm challenge_play_song}
         {challenge_help.ihp set_action_token kAction_Confirm store_offer}
      }
      {$this show_popup_loading}
   )
   (show_popup_loading {{$this loaded_dir} set challenge_popup_state loading})
   (show_popup_message {{$this loaded_dir} set challenge_popup_state visible})
   (hide_popup {{$this loaded_dir} set challenge_popup_state hidden})
   (hide_popup_immediate
      {{$this loaded_dir} set challenge_popup_state force_hidden}
   )
   (show_error
      ($error_token)
      {$this hide_popup}
      {error_popup_panel show_popup $error_token recommendation_panel}
   )
   (switch_back_to_recs
      {set [rec_idx] -1}
      {$this hide_popup}
      {recommendation_screen set_focus_panel recommendation_panel}
   )
   (on_create_challenge_complete
      ($dingo_result)
      {switch $dingo_result
         (kDingoRetSuccess
            {$this show_popup_message}
            {recommendation_screen set_focus_panel $this}
         )
         (kDingoRetTooManyChallengesOutgoing
            {$this show_error error_challenge_too_many}
         )
         (kDingoRetTooManyChallengesIncoming
            {$this show_error error_challenge_opponent_too_many}
         )
         (kDingoRetInvalidChallenge
            {$this show_error error_challenge_create_invalid}
         )
         {$this show_error error_unknown}
      }
   )
   (BUTTON_DOWN_MSG
      {switch $action
         (kAction_Confirm
            {do
               {$this hide_popup_immediate}
               {if_else [rec_metadata]
                  {do
                     {recommendation_panel stop_preview_audio}
                     {loading_screen set_song [rec_metadata]}
                     {ui goto_screen powerup_select_screen}
                  }
                  {do
                     {recommendation_panel go_to_store
                        {{ui get_recommendation_mgr} get_rec_by_index [rec_idx]}
                     }
                  }
               }
               {{ui get_recommendation_mgr} expire_rec_by_idx [rec_idx]}
            }
         )
         (kAction_Cancel
            {{ui get_recommendation_mgr} expire_rec_by_idx [rec_idx]}
            {$this switch_back_to_recs}
         )
         kDataUnhandled
      }
   )
}