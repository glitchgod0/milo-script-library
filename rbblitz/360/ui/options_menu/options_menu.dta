{new UIPanel
   options_panel
   (file "options_menu.milo")
   (focus options.lst)
   (enter
      {if_else
         {'||'
            {trial_mgr get_trial_mode}
            {! {user_mgr is_participating_user_online}}
         }
         {do
            {options.lst set_data
               (tutorial controls calibration audio_video_options credits)
            }
            {trial.flow activate}
         }
         {do
            {options.lst set_data
               (tutorial controls calibration audio_video_options rbworld_link credits)
            }
            {non_trial.flow activate}
         }
      }
      {{background_night_panel find spotlights.grp} set_showing FALSE}
      {shell_sfx set_interceptor $this}
   )
   (exit {shell_sfx clear_interceptor})
   (on_select_msg {shell_sfx play_select})
   (SELECT_MSG
      {switch {$this focus_name}
         (options.lst
            {switch {options.lst selected_sym}
               (calibration {ui goto_screen cal_welcome_screen})
               (audio_video_options {ui goto_screen av_options_screen})
               (rbworld_link {ui goto_screen rbworld_link_screen})
               (controls {ui goto_screen controller_config_screen})
               (tutorial {ui goto_screen tutorials_screen})
               (credits {ui goto_screen credits_screen})
            }
         )
      }
   )
   (BUTTON_DOWN_MSG
      {switch $action
         (kAction_Start
            {handle
               ($this button_down $user $raw_button kAction_Confirm $pad_num)
            }
         )
         (kAction_ViewModify
            {handle
               ($this button_down $user $raw_button kAction_Cancel $pad_num)
            }
         )
         (kAction_Cancel
            {shell_sfx play_back}
            {ui goto_screen {options_screen get back_screen}}
         )
         kDataUnhandled
      }
   )
}
{new UIScreen
   options_screen
   (panels
      background_night_panel
      shell_music_panel
      menu_cache_panel
      options_panel
   )
   (focus options_panel)
   (back_screen splash_screen)
   (enter
      {update_participating_user_presence CONTEXT_PRESENCE_RP_EXPLORING_MENU}
   )
}