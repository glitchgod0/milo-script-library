{new UIPanel
   controller_config_panel
   (file "controller_config.milo")
   (number_of_mappings -1)
   (enter
      {controller_mapping_state.ep set
         controller_mapping_index
         {save_data get_controller_mapping}
      }
      #ifdef HX_PS3
      {ps.mesh set_showing TRUE}
      {xbox.mesh set_showing FALSE}
      #endif
   )
   (BUTTON_DOWN_MSG
      {switch $action
         (kAction_Cancel {ui goto_screen {{ui current_screen} get back_screen}})
         (kAction_Confirm
            {save_data set_controller_mapping
               {controller_mapping_state.ep get controller_mapping_index}
            }
            {if {game_screen get_game}
               {{game_screen get_game} update_controls}
            }
            {ui goto_screen {{ui current_screen} get back_screen}}
         )
         (kAction_Left
            {glow_lt.flow activate}
            {controller_mapping_state.ep set
               controller_mapping_index
               {mod
                  {- {controller_mapping_state.ep get controller_mapping_index} 1}
                  [number_of_mappings]
               }
            }
         )
         (kAction_Right
            {glow_rt.flow activate}
            {controller_mapping_state.ep set
               controller_mapping_index
               {mod
                  {'+' {controller_mapping_state.ep get controller_mapping_index} 1}
                  [number_of_mappings]
               }
            }
         )
         (kAction_Start
            {handle
               ($this button_down $user $raw_button kAction_Confirm $pad_num)
            }
         )
         (kAction_ViewModify
            {handle
               ($this button_down $user $raw_button kAction_Cancel $pad_num)
            }
         )
         kDataUnhandled
      }
   )
}
{controller_config_panel set
   number_of_mappings
   {-
      {size
         {lookup_config
            (beatmatcher controller joypad mappings)
         }
      }
      1
   }
}
{new UIScreen
   controller_config_screen
   (panels
      background_night_panel
      shell_music_panel
      menu_cache_panel
      controller_config_panel
   )
   (focus controller_config_panel)
   (back_screen options_screen)
}
{new UIScreen
   controller_config_pause_screen
   (panels
      game_panel
      pause_panel
      game_fade_out_panel
      background_night_panel
      controller_config_panel
   )
   (focus controller_config_panel)
   (back_screen game_screen)
}