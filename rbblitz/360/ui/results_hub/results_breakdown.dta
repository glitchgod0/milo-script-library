{new UIPanel
   results_breakdown_panel
   (file "results_breakdown.milo")
   (started_animation FALSE)
   (has_server_results)
   (BUTTON_DOWN_MSG
      {switch $action
         (kAction_Confirm
            {if_else [has_server_results]
               {do
                  {if_else {trial_mgr get_trial_mode}
                     {transition_to leaderboard_results_screen}
                     {transition_to song_results_screen}
                  }
               }
               {transition_to apology_results_screen}
            }
            {shell_sfx play_select}
         )
      }
   )
   (enter
      {{results_shared_panel find InlineHelp.ihp} clear_action_token
         kAction_Cancel
      }
      {if {! [started_animation]}
         {do
            ($game_results {{game_panel get_game} get_results})
            ($equipped_powerups {loading_screen get_equipped_powerups})
            ($powerup1 {elem $equipped_powerups 0})
            ($powerup2 {elem $equipped_powerups 1})
            ($powerup3 {elem $equipped_powerups 2})
            {breakdown.ep set base_score {$game_results get_score_breakdown base}}
            {breakdown.ep set blitz_mode {$game_results get_score_breakdown blitz}}
            {breakdown.ep set finale {$game_results get_score_breakdown finale}}
            {breakdown.ep set solo {$game_results get_score_breakdown solo}}
            {breakdown.ep set power_up_1 $powerup1}
            {breakdown.ep set
               power_up_1_amt
               {$game_results get_score_breakdown powerup1}
            }
            {breakdown.ep set power_up_2 $powerup2}
            {breakdown.ep set
               power_up_2_amt
               {$game_results get_score_breakdown powerup2}
            }
            {breakdown.ep set power_up_3 $powerup3}
            {breakdown.ep set
               power_up_3_amt
               {$game_results get_score_breakdown powerup3}
            }
         }
         {init.flow activate}
         {$this set started_animation TRUE}
      }
   )
   (exit
      {{results_shared_panel find InlineHelp.ihp} set_action_token
         kAction_Confirm
         endgame_results_continue
      }
      {{results_shared_panel find InlineHelp.ihp} set_action_token
         kAction_Cancel
         inline_help_back
      }
   )
}
{new UIScreen
   results_breakdown_screen
   (panels
      background_night_panel
      results_shared_panel
      results_cache_panel
      game_panel
      game_fade_out_panel
      results_breakdown_panel
   )
   (header_index 10)
   (focus results_breakdown_panel)
}