{new PowerupSelectPanel
   powerup_select_panel
   (file "powerup_select.milo")
   (focus powerup_select.lst)
   (can_afford TRUE)
   (selected_powerup "none")
   (enter
      {{background_panel find powerup_selection.grp} set_showing TRUE}
      {{background_panel find MainMenu.grp} set_showing FALSE}
      {powerup_select.lst set_provider {$this get_powerup_menu_provider}}
      {active_powerups.lst set_provider {$this get_active_powerups_provider}}
      {passive_powerups_b.lst set_provider
         {$this get_passive_powerups_b_powerups_provider}
      }
      {passive_powerups_a.lst set_provider
         {$this get_passive_powerups_a_provider}
      }
      {active_powerups.lst set_showing FALSE}
      {passive_powerups_a.lst set_showing FALSE}
      {passive_powerups_b.lst set_showing FALSE}
      {your_monies_text.lbl set_token_fmt current_monies {$this get_money}}
      {your_monies_text.lbl set showing {! {trial_mgr get_trial_mode}}}
      {your_cred.lbl set_token_fmt
         current_cred
         {{user_mgr get_participating_user} get_cred}
      }
      {your_cred.lbl set showing {! {trial_mgr get_trial_mode}}}
      {$this get_equipped_powerups}
      {$this remove_powerup_if_too_expensive}
      {$this update_equipped_powerups}
      {$this set_song {loading_screen get_song}}
      {$this set_album_art {{$this get_song} id}}
      {$this set_song_title {$this get_song} selection_title.lbl}
      {powerup_select.lst set_selected 0}
      {powerup_select.lst refresh}
      {{$this loaded_dir} set main_powerup_menu active}
      {$this update_powerup_cost_display active_powerups.lst}
      {$this update_powerup_desc active_powerups.lst}
      {$this set_focus powerup_select.lst}
   )
   (exit
      {{background_panel find powerup_selection.grp} set_showing FALSE}
      {$this update_equipped_powerups}
      {powerup_select.lst refresh}
      {loading_screen set_equipped_powerups {$this get_equipped_powerups}}
   )
   (SELECT_MSG
      {unless {$this is_locked {$this get_powerup_name {$this focus_name}}}
         {if_else
            {'||'
               {'||' {active_powerups.lst showing} {passive_powerups_a.lst showing}}
               {passive_powerups_b.lst showing}
            }
            {do
               {if_else {$this is_locked_in_trial {$this get_powerup_name {$this focus_name}}}
                  {{ui current_screen} show_upsell}
                  {do
                     {if [can_afford]
                        {powerup_select.lst set_showing TRUE}
                        {active_powerups.lst set_showing FALSE}
                        {passive_powerups_a.lst set_showing FALSE}
                        {passive_powerups_b.lst set_showing FALSE}
                        {$this set_focus powerup_select.lst}
                        {$this update_equipped_powerups}
                     }
                  }
               }
            }
            {switch {powerup_select.lst selected_sym}
               (active_equip {$this go_to_powerup_list active_powerups.lst})
               (passive_equip {$this go_to_powerup_list passive_powerups_a.lst})
               (upgrade_equip {$this go_to_powerup_list passive_powerups_b.lst})
               (play_song
                  {song_preview stop}
                  {$this powerups_chosen}
               )
            }
         }
         {powerup_select.lst refresh}
      }
   )
   (go_to_powerup_list
      ($list)
      {set [selected_powerup] {$this get_powerup_name $list}}
      {$list set_showing TRUE}
      {$this set_focus $list}
      {$list set_selected_simulate_scroll {$list selected_pos}}
      {$this update_equipped_powerups}
   )
   (BUTTON_DOWN_MSG
      {switch $action
         (kAction_Cancel
            {if_else
               {'||'
                  {'||' {active_powerups.lst showing} {passive_powerups_a.lst showing}}
                  {passive_powerups_b.lst showing}
               }
               {do
                  {active_powerups.lst set_showing FALSE}
                  {passive_powerups_a.lst set_showing FALSE}
                  {passive_powerups_b.lst set_showing FALSE}
                  {$this set_focus powerup_select.lst}
                  {$this get_equipped_powerups}
               }
               {do
                  ($ret_screen {{ui current_screen} get prev_screen})
                  {if
                     {&&
                        {!= $ret_screen song_select_screen}
                        {!= $ret_screen recommendation_screen}
                     }
                     {song_preview stop}
                  }
                  {{background_panel find MainMenu.grp} set_showing TRUE}
                  {{background_panel find powerup_selection.grp} set_showing FALSE}
                  {ui goto_screen $ret_screen}
               }
            }
         )
         (kAction_Confirm
            {if {$this is_locked {$this get_powerup_name {$this focus_name}}}
               {shell_sfx play_purchase_error}
            }
            {if {&& {! [can_afford]} {!= {$this focus_name} "powerup_select.lst"}}
               {shell_sfx play_purchase_error}
               {coins.anim animate}
            }
         )
      }
      kDataUnhandled
   )
   (update_powerup_cost_display
      ($list)
      {if_else {trial_mgr get_trial_mode}
         {if_else {$this is_locked_in_trial {$this get_powerup_name {$this focus_name}}}
            {cost_text.lbl set_token_fmt powerup_upgrade}
            {cost_text.lbl set_showing FALSE}
         }
         {if_else {$this is_locked {$this get_powerup_name $list}}
            {do
               {cost_text.lbl set_showing TRUE}
               {cost_text.lbl set_token_fmt
                  cred_unlock_text
                  {$this get_unlock_cred {$this get_powerup_name $list}}
               }
            }
            {do
               ($cost {$this get_cost {$this get_powerup_name $list}})
               {if_else {== $cost -1}
                  {do
                     {cost_text.lbl set_showing FALSE}
                  }
                  {do
                     {cost_text.lbl set_showing TRUE}
                     {cost_text.lbl set_token_fmt cost $cost}
                     {$this update_ability_to_afford $cost}
                     {if_else [can_afford]
                        {$this update_list_state kComponentFocused}
                        {$this update_list_state kComponentDisabled}
                     }
                  }
               }
            }
         }
      }
   )
   (update_list_state
      ($state)
      {active_powerups.lst set_state $state}
      {passive_powerups_a.lst set_state $state}
      {passive_powerups_b.lst set_state $state}
   )
   (update_ability_to_afford
      ($cost)
      {do
         ($selected_cost {$this get_cost [selected_powerup]})
         ($afford {<= $cost {'+' {$this get_current_coin_display} $selected_cost}})
         {set [can_afford] $afford}
      }
   )
   (update_powerup_desc
      ($list)
      {if_else {== {$this get_powerup_name $list} none}
         {do
            {cost_text.lbl set_showing FALSE}
            {name.lbl set_token_fmt {$this get_powerup_name $list}}
            {switch $list
               (active_powerups.lst {powerup_desc.lbl set_token_fmt active_description})
               (passive_powerups_a.lst
                  {powerup_desc.lbl set_token_fmt passive_description}
               )
               (passive_powerups_b.lst
                  {powerup_desc.lbl set_token_fmt upgrade_description}
               )
            }
         }
         {do
            ($powerup_name {$this get_powerup_name $list})
            {if {trial_mgr get_trial_mode}
               {if_else {$this is_locked_in_trial {$this get_powerup_name $list}}
                  {do
                     {cost_text.lbl set_showing TRUE}
                     {cost_text.lbl set_token_fmt powerup_upgrade}
                  }
                  {cost_text.lbl set_showing FALSE}
               }
            }
            {if_else {$this is_locked $powerup_name}
               {name.lbl set text_token locked_powerup}
               {name.lbl set_token_fmt $powerup_name}
            }
            {powerup_desc.lbl set_showing {! {$this is_locked $powerup_name}}}
            {powerup_desc.lbl set_token_fmt {$this get_description $powerup_name}}
         }
      }
   )
   (SCROLL_MSG
      {switch {powerup_select.lst selected_sym}
         (active_equip
            {do
               {{$this loaded_dir} set main_powerup_menu active}
               {$this update_powerup_cost_display active_powerups.lst}
               {$this update_powerup_desc active_powerups.lst}
            }
         )
         (passive_equip
            {do
               {{$this loaded_dir} set main_powerup_menu passive_a}
               {$this update_powerup_cost_display passive_powerups_a.lst}
               {$this update_powerup_desc passive_powerups_a.lst}
            }
         )
         (upgrade_equip
            {do
               {{$this loaded_dir} set main_powerup_menu passive_b}
               {$this update_powerup_cost_display passive_powerups_b.lst}
               {$this update_powerup_desc passive_powerups_b.lst}
            }
         )
         (play_song
            {do
               {{$this loaded_dir} set main_powerup_menu play_song}
            }
         )
      }
      {$this update_total_purchase_cost}
   )
   (show_error
      ($error_token)
      {error_popup_panel show_popup $error_token powerup_select_panel}
   )
   (set_album_art
      ($song_id)
      {do
         ($content_name {song_mgr content_name $song_id})
         {if_else {'||' {content_mgr is_mounted $content_name} {{$this get_song} is_on_disc}}
            {album_keep.tex set_bitmap {song_mgr get_album_art_path $song_id}}
            {content_mgr mount_content $content_name}
         }
         {foreach $part (guitar drum vocals bass keys)
            {do
               ($tier {song_mgr rank_tier_for_song $song_id $part})
               ($idd {sprintf "diff_%s" $part})
               {$this set_inst_difficulty $idd $tier}
            }
         }
      }
   )
   (mounting_finished {$this set_album_art {{$this get_song} id}})
   (powerups_chosen {$this purchase_powerups})
   (purchase_complete
      ($dingo_result $transaction_ticket)
      {switch $dingo_result
         (kDingoRetSuccess
            {{user_mgr get_participating_user} set_difficulty kDifficultyExpert}
            {shell_sfx play_splash}
            {loading_screen set wait_for_button_press TRUE}
            {submit_results_panel set powerups_transaction_ticket $transaction_ticket}
            {ui goto_screen loading_screen}
         )
         (kDingoRetRBWNotEnoughCoins {$this show_error error_not_enough_coins})
         (kDingoRetRBWNotEnoughCred {$this show_error error_not_enough_cred})
         (kDingoRetRBWUnknownPowerup {$this show_error error_unknown_powerup})
         (kDingoRetRBWDown {$this show_error error_rbw_down})
         {$this show_error error_unknown}
      }
   )
}
{new UIScreen
   powerup_select_screen
   (panels
      background_panel
      menu_cache_panel
      powerup_select_panel
      upsell_panel
      shell_music_panel
      error_popup_panel
   )
   (focus powerup_select_panel)
   (prev_screen recommendation_screen)
   (enter
      ($from_screen)
      {shell_sfx set_interceptor $this}
      {powerup_select_panel set_showing TRUE}
      {$this set_focus_panel powerup_select_panel}
      {$this set_song {loading_screen get_song}}
      {do
         ($restricted_screens (game_screen pause_confirm_restart_song_screen))
         {if {! {find_elem $restricted_screens $from_screen}}
            {$this set prev_screen $from_screen}
         }
      }
      {update_participating_user_presence CONTEXT_PRESENCE_RP_EXPLORING_MENU}
   )
   (exit {shell_sfx clear_interceptor})
   (show_upsell
      {upsell_panel set_options
         upsell_title_powerup_sel
         upsell_text_powerup_sel
         offer_trial_upgrade
      }
      {upsell_panel set_showing TRUE}
      {$this set_focus_panel upsell_panel}
   )
   (hide_upsell
      {upsell_panel set_showing FALSE}
      {$this set_focus_panel powerup_select_panel}
   )
}