{new CharacterCreatorPanel
   charactercreator_panel
   (file "character_creator.milo")
   (reset_focus 1)
   (enter
      {name.lbl set text_token {$this get_name}}
      {hometown.lst set_provider {$this hometown_provider}}
      {outfit.lst set_provider {$this outfit_provider}}
      {face_type.lst set_data (custom)}
      {options_grid.lst set_provider {$this face_options_provider}}
      {body_type.lst set_data
         (body_type_1 body_type_2 body_type_3 body_type_4 body_type_5)
      }
      {height.lst set_data (height_average height_tall height_short)}
   )
   (update_state
      ($state)
      {switch $state
         (1
            {closet_panel goto_shot cc_enter.shot}
            {choose_gender.trg trigger}
            {$this set_focus male.btn}
         )
         (2
            {closet_panel goto_shot cc_modify_body.shot}
            {character_options.trg trigger}
            {$this set_focus name.btn}
         )
         (3
            {closet_panel goto_shot cc_modify_face.shot}
            {modify_face.trg trigger}
            {$this set_focus face_type.btn}
         )
         (4
            {closet_panel goto_shot cc_modify_body.shot}
            {modify_body.trg trigger}
            {$this set_focus body_type.btn}
         )
         (5
            {face_maker_menu.trg trigger}
            {$this set_focus randomize.btn}
         )
         (6
            {face_maker_choose_shape.trg trigger}
            {options_grid.lst set_selected {$this get_feature_index shape}}
            {$this set_focus options_grid.lst}
         )
         (7
            {face_maker_choose_chin.trg trigger}
            {options_grid.lst set_selected {$this get_feature_index chin}}
            {$this set_focus options_grid.lst}
         )
         (8
            {face_maker_modify_chin.trg trigger}
            {$this set_focus done.btn}
         )
         (9
            {face_maker_modify_jaw.trg trigger}
            {$this set_focus done.btn}
         )
         (10
            {face_maker_choose_eyes.trg trigger}
            {options_grid.lst set_selected {$this get_feature_index eye}}
            {$this set_focus options_grid.lst}
         )
         (11
            {face_maker_modify_eyes.trg trigger}
            {$this set_focus done.btn}
         )
         (12
            {face_maker_choose_brows.trg trigger}
            {$this set_focus options_grid.lst}
         )
         (13
            {face_maker_modify_brows.trg trigger}
            {$this set_focus done.btn}
         )
         (14
            {face_maker_choose_nose.trg trigger}
            {options_grid.lst set_selected {$this get_feature_index nose}}
            {$this set_focus options_grid.lst}
         )
         (15
            {face_maker_modify_nose.trg trigger}
            {$this set_focus done.btn}
         )
         (16
            {face_maker_choose_mouth.trg trigger}
            {options_grid.lst set_selected {$this get_feature_index mouth}}
            {$this set_focus options_grid.lst}
         )
         (17
            {face_maker_modify_mouth.trg trigger}
            {$this set_focus done.btn}
         )
      }
   )
   (update_components
      {outfit.lst set_selected {$this get_outfit}}
      {eye_color.lst set_selected {$this get_eye_color}}
      {body_type.lst set_selected {$this get_body_type}}
      {height.lst set_selected {$this get_height}}
      {skin_tone.lst set_selected {$this get_skin_tone}}
   )
   (refresh_face_options_list {options_grid.lst refresh})
   (update_name_label {name.lbl set text_token {$this get_name}})
   (update_hometown_list {hometown.lst set_selected {$this get_hometown}})
   (update_outfit_list {outfit.lst set_selected {$this get_outfit}})
   (FOCUS_MSG
      {switch $new_focus
         (male.btn
            {$this set_gender male}
            {$this update_components}
         )
         (female.btn
            {$this set_gender female}
            {$this update_components}
         )
      }
   )
   (SCROLL_MSG
      {switch $component
         (hometown.lst {$this set_hometown {hometown.lst selected_sym}})
         (outfit.lst {$this set_outfit {outfit.lst selected_sym}})
         (eye_color.lst {$this set_eye_color {eye_color.lst selected_pos}})
         (body_type.lst {$this set_body_type {body_type.lst selected_pos}})
         (height.lst {$this set_height {height.lst selected_pos}})
         (skin_tone.lst {$this set_skin_tone {skin_tone.lst selected_pos}})
         (options_grid.lst {$this set_face_option {options_grid.lst selected_pos}})
      }
   )
   (SELECT_MSG
      {switch $component
         (male.btn {$this set_state 2})
         (female.btn {$this set_state 2})
         (name.btn
            {virtual_keyboard show_keyboard
               {$this get_user}
               25
               {localize create_char_vk_title}
               {localize create_char_vk_desc}
               {$this get_name}
               $this
            }
         )
         (hometown.btn {$this set_focus hometown.lst})
         (hometown.lst {$this set_focus hometown.btn})
         (outfit.btn {$this set_focus outfit.lst})
         (outfit.lst {$this set_focus outfit.btn})
         (modify_body.btn {$this set_state 4})
         (modify_face.btn {$this set_state 3})
         (create.btn
            {$this create_character}
            {ui goto_screen main_hub_screen}
         )
         (face_type.btn {$this set_focus face_type.lst})
         (face_type.lst
            {if_else {== {face_type.lst selected_sym} custom}
               {$this set_state 5}
               {$this set_focus face_type.btn}
            }
         )
         (eye_color.btn {$this set_focus eye_color.lst})
         (eye_color.lst {$this set_focus eye_color.btn})
         (face_return.btn {$this set_state 2})
         (body_type.btn {$this set_focus body_type.lst})
         (body_type.lst {$this set_focus body_type.btn})
         (height.btn {$this set_focus height.lst})
         (height.lst {$this set_focus height.btn})
         (skin_tone.btn {$this set_focus skin_tone.lst})
         (skin_tone.lst {$this set_focus skin_tone.btn})
         (body_return.btn {$this set_state 2})
         (randomize.btn {$this randomize_face})
         (shape.btn {$this set_state 6})
         (chin.btn {$this set_state 7})
         (eyes.btn {$this set_state 10})
         (brows.btn {$this set_state 12})
         (nose.btn {$this set_state 14})
         (mouth.btn {$this set_state 16})
         (options_grid.lst
            {switch {$this get_state}
               (7 {$this set_state 8})
               (10 {$this set_state 11})
               (12 {$this set_state 13})
               (14 {$this set_state 15})
               (16 {$this set_state 17})
               {$this set_state 5}
            }
         )
         (done.btn
            {switch {$this get_state}
               (8 {$this set_state 9})
               {$this set_state 5}
            }
         )
         (finished.btn {$this set_state 3})
      }
   )
   (VIRTUAL_KEYBOARD_RESULT_MSG
      {if
         {&&
            $ok
            $this
            {ui focus_panel}
            {== {ui focus_panel} $this}
            {! {== $text ""}}
         }
         {$this set_name $text}
      }
   )
}
{new BandScreen
   charactercreator_screen
   (panels meta closet_panel charactercreator_panel)
   (focus charactercreator_panel)
   (back main_hub_screen)
}