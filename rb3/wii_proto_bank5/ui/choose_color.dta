{new UIPanel
   choose_color_panel
   (file "choose_color.milo")
   (focus colors.lst)
   (init
      ($panel $config)
      {set [panel] $panel}
      {set [config] $config}
      {set [current_color] 1}
   )
   (enter {$this refresh})
   (refresh
      {if_else {== [current_color] 1}
         {do
            {choose_color_title.lbl set text_token shell_primary_color}
            {set $palette {[config] get (mats 0 color1_palette)}}
            {set $index {[config] get primary_color}}
         }
         {do
            {choose_color_title.lbl set text_token shell_secondary_color}
            {set $palette {[config] get (mats 0 color2_palette)}}
            {set $index {[config] get secondary_color}}
         }
      }
      {colors.lst set color_palette $palette}
      {colors.lst set_selected $index}
      {colors.lst store}
      {$this set_focus colors.lst}
   )
   (confirm_primary
      {colors.lst confirm}
      {set $num_colors {[config] num_color_options}}
      {if_else {== [current_color] $num_colors}
         {do
            {[panel] handle_confirm_colors}
            {ui pop_screen}
         }
         {do
            {set [current_color] 2}
            {$this refresh}
         }
      }
   )
   (cancel_primary
      {colors.lst undo $user}
      {ui pop_screen}
   )
   (confirm_secondary
      {colors.lst confirm}
      {[panel] handle_confirm_colors}
      {ui pop_screen}
   )
   (cancel_secondary
      {colors.lst undo $user}
      {set [current_color] 1}
      {$this refresh}
   )
   (SCROLL_MSG
      {switch $component
         (colors.lst
            {if_else {== [current_color] 1}
               {[config] set primary_color {colors.lst selected_pos}}
               {[config] set secondary_color {colors.lst selected_pos}}
            }
         )
      }
   )
   (BUTTON_DOWN_MSG
      {switch $action
         (1
            {if_else {== [current_color] 1}
               {$this confirm_primary}
               {$this confirm_secondary}
            }
         )
         (2
            {if_else {== [current_color] 1}
               {$this cancel_primary}
               {$this cancel_secondary}
            }
         )
         kDataUnhandled
      }
   )
}
{new UIPanel
   choose_finish_panel
   (file "choose_finish.milo")
   (focus finishes.lst)
   (init
      ($panel $name)
      {set [panel] $panel}
      {set [name] $name}
   )
   (enter
      {finishes.lst set_provider {[panel] instrument_finish_provider}}
      {finishes.lst refresh}
      {finishes.lst set_selected 0}
      {finishes.lst store}
      {$this set_focus finishes.lst}
      {[panel] preview_finish [name] {finishes.lst selected_sym}}
   )
   (SCROLL_MSG
      {switch $component
         (finishes.lst {[panel] preview_finish [name] {finishes.lst selected_sym}})
      }
   )
   (BUTTON_DOWN_MSG
      {switch $action
         (1
            {finishes.lst confirm}
            {[panel] handle_confirm_finish [name]}
            {ui pop_screen}
         )
         (2
            {finishes.lst undo $user}
            {ui pop_screen}
         )
         kDataUnhandled
      }
   )
}