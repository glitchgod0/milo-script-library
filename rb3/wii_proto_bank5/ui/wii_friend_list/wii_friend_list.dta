{new WiiFriendsProvider wii_friends_provider}
{new WiiFriendsDetailsProvider wii_friends_details_provider}
{new WiiInvitationsProvider wii_invitations_provider}
{new WiiFriendsListPanel
   wiifriend_panel
   (prevents_override 1)
   (file wii_friend_list.milo)
   (enter
      {overshell set_active_status 0}
      {friends.lst set_provider wii_friends_provider}
      {details.lst set_provider wii_friends_details_provider}
      {friends.lst set_selected 0}
      {$this set_focus friends.lst}
      {$this update_prompts}
   )
   (refresh_lists
      {friends.lst refresh}
      {wii_friends_details_provider set_wii_friend
         {wii_friends_provider get_friend_name {friends.lst selected_pos}}
      }
      {details.lst refresh}
   )
   (poll)
   (exit {overshell set_active_status 1})
   (update_prompts
      {if_else {== {$this focus_name} "details.lst"}
         {prompts.ihp set_config
            (
               (1 wii_friends_details_compare_goals)
               (14 wii_friends_details_compare_scores)
            )
         }
         {prompts.ihp set_config
            (
               (1 wii_friends_view_profiles)
               (14 wii_friends_send_invite)
            )
         }
      }
   )
   (BUTTON_DOWN_MSG
      {switch $action
         (2
            {if_else {== {$this focus_name} "details.lst"}
               {do
                  {$this set_focus friends.lst}
                  {$this update_prompts}
               }
               {ui goto_screen main_hub_screen}
            }
         )
         (14
            {if_else {== {$this focus_name} "details.lst"}
               {print
                  "[ISDebug] Compare career scores with "
                  {details.lst selected_pos}
                  " goes here"
               }
               {print
                  "[ISDebug] Invite player "
                  {wii_friends_provider get_friend_name {friends.lst selected_pos}}
               }
            }
         )
         kDataUnhandled
      }
   )
   (SELECT_MSG
      {switch $component
         (friends.lst
            {$this set_focus details.lst}
            {$this update_prompts}
         )
         (details.lst
            {print
               "[ISDebug] Compare goals with "
               {details.lst selected_pos}
               " goes here"
            }
         )
         kDataUnhandled
      }
   )
   (SCROLL_MSG
      {if {== $component friends.lst}
         {do
            {wii_friends_details_provider set_wii_friend
               {wii_friends_provider get_friend_name {friends.lst selected_pos}}
            }
            {details.lst set_selected 0}
            {details.lst refresh}
         }
      }
   )
}
{new BandScreen
   wiifriends_screen
   (prevents_override 1)
   (panels meta wiifriend_panel)
   (focus wiifriend_panel)
   (back main_hub_screen)
}
{new BandScreen
   wii_friends_prompt
   (panels dialog_panel)
   (focus dialog_panel)
   (show_followup 0)
   (enter
      {overshell set_active_status 0}
      {profile_mgr set_wii_friends_prompt_shown}
      {dialog_panel set_yesno wii_friends_prompt_text no.btn}
      {set [show_followup] 0}
   )
   (exit {overshell set_active_status 1})
   (poll
      {if [show_followup]
         {if_else {profile_mgr get_using_wii_friends}
            {dialog_panel set_ok wii_friends_prompt_followup_yes ok.btn}
            {dialog_panel set_ok wii_friends_prompt_followup_no ok.btn}
         }
      }
   )
   (close_dialog
      {ui pop_screen}
      {if {== {ui transition_screen} splash_screen}
         {splash_panel set_splash_state kSplashScreen_PreEndOvershell}
      }
   )
   (SELECT_MSG
      {switch $component
         (yes.btn
            {profile_mgr set_using_wii_friends 1}
            {set [show_followup] 1}
         )
         (no.btn
            {profile_mgr set_using_wii_friends 0}
            {set [show_followup] 1}
         )
         (ok.btn {$this close_dialog})
      }
      kDataUnhandled
   )
}
{wii_friend_mgr add_fake_friend "Micah" "Micah" 2 32546 64}
{wii_friend_mgr add_fake_friend "Micah" "Mom" 0 500 64}
{wii_friend_mgr add_fake_friend "Micah" "Robert" 2 24 2}
{wii_friend_mgr add_fake_friend "Sara" "Jess" 0 32546 64}
{wii_friend_mgr add_fake_friend "Sara" "Brian" 0 500 64}
{wii_friend_mgr add_fake_friend "GrandpaTaylor" "Robert" 2 24 2}
{wii_friend_mgr add_fake_friend "GrandpaTaylor" "Stephanie" 2 24 2}
{wii_friend_mgr add_fake_friend "GrandpaTaylor" "George" 2 24 2}
{wii_friend_mgr add_fake_friend "Rocco" "Chris" 0 24 2}
{wii_friend_mgr add_fake_friend "Lisa" "Stephanie" 1 24 2}