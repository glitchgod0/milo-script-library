{new_gh_panel pause
   (clear_vram TRUE)
   (navigator
      (vertical
         pause_resume.btn
         pause_restart.btn
         pause_options.btn
         pause_quit.btn
      )
   )
   (enter {$this set_focus pause_resume.btn})
}
#define PAUSE_SCREEN_HANDLERS (
   (allow_back FALSE)
   (animate_transition FALSE)
   (enter
      {game set_paused TRUE}
      {app set_paused TRUE}
   )
   (exit {app set_paused FALSE})
   (resume
      {game set_paused FALSE}
      {ui goto_screen game}
   )
)
{new_gh_screen pause
   (panels GAME_PANELS pause)
   (focus pause)
   (in_game TRUE)
   PAUSE_SCREEN_HANDLERS
   (SELECT_MSG
      {switch $component
         (pause_resume.btn {$this resume})
         (pause_restart.btn
            {game restart}
            {ui goto_screen game}
         )
         (pause_options.btn {ui goto_screen pause_settings})
         (pause_quit.btn {meta_loading_goto {meta get continue_screen}})
      }
   )
}
{func meta_loading_goto
   ($next_screen)
   {{ui screen meta_loading} set next_screen $next_screen}
   {ui goto_screen meta_loading}
}
{new_gh_screen meta_loading
   (panels)
   (allow_back FALSE)
   (animate_transition FALSE)
   (enter {autosave_goto {$this get next_screen}})
}
{new_gh_panel pause_controller
   (clear_vram TRUE)
   (focus pause_controller_resume.btn)
   (enter
      {pause_controller_msg.lbl set_text
         {if_else {game multiplayer}
            controller_loss_msg_multi
            controller_loss_msg
         }
      }
   )
   (poll
      {if_else {game is_missing_controller}
         {pause_controller_resume.btn set_state kDisabled}
         {pause_controller_resume.btn set_state kFocused}
      }
   )
}
{new_gh_screen pause_controller
   (panels GAME_PANELS pause_controller)
   (focus pause_controller)
   (in_game TRUE)
   PAUSE_SCREEN_HANDLERS
   (SELECT_MSG
      {switch $component
         (pause_controller_resume.btn {$this resume})
      }
   )
   (BUTTON_DOWN_MSG
      {if_else {game is_missing_controller}
         TRUE
         kDataUnhandled
      }
   )
}