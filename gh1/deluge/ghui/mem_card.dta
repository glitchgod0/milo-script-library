#define kMCNoError (0)
#define kMCNoCard (-101)
#define kMCNotFormatted (-102)
#define kMCDifferentCard (-103)
#define kMCReadWriteFailed (-104)
#define kMCCorrupt (-105)
#define kMCNotEnoughSpace (-106)
#define kMCGeneralError (-110)
#define kMCFileExists (-120)
#define kMCAlreadyFormatted (-121)
#define MEMCARD_RESULT_MSG (memcard_result ($result $space_free))
#define DIALOG_SCREEN (
   (panels meta dialog helpbar)
   (focus dialog)
   (allow_back FALSE)
   (animate_transition FALSE)
)
{new_panel dialog
   (object_name dialog)
   (force_exit TRUE)
   (navigator (vertical dl_button1.btn dl_button2.btn))
   (get_button
      ($button_name)
      {sprintf 'dl_%s.btn' $button_name}
   )
   (set_button_text
      ($button $text)
      {$button set_showing TRUE}
      {$button set_state kNormal}
      {$button set_text $text}
   )
   (set_button_focus
      ($button)
      {$this set_focus $button}
      {$button set_state kFocused}
   )
   (hide_button
      ($button)
      {$button set_showing FALSE}
      {$button set_state kDisabled}
   )
   (setup
      ($btn1_text $btn2_text $default $msg $title)
      {do
         (
            ($b1 {$this get_button button1})
            ($b2 {$this get_button button2})
         )
         {if_else {!= $btn1_text ''}
            {$this set_button_text $b1 $btn1_text}
            {$this hide_button $b1}
         }
         {if_else {!= $btn2_text ''}
            {$this set_button_text $b2 $btn2_text}
            {$this hide_button $b2}
         }
         {switch $default
            (button1 {$this set_button_focus $b1})
            (button2 {$this set_button_focus $b2})
            (none 0)
            {fail "Expected button1/button2/none, got: " $default}
         }
      }
      {dl_message.lbl set_localized_text {memcard localize $msg}}
      {dl_title.lbl set_text $title}
      {helpbar set_display {$this get_help_text ()}}
   )
   (get_help_text
      ($array)
      {resize $array 0}
      {if {dl_button1.btn showing}
         {push_back $array (fret1 help_select)}
      }
      {if {dl_button2.btn showing}
         {push_back $array (strum help_updown)}
      }
      {eval $array}
   )
   (message
      ($msg $title)
      {$this setup '' '' none $msg $title}
   )
}
{new_gh_screen bootup_load
   (panels)
   (allow_back FALSE)
   (animate_transition FALSE)
   (enter
      {set $autosave TRUE}
      {$this delay_task 1000 (preserve $this) {memcard load_data $this}}
   )
   (MEMCARD_RESULT_MSG
      {switch $result
         (kMCNoError
            {set $mc_overwrite TRUE}
            {$this delay_task 1000 {ui goto_screen pub_splash}}
         )
         (kMCNoCard {ui goto_screen bootup_nocard})
         (kMCCorrupt
            {set $mc_overwrite TRUE}
            {ui goto_screen bootup_corrupt_data}
         )
         {set $mc_overwrite FALSE}
         {ui goto_screen bootup_create_save_check}
      }
   )
}
{new_gh_screen bootup_create_save_check
   DIALOG_SCREEN
   (enter
      {dialog setup yes no button1 bootup_create_save_check mc_title_nosave}
   )
   (SELECT_MSG
      {switch $component
         (dl_button1.btn {ui goto_screen bootup_create_save})
         (dl_button2.btn {ui goto_screen bootup_caution})
      }
   )
}
{new_gh_screen bootup_create_save
   DIALOG_SCREEN
   (enter
      {dialog message bootup_create_save mc_title_saving}
      {$this delay_task
         1000
         (preserve $this)
         {memcard save_data $this $mc_overwrite}
      }
   )
   (MEMCARD_RESULT_MSG
      {switch $result
         (kMCNoError
            {set $mc_overwrite TRUE}
            {dialog message bootup_create_success mc_title_saving}
            {$this delay_task 1000 {ui goto_screen pub_splash}}
         )
         (kMCNoCard {ui goto_screen bootup_nocard})
         (kMCNotEnoughSpace {ui goto_screen bootup_nospace})
         (kMCFileExists
            {set $mc_replace_screen $this}
            {set $mc_load_screen bootup_load}
            {ui goto_screen bootup_replace_save_check}
         )
         (kMCNotFormatted
            {set $mc_overwrite FALSE}
            {set $nfw_retry_screen $this}
            {set $nfw_continue_screen bootup_caution_format_cancelled}
            {set $mc_format_start bootup_load}
            {set $mc_format_done bootup_create_save}
            {set $mc_post_save_screen bootup_load}
            {set $mc_no_screen bootup_caution_format_cancelled}
            {set $mc_yes_screen mc_format}
            {ui goto_screen bootup_format_check}
         )
         (kMCDifferentCard
            {set $mc_overwrite FALSE}
            {memcard save_data $this $mc_overwrite}
         )
         {set $mc_retry_screen $this}
         {set $mc_continue_screen bootup_caution}
         {set $mc_retry_continue_text save_error}
         {ui goto_screen retry_continue}
      }
   )
}
{new_gh_screen bootup_nocard
   DIALOG_SCREEN
   (enter {dialog setup yes no button1 bootup_nocard mc_title_error})
   (SELECT_MSG
      {switch $component
         (dl_button1.btn {ui goto_screen autosave_disable_check})
         (dl_button2.btn {ui goto_screen bootup_load})
      }
   )
}
{new_gh_screen bootup_nospace
   DIALOG_SCREEN
   (enter {dialog setup yes no button2 bootup_nospace mc_title_error})
   (SELECT_MSG
      {switch $component
         (dl_button1.btn {ui goto_screen autosave_disable_check})
         (dl_button2.btn {ui goto_screen bootup_load})
      }
   )
}
{new_gh_screen bootup_corrupt_data
   DIALOG_SCREEN
   (enter {dialog setup yes no button1 bootup_corrupt_data mc_title_error})
   (SELECT_MSG
      {switch $component
         (dl_button1.btn {ui goto_screen bootup_create_save})
         (dl_button2.btn {ui goto_screen bootup_caution})
      }
   )
}
{new_gh_screen bootup_replace_save_check
   DIALOG_SCREEN
   (enter
      {dialog setup
         load
         replace
         button1
         bootup_replace_save_check
         mc_title_caution
      }
   )
   (SELECT_MSG
      {switch $component
         (dl_button1.btn
            {set $mc_overwrite FALSE}
            {ui goto_screen $mc_load_screen}
         )
         (dl_button2.btn
            {set $mc_overwrite TRUE}
            {ui goto_screen $mc_replace_screen}
         )
      }
   )
}
{new_gh_screen bootup_caution
   DIALOG_SCREEN
   (enter {dialog setup yes no button1 bootup_caution mc_title_caution})
   (SELECT_MSG
      {switch $component
         (dl_button1.btn {ui goto_screen autosave_disable_check})
         (dl_button2.btn {ui goto_screen bootup_load})
      }
   )
}
{new_gh_screen autosave_disable_check
   DIALOG_SCREEN
   (enter
      {dialog setup yes no button1 autosave_disable_check mc_title_caution}
   )
   (SELECT_MSG
      {switch $component
         (dl_button1.btn {set $autosave FALSE})
         (dl_button2.btn {set $autosave TRUE})
      }
      {ui goto_screen pub_splash}
   )
}
{new_gh_screen bootup_format_check
   DIALOG_SCREEN
   (enter {dialog setup yes no button1 unformatted_card mc_title_caution})
   (SELECT_MSG
      {switch $component
         (dl_button1.btn {ui goto_screen $mc_yes_screen})
         (dl_button2.btn {ui goto_screen $mc_no_screen})
      }
   )
}
{new_gh_screen bootup_caution_format_cancelled
   DIALOG_SCREEN
   (enter
      {dialog setup
         yes
         no
         button2
         bootup_caution_format_cancelled
         mc_title_caution
      }
   )
   (SELECT_MSG
      {switch $component
         (dl_button1.btn
            {set $mc_overwrite FALSE}
            {ui goto_screen bootup_create_save}
         )
         (dl_button2.btn {ui goto_screen autosave_disable_check})
      }
   )
}
{new_gh_screen mc_format
   DIALOG_SCREEN
   (enter
      {dialog message format_card mc_title_caution}
      {$this delay_task 1000 (preserve $this) {memcard format $this}}
   )
   (MEMCARD_RESULT_MSG
      {switch $result
         (kMCNoError {ui goto_screen $mc_format_done})
         (kMCDifferentCard {memcard format $this})
         (kMCNoCard
            {set $mc_retry_screen $this}
            {set $mc_cancel_screen $mc_post_save_screen}
            {set $mc_retry_cancel_text format_no_card_error}
            {ui goto_screen retry_cancel}
         )
         (kMCAlreadyFormatted
            {set $mc_retry_screen $mc_format_done}
            {set $mc_cancel_screen $mc_post_save_screen}
            {set $mc_retry_cancel_text already_formatted_error}
            {ui goto_screen retry_cancel}
         )
         (kMCDamaged
            {set $mc_overwrite FALSE}
            {set $mc_retry_screen $mc_format_done}
            {set $mc_cancel_screen $mc_post_save_screen}
            {set $mc_retry_cancel_text card_damaged_error}
            {ui goto_screen retry_cancel}
         )
         {set $mc_retry_screen $this}
         {set $mc_cancel_screen mc_no_format_warn}
         {set $mc_retry_cancel_text format_card_error}
         {ui goto_screen retry_cancel}
      }
   )
}
{new_gh_screen retry_cancel
   DIALOG_SCREEN
   (enter
      {dialog setup retry cancel button2 $mc_retry_cancel_text mc_title_error}
   )
   (SELECT_MSG
      {switch $component
         (dl_button1.btn {ui goto_screen $mc_retry_screen})
         (dl_button2.btn {ui goto_screen $mc_cancel_screen})
      }
   )
}
{new_gh_screen retry_continue
   DIALOG_SCREEN
   (enter
      {dialog setup
         continue
         retry
         button1
         $mc_retry_continue_text
         mc_title_error
      }
   )
   (SELECT_MSG
      {switch $component
         (dl_button1.btn {ui goto_screen $mc_continue_screen})
         (dl_button2.btn {ui goto_screen $mc_retry_screen})
      }
   )
}
{new_gh_screen autosave
   DIALOG_SCREEN
   (enter
      {dialog message save_saving mc_title_saving}
      {$this delay_task
         1000
         (preserve $this)
         {memcard save_data $this $mc_overwrite}
      }
   )
   (MEMCARD_RESULT_MSG
      {switch $result
         (kMCNoError
            {dialog message save_success mc_title_saving}
            {campaign save_complete}
            {set $mc_overwrite TRUE}
            {$this delay_task 1000 {ui goto_screen $mc_post_save_screen}}
         )
         (kMCDifferentCard
            {set $mc_overwrite FALSE}
            {memcard save_data $this $mc_overwrite}
         )
         (kMCNoCard
            {set $mc_overwrite FALSE}
            {set $mc_no_screen $this}
            {set $mc_yes_screen $mc_post_save_screen}
            {ui goto_screen save_nocard}
         )
         (kMCNotEnoughSpace
            {set $mc_overwrite FALSE}
            {set $mc_no_screen $this}
            {set $mc_yes_screen $mc_post_save_screen}
            {ui goto_screen save_nospace}
         )
         (kMCFileExists
            {set $mc_no_screen $mc_post_save_screen}
            {set $mc_yes_screen $this}
            {ui goto_screen replace_save_check}
         )
         (kMCNotFormatted
            {set $mc_overwrite FALSE}
            {set $nfw_retry_screen $this}
            {set $nfw_continue_screen $mc_post_save_screen}
            {set $mc_format_start $this}
            {set $mc_format_done $this}
            {set $mc_no_screen mc_no_format_warn}
            {set $mc_yes_screen mc_format}
            {ui goto_screen save_unformatted}
         )
         {set $mc_retry_screen $this}
         {set $mc_continue_screen $mc_post_save_screen}
         {set $mc_retry_continue_text save_error}
         {ui goto_screen retry_continue}
      }
   )
}
{new_gh_screen save_nocard
   DIALOG_SCREEN
   (enter {dialog setup yes no button1 no_card_error mc_title_error})
   (SELECT_MSG
      {switch $component
         (dl_button1.btn {ui goto_screen $mc_yes_screen})
         (dl_button2.btn {ui goto_screen $mc_no_screen})
      }
   )
}
{new_gh_screen save_nospace
   DIALOG_SCREEN
   (enter {dialog setup yes no button1 no_space_error mc_title_error})
   (SELECT_MSG
      {switch $component
         (dl_button1.btn {ui goto_screen $mc_yes_screen})
         (dl_button2.btn {ui goto_screen $mc_no_screen})
      }
   )
}
{new_gh_screen replace_save_check
   DIALOG_SCREEN
   (enter {dialog setup yes no button2 file_exists_error mc_title_caution})
   (SELECT_MSG
      {switch $component
         (dl_button1.btn
            {set $mc_overwrite TRUE}
            {ui goto_screen $mc_yes_screen}
         )
         (dl_button2.btn
            {set $mc_overwrite FALSE}
            {ui goto_screen $mc_no_screen}
         )
      }
   )
}
{new_gh_screen save_unformatted
   DIALOG_SCREEN
   (enter {dialog setup yes no button2 unformatted_card mc_title_caution})
   (SELECT_MSG
      {switch $component
         (dl_button1.btn {ui goto_screen $mc_yes_screen})
         (dl_button2.btn {ui goto_screen $mc_no_screen})
      }
   )
}
{new_gh_screen mc_no_format_warn
   DIALOG_SCREEN
   (enter
      {dialog setup continue retry button2 no_format_warn mc_title_caution}
   )
   (SELECT_MSG
      {switch $component
         (dl_button1.btn {ui goto_screen $nfw_continue_screen})
         (dl_button2.btn {ui goto_screen $nfw_retry_screen})
      }
   )
}
{new_gh_screen memcard_load_error
   DIALOG_SCREEN
   (enter {dialog setup yes no button2 $mc_load_text mc_title_error})
   (SELECT_MSG
      {switch $component
         (dl_button1.btn {ui goto_screen memcard_load})
         (dl_button2.btn {ui goto_screen $mc_post_load_screen})
      }
   )
}
{new_gh_screen memcard_load
   DIALOG_SCREEN
   (enter
      {dialog message load_loading mc_title_loading}
      {switch {memcard get_info}
         (kMCNoCard
            {set $mc_load_text mc_nocard_error}
            {ui goto_screen memcard_load_error}
         )
      }
      {$this delay_task 1000 (preserve $this) {memcard load_data $this}}
   )
   (MEMCARD_RESULT_MSG
      {switch $result
         (kMCNoError
            {dialog message load_success mc_title_loading}
            {set $mc_overwrite TRUE}
            {$this delay_task 1000 {ui goto_screen $mc_post_load_screen}}
         )
         (kMCNoCard
            {set $mc_load_text mc_card_removed_error}
            {ui goto_screen memcard_load_error}
         )
         (kMCCorrupt
            {set $mc_load_text mc_corrupt_error}
            {ui goto_screen memcard_load_error}
         )
         (kMCDifferentCard {memcard load_data $this})
         (kMCDamaged
            {set $mc_retry_screen $this}
            {set $mc_continue_screen $mc_post_load_screen}
            {set $mc_retry_continue_text card_damaged_error_load}
            {ui goto_screen retry_continue}
         )
         {set $mc_load_text mc_nodata_error}
         {ui goto_screen memcard_load_error}
      }
   )
}