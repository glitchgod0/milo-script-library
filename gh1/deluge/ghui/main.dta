{new_gh_panel main
   (navigator
      (vertical
         main_career.btn
         main_quickspin.btn
         main_multiplayer.btn
         main_tutorial.btn
         main_options.btn
      )
   )
   (focus main_career.btn)
   (enter
      {$this reset_player_settings}
      {if {== {campaign tutorials_done} 0}
         {$this disable_component main_career.btn}
         {$this disable_component main_quickspin.btn}
         {$this disable_component main_multiplayer.btn}
         {$this set_focus main_tutorial.btn}
      }
      {if {! {$this has played_intro_lick}}
         {synth play_sequence meta_lick}
         {$this set played_intro_lick TRUE}
      }
   )
   (SELECT_MSG
      {switch $component
         (main_career.btn {ui goto_screen sel_difficulty})
         (main_quickspin.btn {ui goto_screen qp_selsong})
         (main_multiplayer.btn {ui goto_screen multi_sel_character})
         (main_tutorial.btn {ui goto_screen tutorials})
         (main_options.btn {ui goto_screen options})
      }
   )
   (poll
      {if_else {'||' {game is_missing_multi_controller} {== {campaign tutorials_done} 0}}
         {$this disable_component main_multiplayer.btn}
         {$this enable_component main_multiplayer.btn}
      }
   )
   (reset_player_settings
      {game set_difficulty kDifficultyMedium}
      {game set_song_index 0}
      {game set_venue basement}
      {game set_character metal}
      {game set_guitar_index 0}
      {do
         (($player2_config {game get_player_config 1}))
         {$player2_config set_character classic}
         {$player2_config set_guitar_index 0}
      }
   )
}
{new_gh_screen main
   (panels meta main helpbar)
   (focus main)
   (helpbar
      (default
         (
            (fret1 help_continue)
            (strum help_updown)
         )
      )
   )
   (access_all
      {{ui panel main} enable_component main_career.btn}
      {{ui panel main} enable_component main_quickspin.btn}
      {{ui panel main} enable_component main_multiplayer.btn}
   )
}