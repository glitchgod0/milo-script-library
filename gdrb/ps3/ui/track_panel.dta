#define TRACK_END_FRAME (1920)
#define TRACK_PANEL_HANDLERS (
   (enter
      {game add_sink $this}
      {$this set_showing
         {'||'
            $skip_intro
            {gamemode in_mode drum_trainer}
            {! {gamemode in_mode practice}}
         }
      }
   )
   (exit
      {if {exists guitar_effects}
         {do
            {guitar_effects set_showing FALSE}
         }
      }
      {if {exists bass_effects}
         {do
            {bass_effects set_showing FALSE}
         }
      }
      {game remove_sink $this}
   )
   (unison_hit)
   (unison_miss)
   (set_track_out)
   (set_track_in)
   (animate_track ($start $end $period $units))
   (animate_track_out)
   (animate_track_out_fast)
   (animate_track_in_fast)
   (finish_load
      {$this set_showing FALSE}
      {$this set_track_out}
   )
   (intro_start
      {$this track_reset}
      {$this set_showing TRUE}
      {$this set_track_in}
   )
   (intro_skip
      {$this track_reset}
      {$this set_showing TRUE}
      {$this set_track_out}
      {if {&& {exists gamemode} {! {gamemode get is_tutorial}}}
         {$this play_intro}
      }
   )
   (configure_optimizations
      {do
         ($total_players 0)
         {if {exists beatmatch}
            {beatmatch foreach_active_player $player
               {'++' $total_players}
            }
         }
         #ifdef HX_PS3
         {set $optimization_lod
            {if_else {> $total_players 2}
               1
               0
            }
         }
         #else
         {set $optimization_lod 0}
         #endif
         {set $track_LOD $optimization_lod}
      }
   )
   (on_reset
      {beatmatch foreach_active_player $m
         {$m on_new_track}
         {{$m track} init {$m track}}
      }
      {{$this loaded_dir} reset}
      {$this configure_optimizations}
   )
   (on_extend
      {$this play_intro}
      {if {&& {exists gamemode} {gamemode get play_track_intro_sfx}}
         {synth play track_beg.cue}
      }
   )
)
{new TrackPanel
   coop_track_panel
   (file "track/trackpanel.milo")
   TRACK_PANEL_HANDLERS
}
{new TrackPanel
   h2h_track_panel
   (file "track/trackpanel_h2h.milo")
   TRACK_PANEL_HANDLERS
}
{func get_track_panel
   {gamemode get track_panel}
}
{set $playback_file ""}