(old_excitement kExcitementInvalid)
(override_group '')
(old_override_group '')
(enter
   {$this set_fullness 1 1}
   {set [old_excitement] kExcitementInvalid}
   {set [override_group] ''}
   {set [old_override_group] ''}
)
(animate
   ($bad $ok $great)
   {$this iterate_frac
      ($bad {main.drv play_group bad $crowd.play_flags})
      ($ok {main.drv play_group ok $crowd.play_flags})
      ($great {main.drv play_group great $crowd.play_flags})
   }
)
(crowd_update
   {do
      ($over [override_group])
      ($old_override [old_override_group])
      ($level {min $crowd.authored_excitement $crowd.game_excitement})
      {if_else {&& {&& {!= $level kExcitementLost} {!= $level kExcitementWon}} $over}
         {do
            {if {!= $over $old_override}
               {$this iterate_frac (1 {main.drv play_group $over $crowd.play_flags})}
            }
            {set [old_excitement] kExcitementInvalid}
            {set [old_override_group] $over}
         }
         {if_else
            {&&
               {&& {!= $level kExcitementLost} {!= $level kExcitementWon}}
               $crowd.authored_action
            }
            {do
               {$this iterate_frac
                  (1 {main.drv play_group $crowd.authored_action $crowd.play_flags})
               }
               {set [old_excitement] kExcitementInvalid}
               {set [old_override_group] ''}
            }
            {do
               {set [old_override_group] ''}
               {if {!= $level [old_excitement]}
                  {set [old_excitement] $level}
                  {$this set show_3d_only FALSE}
                  {switch $level
                     (kExcitementIdle
                        {$this iterate_frac
                           (1 {main.drv play_group realtime_idle $crowd.idle_flags})
                        }
                     )
                     (kExcitementLost
                        {$this iterate_frac (1 {main.drv play_group lose $crowd.idle_flags})}
                     )
                     (kExcitementBoot {$this animate 1.0 0.0 0.0})
                     (kExcitementBad {$this animate 1.0 0.0 0.0})
                     (kExcitementOkay {$this animate 0.0 1.0 0.0})
                     (kExcitementGreat {$this animate 0.0 0.0 1.0})
                     (kExcitementWon
                        {do
                           {$this iterate_frac (1 {main.drv play_group win $crowd.idle_flags})}
                        }
                     )
                     {notify "unknown excitement level " $level}
                  }
               }
            }
         }
      }
   }
)
(override_anim
   ($group_name)
   {set [override_group] $group_name}
)
(force_override_anim
   ($group_name)
   {do
      {set [override_group] $group_name}
      {$this crowd_update}
   }
)