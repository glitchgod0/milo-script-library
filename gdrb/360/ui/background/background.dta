{new UIPanel
   background_panel
   (file background.milo)
   (slot0highlighted FALSE)
   (slot1highlighted FALSE)
   (slot2highlighted FALSE)
   (slot3highlighted FALSE)
   (setup_screen
      {if {{ui current_screen} has (background_view)}
         {do
            ($bg_dir {$this loaded_dir})
            ($screen {ui current_screen})
            ($viewname {$screen get background_view})
            ($view {$bg_dir find $viewname})
            {if $view
               {$screen foreach_panel $panel
                  {do
                     ($dir {$panel loaded_dir})
                     {if {&& $dir {! {== $dir $bg_dir}}}
                        {$bg_dir place_in_view $dir $view}
                        {$dir set cam {$bg_dir get cam}}
                        {$dir iterate
                           PanelDir
                           $proxy
                           {$proxy set cam {$bg_dir get cam}}
                        }
                     }
                  }
               }
            }
         }
      }
   )
   (unload {{$this loaded_dir} set current_view ""})
   (highlight_slot
      ($slot)
      {unless {$this get_highlighted $slot}
         {$this set_highlighted $slot TRUE}
         {{sprintf "match_slot0%i.trg" {'+' $slot 1}} trigger}
      }
   )
   (unhighlight_slot
      ($slot)
      {if {$this get_highlighted $slot}
         {$this set_highlighted $slot FALSE}
         {{sprintf "match_slot0%iR.trg" {'+' $slot 1}} trigger}
      }
   )
   (force_highlight_slot_immediate
      ($slot)
      {$this set_highlighted $slot TRUE}
      {{sprintf "match_slot0%i.trg" {'+' $slot 1}} play_end_of_anims}
   )
   (force_unhighlight_slot_immediate
      ($slot)
      {$this set_highlighted $slot FALSE}
      {{sprintf "match_slot0%iR.trg" {'+' $slot 1}} play_end_of_anims}
   )
   (get_highlighted
      ($slot)
      {switch $slot
         (0 [slot0highlighted])
         (1 [slot1highlighted])
         (2 [slot2highlighted])
         (3 [slot3highlighted])
      }
   )
   (set_highlighted
      ($slot $highlighted)
      {switch $slot
         (0 {set [slot0highlighted] $highlighted})
         (1 {set [slot1highlighted] $highlighted})
         (2 {set [slot2highlighted] $highlighted})
         (3 {set [slot3highlighted] $highlighted})
      }
   )
}