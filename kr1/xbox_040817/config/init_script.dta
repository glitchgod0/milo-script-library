{defun has_guitarist (locals $i $e)
   {set $i {- {num_elems $arena.stage_chars} 1}}
   {set $e 0}
   {while {>= $i 0}
      {when {== "guitar" {elem $arena.stage_char_insts $i}}
         {set $e 1}
         {set $i -1}
      }
      {set $i {- $i 1}}
   }
   {return $e}
}

{defun anim_background (args $r $g $b $period)
   {interp_task $period
      (interp $r {the_rnd clear_color_r} $r)
      (interp $g {the_rnd clear_color_g} $g)
      (interp $b {the_rnd clear_color_b} $b)
      {the_rnd set_clear_color $r $g $b}
   }
}

; PS2 only
{defun fade_screen (args $period)
   {interp_task $period
      (interp $alpha 1 0)
      {the_rnd set_blur_params $alpha 0 0 0 1 1}
   }
   {delay_task $period
      {the_rnd set_blur FALSE}
   }
   {the_rnd set_blur TRUE}
}

{defun anim_value (args $object $get_value $set_value $value $period)
   {interp_task $period
      (preserve $object $set_value)
      (interp $value {$object $get_value} $value)
      {$object $set_value $value}
   }
}

{defun animate_from (args $animatable $from $period)
   {interp_task $period
      (preserve $animatable)
      (interp $frame $from {+ $from $period})
      {$animatable set_frame $frame}
   }
}

{defun animate (args $animatable $period)
   {animate_from $animatable {$animatable frame} $period}
}

{defun animate_to (args $animatable $to $period $interp $wrap)
   {interp_task $period
      (preserve $animatable)
      (interp $frame {$animatable frame} $to $interp $wrap)
      {$animatable set_frame $frame}
   }
}
