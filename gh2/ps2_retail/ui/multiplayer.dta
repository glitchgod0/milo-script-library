{new GHPanel
   multi_panel
   (file multi.milo)
   (focus coop.btn)
   (enter
      {{game get_player_config 0} set_character rockabill1}
      {{game get_player_config 0} set_guitar lespaul lp_cayenne}
      {{game get_player_config 1} set_character alterna1}
      {{game get_player_config 1} set_guitar sg sg_cherry}
      {game set_venue small2}
      {if_else {campaign is_unlocked multi_fo}
         {do
            {$this enable faceoff.btn}
            {faceoff.btn set_showing TRUE}
         }
         {do
            {$this disable faceoff.btn}
            {faceoff.btn set_showing FALSE}
         }
      }
   )
   (SELECT_MSG
      {switch $component
         (coop.btn
            {gamecfg set mode multi_coop}
            {ui goto_screen multi_coop_selsong_screen}
         )
         (versus.btn
            {gamecfg set mode multi_vs}
            {ui goto_screen multi_vs_selsong_screen}
         )
         (faceoff.btn
            {gamecfg set mode multi_fo}
            {ui goto_screen multi_sel_character_screen}
         )
      }
   )
}
{new GHScreen
   multi_screen
   (panels meta multi_panel helpbar)
   (focus multi_panel)
   (helpbar
      (default
         (
            (fret1 help_continue)
            (fret2 help_back)
            (strum help_updown)
         )
      )
   )
}
#define MULTISELECT_OUTFIT_HANDLERS (
   (sel_buttons outfit1.btn outfit2.btn)
   (enter {set [active] FALSE})
   (FOCUS_MSG
      {if {== {$this loaded_dir} $panel_dir}
         {do
            ($idx)
            {if_else {== $new_focus "outfit1.btn"}
               {set $idx 0}
               {set $idx 1}
            }
            {multi_sel_character_panel set_outfit [player_num] $idx}
            {char_multi show_char
               [player_num]
               {{game get_player_config [player_num]} get_character}
            }
         }
      }
   )
)
{new MultiSelectPanel
   multi_char_outfit0
   (file multi_char_outfit1.milo)
   (player_num 0)
   (ready_label "ready.mesh")
   MULTISELECT_OUTFIT_HANDLERS
}
{new MultiSelectPanel
   multi_char_outfit1
   (file multi_char_outfit2.milo)
   (player_num 1)
   (ready_label "ready.mesh")
   MULTISELECT_OUTFIT_HANDLERS
}
{new MultiCharSelPanel
   multi_sel_character_panel
   (file multi_sel_character.milo)
   (skin_select1 FALSE)
   (skin_select2 FALSE)
   (enter
      {set [skin_select1] FALSE}
      {set [skin_select2] FALSE}
   )
   (choose_skins
      ($player)
      {if_else {== $player 0}
         {do
            {set [skin_select1] TRUE}
         }
         {do
            {set [skin_select2] TRUE}
         }
      }
   )
   (char_navigator CHARACTERS)
}
{new CharsysPanel
   char_multi
   (file char_multi.milo)
   (num_placers 2)
   (load_order LOAD_CHARACTERS)
   (enter
      {$this show_char 0 {game get_character}}
      {do
         ($player2 {game get_player_config 1})
         {$this show_char 1 {$player2 get_character}}
      }
   )
}
{new MultiSelectScreen
   multi_sel_character_screen
   (panels
      meta
      multi_sel_character_panel
      char_multi
      multi_char_outfit0
      multi_char_outfit1
      helpbar
   )
   (focus multi_sel_character_panel)
   (panel_base_name multi_char_outfit)
   (helpbar
      (default
         (
            (fret1 help_continue)
            (fret2 help_back)
            (strum help_updown)
         )
      )
   )
   (exclusive FALSE)
   (get_selection ($player) outfit1)
   (set_selection ($player $selection))
   (all_ready {ui goto_screen multi_sel_guitar_screen})
   (not_ready)
   (scroll_char_sel
      ($playerNum)
      {do
         ($config {game get_player_config $playerNum})
         {{multi_sel_character_panel
               find
               {sprintf
                  "sc%d_char_nm.lbl"
                  {'+' $playerNum 1}}} set_text
            {$config get_character}
         }
         {char_multi show_char $playerNum {$config get_character}}
         {{multi_sel_character_panel
               find
               {sprintf
                  "sc%d_char_highlight.tnm"
                  {'+' $playerNum 1}}} set_frame
            {$this get_char_idx $playerNum}
         }
      }
   )
   (enter
      {animate_forever_30fps $this multi_sel_character.view}
      {$this scroll_char_sel 0}
      {$this scroll_char_sel 1}
   )
   (TRANSITION_COMPLETE_MSG
      {char_multi set_placer
         0
         {multi_sel_character_panel find char_multi0.placer}
      }
      {char_multi set_placer
         1
         {multi_sel_character_panel find char_multi1.placer}
      }
   )
   (exit
      {char_multi set_placer 0}
      {char_multi set_placer 1}
   )
   (poll
      {{multi_sel_character_panel find msc_loading1.lbl} set_showing
         {! {char_multi is_char_loaded 0}}
      }
      {{multi_sel_character_panel find msc_loading2.lbl} set_showing
         {! {char_multi is_char_loaded 1}}
      }
   )
   (multi_char_scroll
      ($player_num)
      {if_else {== $player_num 0}
         {$this scroll_char_sel 0}
         {$this scroll_char_sel 1}
      }
   )
   (multi_char_selected
      ($player)
      {synth play_sequence button_select}
      {if_else {== $player 0}
         {do
            ($config {game get_player_config 0})
            {multi_char_outfit0 set_active TRUE}
            {if_else {> {$this num_outfits {$config get_character}} 1}
               {do
                  {{multi_char_outfit0 find outfit1.btn} set_text
                     {sprint
                        {$this get_outfit {$config get_character} 0}
                        _outfit
                     }
                  }
                  {{multi_char_outfit0 find outfit2.btn} set_text
                     {sprint
                        {$this get_outfit {$config get_character} 1}
                        _outfit
                     }
                  }
               }
               {do
                  {{multi_char_outfit0 find outfit1.btn} set_localized_text ""}
                  {{multi_char_outfit0 find outfit2.btn} set_localized_text ""}
                  {multi_char_outfit0 force_select outfit1.btn}
               }
            }
         }
         {do
            ($config {game get_player_config 1})
            {multi_char_outfit1 set_active TRUE}
            {if_else {> {$this num_outfits {$config get_character}} 1}
               {do
                  {{multi_char_outfit1 find outfit1.btn} set_text
                     {sprint
                        {$this get_outfit {$config get_character} 0}
                        _outfit
                     }
                  }
                  {{multi_char_outfit1 find outfit2.btn} set_text
                     {sprint
                        {$this get_outfit {$config get_character} 1}
                        _outfit
                     }
                  }
               }
               {do
                  {{multi_char_outfit1 find outfit1.btn} set_localized_text ""}
                  {{multi_char_outfit1 find outfit2.btn} set_localized_text ""}
                  {multi_char_outfit1 force_select outfit1.btn}
               }
            }
         }
      }
   )
   (chars_selected {ui goto_screen multi_sel_guitar_screen})
   (deselect_char
      ($player)
      {synth play_sequence button_back}
      {if_else {== $player 0}
         {multi_char_outfit0 set_active FALSE}
         {multi_char_outfit1 set_active FALSE}
      }
      {multi_sel_character_panel set_outfit $player 0}
   )
   (multi_select
      ($part $panel $selected)
      {if {! $selected}
         {do
            ($config $idx)
            {if_else {== $panel multi_char_outfit0}
               {set $idx 0}
               {set $idx 1}
            }
            {set $config {game get_player_config $idx}}
            {if {< {$this num_outfits {$config get_character}} 2}
               {synth play_sequence button_back}
               {$panel set_active FALSE}
               {multi_sel_character_panel set_frozen $idx FALSE}
            }
         }
      }
   )
}
{new UIPanel multi_sel_guitar_panel (file multi_sel_guitar.milo)}
{new GuitarDisplayPanel
   multi_guitar_display_panel
   (file guitar_display.milo)
   (force_exit TRUE)
   (config multiplayer)
}
{new GuitarSelectPanel
   multi_input_guitar_panel
   (file "")
   (multiplayer TRUE)
}
#define MULTI_SKIN_ANIM_START (0)
#define MULTI_SKIN_ANIM_END (10)
#define MULTI_SKIN_ANIM_PERIOD (0.25)
{new GHScreen
   multi_sel_guitar_screen
   (panels
      meta
      multi_sel_guitar_panel
      multi_input_guitar_panel
      multi_guitar_display_panel
      helpbar
   )
   (helpbar
      (default
         (
            (fret1 help_continue)
            (fret2 help_back)
            (strum help_updown)
         )
      )
   )
   (player0_done FALSE)
   (player1_done FALSE)
   (focus multi_input_guitar_panel)
   (enter
      {$this set player0_done {< {$this get_num_guitar_skins 0} 2}}
      {if_else {> {$this get_num_guitar_skins 0} 1}
         {{multi_sel_guitar_panel find msg_label0.lbl} set_text msg_select_guitar}
         {{multi_sel_guitar_panel find msg_label0.lbl} set_localized_text ""}
      }
      {{multi_sel_guitar_panel find sg1_player.lbl} set_text
         {sprintf
            {localize sg_player1}
            {localize {multi_input_guitar_panel get_instrument_type 0}}
         }
      }
      {$this set player1_done {< {$this get_num_guitar_skins 1} 2}}
      {if_else {> {$this get_num_guitar_skins 1} 1}
         {{multi_sel_guitar_panel find msg_label1.lbl} set_text msg_select_guitar}
         {{multi_sel_guitar_panel find msg_label1.lbl} set_localized_text ""}
      }
      {{multi_sel_guitar_panel find sg2_player.lbl} set_text
         {sprintf
            {localize sg_player2}
            {localize {multi_input_guitar_panel get_instrument_type 1}}
         }
      }
      {multi_input_guitar_panel set_skin_select 0 FALSE}
      {multi_input_guitar_panel set_skin_select 1 FALSE}
      {multi_input_guitar_panel set_quickplay 0 TRUE}
      {multi_input_guitar_panel set_quickplay 1 TRUE}
      {$this update_display}
   )
   (exit)
   (poll
      {{multi_sel_guitar_panel find sg_ready_p1.mesh} set_showing [player0_done]}
      {{multi_sel_guitar_panel find sg_ready_p2.mesh} set_showing [player1_done]}
   )
   (update_display
      {$this update_player_display 0}
      {$this update_player_display 1}
   )
   (update_player_display
      ($player)
      {$this set_guitar_label
         $player
         {multi_input_guitar_panel get_selected_skin $player}
      }
      {multi_guitar_display_panel show_guitar
         $player
         {multi_input_guitar_panel get_selected_guitar $player}
         {multi_input_guitar_panel get_selected_skin $player}
         {{multi_sel_guitar_panel loaded_dir} find
            {sprintf "guitar_multi%d.pxy" $player}
         }
         {{multi_sel_guitar_panel loaded_dir} find
            {sprintf "guitar_multi%d.filt" $player}
         }
      }
   )
   (set_guitar_label
      ($player $text)
      {if_else $player
         {{multi_sel_guitar_panel find sg2_guitar_nm.lbl} set_text $text}
         {{multi_sel_guitar_panel find sg1_guitar_nm.lbl} set_text $text}
      }
   )
   (get_num_guitar_skins
      ($playerNum)
      {multi_input_guitar_panel get_num_skins
         $playerNum
         {multi_input_guitar_panel get_selected_guitar $playerNum}
      }
   )
   (SCROLL_MSG {$this update_player_display $player_num})
   (guitar_selected
      {$this handle_select $player_num}
      {if {&& [player0_done] [player1_done]}
         {$this set_guitar 0}
         {$this set_guitar 1}
         {switch {gamecfg get mode}
            (multi_coop {ui goto_screen loading_screen})
            (multi_vs {ui goto_screen multi_vs_venue_screen})
            (multi_fo {ui goto_screen multi_fo_venue_screen})
         }
      }
      kDataUnhandled
   )
   (set_guitar
      ($player_num)
      {do
         ($config {game get_player_config $player_num})
         {$config set_guitar
            {multi_input_guitar_panel get_selected_guitar $player_num}
            {multi_input_guitar_panel get_selected_skin $player_num}
         }
      }
   )
   (handle_select
      ($player)
      {synth play_sequence button_select}
      {$this set_done $player TRUE}
      {$this update_player_display $player}
      TRUE
   )
   (is_done
      ($player)
      {if_else $player
         [player1_done]
         [player0_done]
      }
   )
   (set_done
      ($player $status)
      {if_else $player
         {set [player1_done] $status}
         {set [player0_done] $status}
      }
   )
   (BUTTON_DOWN_MSG
      {switch $button
         (kPad_Tri
            {if_else
               {&&
                  {$this is_done $player_num}
                  {> {$this get_num_guitar_skins $player_num} 1}
               }
               {do
                  {synth play_sequence button_back}
                  {$this set_done $player_num FALSE}
                  {$this update_player_display $player_num}
                  TRUE
               }
               kDataUnhandled
            }
         )
         (kPad_X {handle ($this guitar_selected $player_num)})
         (kPad_DUp
            {if_else {$this is_done $player_num}
               'TRUE;'
               kDataUnhandled
            }
         )
         (kPad_DDown
            {if_else {$this is_done $player_num}
               'TRUE;'
               kDataUnhandled
            }
         )
         kDataUnhandled
      }
   )
}
#define MULTI_SELDIFF_SCREEN_HANDLERS (
   (helpbar
      (default
         (
            (fret1 help_continue)
            (fret2 help_back)
            (strum help_updown)
         )
      )
   )
   (SELECT_MSG {ui goto_screen [next_screen]} kDataUnhandled)
)
{new GHScreen
   multi_fo_seldiff_screen
   (panels meta sel_difficulty_panel helpbar)
   (focus sel_difficulty_panel)
   MULTI_SELDIFF_SCREEN_HANDLERS
   (next_screen loading_screen)
}
#define MULTISELECT_DIFF_HANDLERS ((sel_buttons easy.btn medium.btn hard.btn expert.btn))
{new UIPanel seldiff_bg_panel (file seldiff_bg.milo)}
{new MultiSelectPanel
   seldiff0
   (file seldiff0.milo)
   (player_num 0)
   (ready_label "ready.lbl")
   (focus_button_name medium)
   MULTISELECT_DIFF_HANDLERS
}
{new MultiSelectPanel
   seldiff1
   (file seldiff1.milo)
   (player_num 1)
   (ready_label "ready.lbl")
   (focus_button_name medium)
   MULTISELECT_DIFF_HANDLERS
}
#define DUAL_DIFF_SEL_SCREEN_HANDLERS (
   (panels meta seldiff_bg_panel seldiff0 seldiff1 helpbar)
   (focus seldiff_bg_panel)
   (panel_base_name seldiff)
   (helpbar
      (default
         (
            (fret1 help_continue)
            (fret2 help_back)
            (strum help_updown)
         )
      )
   )
   (exclusive FALSE)
   (get_selection
      ($player)
      {if_else {== $player 0}
         {seldiff0 get focus_button_name}
         {seldiff1 get focus_button_name}
      }
   )
   (set_selection
      ($player $selection)
      {if_else {== $player 0}
         {seldiff0 set focus_button_name $selection}
         {seldiff1 set focus_button_name $selection}
      }
      {{game get_player_config $player} set_difficulty $selection}
   )
   (all_ready {ui goto_screen loading_screen})
)
{new MultiSelectScreen
   multi_coop_seldiff_screen
   DUAL_DIFF_SEL_SCREEN_HANDLERS
}
{new MultiSelectScreen
   multi_vs_seldiff_screen
   DUAL_DIFF_SEL_SCREEN_HANDLERS
}
#define MULTISELECT_PART_HANDLERS (
   (sel_buttons part0.btn part1.btn)
   (enter
      {part0.btn set_text
         {song_provider get_instrument {gamecfg get_song} 0}
      }
      {part1.btn set_text
         {song_provider get_instrument {gamecfg get_song} 1}
      }
   )
)
{new MultiSelectPanel
   selpart0
   (file selpart0.milo)
   (player_num 0)
   (ready_label "ready.lbl")
   MULTISELECT_PART_HANDLERS
}
{new MultiSelectPanel
   selpart1
   (file selpart1.milo)
   (player_num 1)
   (ready_label "ready.lbl")
   MULTISELECT_PART_HANDLERS
}
{new UIPanel selpart_bg_panel (file selpart_bg.milo)}
{new MultiSelectScreen
   selpart_screen
   (panels meta selpart_bg_panel selpart0 selpart1 helpbar)
   (focus selpart_bg_panel)
   (panel_base_name selpart)
   (helpbar
      (default
         (
            (fret1 help_continue)
            (fret2 help_back)
            (strum help_updown)
         )
      )
   )
   (exclusive TRUE)
   (get_selection
      ($player)
      {if_else
         {==
            {gamecfg get_track_sym $player}
            {song_provider get_instrument {gamecfg get_song} 0}
         }
         part0
         part1
      }
   )
   (set_selection
      ($player $selection)
      {if_else {== $selection part0}
         {gamecfg set_track $player 0}
         {gamecfg set_track $player 1}
      }
   )
   (all_ready {ui goto_screen multi_coop_seldiff_screen})
}
{new GHScreen
   multi_vs_venue_screen
   (next_screen multi_vs_selsong_screen)
   SEL_VENUE_SCREEN_HANDLERS
   (BUTTON_DOWN_MSG
      {cond
         ({== $button kPad_Tri}
            {$this backwards_anim}
            {synth play_sequence button_back}
            {ui goto_screen multi_screen}
         )
      }
      kDataUnhandled
   )
}
{new GHScreen
   multi_coop_venue_screen
   (next_screen multi_coop_selsong_screen)
   SEL_VENUE_SCREEN_HANDLERS
   (BUTTON_DOWN_MSG
      {cond
         ({== $button kPad_Tri}
            {$this backwards_anim}
            {synth play_sequence button_back}
            {ui goto_screen multi_screen}
         )
      }
      kDataUnhandled
   )
}
{new GHScreen
   multi_fo_venue_screen
   (next_screen multi_fo_selsong_screen)
   SEL_VENUE_SCREEN_HANDLERS
   (BUTTON_DOWN_MSG
      {cond
         ({== $button kPad_Tri}
            {$this backwards_anim}
            {synth play_sequence button_back}
            {ui goto_screen multi_screen}
         )
      }
      kDataUnhandled
   )
}
#define MULTI_SELSONG_SCREEN_HANDLERS (
   (panels meta sel_song_panel helpbar)
   (focus sel_song_panel)
   (helpbar
      (default
         (
            (fret1 help_continue)
            (fret2 help_back)
            (strum help_updown)
         )
      )
   )
   (sel_song_career FALSE)
)
{new GHScreen
   multi_coop_selsong_screen
   MULTI_SELSONG_SCREEN_HANDLERS
   (SELECT_MSG
      {gamecfg set_track 0 0}
      {gamecfg set_track 1 1}
      {if {song_provider has_instrument {gamecfg get_song} bass}
         {{game get_player_config 1} set_guitar bass_musicman bass_music_black}
      }
      {ui goto_screen multi_coop_seldiff_screen}
      kDataUnhandled
   )
}
{new GHScreen
   multi_vs_selsong_screen
   MULTI_SELSONG_SCREEN_HANDLERS
   (SELECT_MSG {ui goto_screen multi_vs_seldiff_screen} kDataUnhandled)
}
{new GHScreen
   multi_fo_selsong_screen
   MULTI_SELSONG_SCREEN_HANDLERS
   (SELECT_MSG {ui goto_screen multi_fo_seldiff_screen} kDataUnhandled)
}
#define MULTI_COMPETE_COMMON_PANEL_HANDLERS (
   (update_common_info
      {me_song_data.lbl set_localized_text
         {sprintf
            "%s %s %s"
            {game get_song_text}
            {localize mtv_made_famous}
            {game get_song_artist_text}
         }
      }
      {me_percent_data1.lbl set_localized_text
         {sprintf {localize me_percent_format} {player0 percent_hit}}
      }
      {me_percent_data2.lbl set_localized_text
         {sprintf {localize me_percent_format} {player1 percent_hit}}
      }
      {percentbar_p1.mesh set_local_scale {/ {player0 percent_hit} 100.0} 1 1}
      {percentbar_p2.mesh set_local_scale {/ {player1 percent_hit} 100.0} 1 1}
   )
)
#define MULTI_COMPETE_PANEL_HANDLERS (
   (update_winner_info
      {do
         ($winner)
         {cond
            ({>
                  {player0 score}
                  {player1 score}}
               {set $winner 0}
            )
            ({<
                  {player0 score}
                  {player1 score}}
               {set $winner 1}
            )
            ({==
                  {player0 score}
                  {player1 score}}
               {set $winner -1}
            )
         }
         {winner_photo0.mesh set_showing {== 0 $winner}}
         {winner_photo1.mesh set_showing {== 1 $winner}}
         {if_else {== $winner -1}
            {me_caption.lbl set_text me_caption_tie}
            {me_caption.lbl set_localized_text
               {sprintf
                  {localize me_caption_format}
                  {localize {{gamecfg get_player_config $winner} get_character}}
               }
            }
         }
         {me_score_data1.lbl set_localized_text {sprint {player0 score}}}
         {me_score_data2.lbl set_localized_text {sprint {player1 score}}}
         {me_streak_data1.lbl set_localized_text {sprint {player0 longest_streak}}}
         {me_streak_data2.lbl set_localized_text {sprint {player1 longest_streak}}}
         {me_stars1.lbl set_text {player0 star_rating}}
         {me_stars2.lbl set_text {player1 star_rating}}
      }
   )
)
{new EndGamePanel
   multi_compete_panel
   (file multi_compete.milo)
   MULTI_COMPETE_COMMON_PANEL_HANDLERS
   MULTI_COMPETE_PANEL_HANDLERS
   HEADLINE_HANDLERS
   (enter
      {crowd_audio turn_off}
      {script_task (delay 0.3) (script {play_sfx postshow_loop})}
      {$this update_common_info}
      {$this update_winner_info}
      {$this set_headline me_winner.lbl}
      {me_diff_data1.lbl set_text {game get_difficulty_sym 0}}
      {me_diff_data2.lbl set_text {game get_difficulty_sym 1}}
      {$this set_focus me_continue.btn}
   )
   (SELECT_MSG
      {synth play_sequence button_select}
      {switch $component
         (me_continue.btn {ui goto_screen complete_screen})
         (me_morestats.btn
            {endgame_stats_screen set prev_screen {ui current_screen}}
            {endgame_stats_screen set next_screen complete_screen}
            {ui goto_screen endgame_stats_screen}
         )
      }
      kDataUnhandled
   )
}
{new GHScreen
   multi_compete_screen
   (panels GAME_PANELS multi_compete_panel helpbar)
   (focus multi_compete_panel)
   (clear_vram TRUE)
   (allow_back FALSE)
   (animate_transition FALSE)
   (helpbar (default ((fret1 help_continue))))
   (enter
      {game set_paused TRUE}
      {world_panel set_showing FALSE}
   )
   (BUTTON_DOWN_MSG
      {if {== $button kPad_X}
         {synth play_sequence button_select}
         {ui goto_screen complete_screen}
      }
      TRUE
   )
}
{new EndGamePanel
   multi_compete_coop_panel
   (file multi_compete_coop.milo)
   MULTI_COMPETE_COMMON_PANEL_HANDLERS
   MULTI_COMPETE_PANEL_HANDLERS
   HEADLINE_HANDLERS
   ENDGAME_SELECT_HANDLERS
   (enter
      {crowd_audio turn_off}
      {script_task (delay 0.3) (script {play_sfx postshow_loop})}
      {$this update_common_info}
      {$this set_headline me_winner.lbl}
      {me_score_data.lbl set_localized_text {sprint {band score}}}
      {me_stars.lbl set_text {band star_rating}}
      {me_streak_data2.lbl set_localized_text
         {sprintf {localize me_streak_format} {band longest_streak}}
      }
      {me_diff_data1.lbl set_text {game get_difficulty_sym 0}}
      {me_diff_data2.lbl set_text {game get_difficulty_sym 1}}
      {$this set_focus me_continue.btn}
   )
   (SELECT_MSG
      {synth play_sequence button_select}
      {switch $component
         (me_continue.btn {ui goto_screen complete_screen})
         (me_morestats.btn
            {endgame_stats_screen set prev_screen {ui current_screen}}
            {set endgame_stats_screen next_screen complete_screen}
            {ui goto_screen endgame_stats_screen}
         )
      }
      kDataUnhandled
   )
}
{new GHScreen
   multi_compete_coop_screen
   (panels GAME_PANELS multi_compete_coop_panel helpbar)
   (focus multi_compete_coop_panel)
   (clear_vram TRUE)
   (allow_back FALSE)
   (animate_transition FALSE)
   (helpbar (default ((fret1 help_continue))))
   (enter
      {game set_paused TRUE}
      {world_panel set_showing FALSE}
   )
   (BUTTON_DOWN_MSG
      {if {== $button kPad_X}
         {synth play_sequence button_select}
         {ui goto_screen complete_screen}
      }
      TRUE
   )
}
{new GHPanel
   multi_compete_fo_panel
   (file multi_compete_fo.milo)
   MULTI_COMPETE_PANEL_HANDLERS
   ENDGAME_SELECT_HANDLERS
   (enter
      {crowd_audio turn_off}
      {script_task (delay 0.3) (script {play_sfx postshow_loop})}
      {$this update_winner_info}
      {me_diff_data0.lbl set_text {game get_difficulty_sym 0}}
      {me_diff_data1.lbl set_text {game get_difficulty_sym 1}}
      {$this set_focus me_continue.btn}
   )
   (SELECT_MSG
      {synth play_sequence button_select}
      {switch $component
         (me_continue.btn {ui goto_screen complete_screen})
         (me_morestats.btn
            {endgame_stats_screen set prev_screen {ui current_screen}}
            {set endgame_stats_screen next_screen complete_screen}
            {ui goto_screen endgame_stats_screen}
         )
      }
      kDataUnhandled
   )
}
{new GHScreen
   multi_compete_fo_screen
   (panels GAME_PANELS multi_compete_fo_panel helpbar)
   (focus multi_compete_fo_panel)
   (clear_vram TRUE)
   (allow_back FALSE)
   (animate_transition FALSE)
   (helpbar (default ((fret1 help_continue))))
   (enter
      {game set_paused TRUE}
      {world_panel set_showing FALSE}
   )
}