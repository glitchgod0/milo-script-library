{new TexLoadPanel
   hint_panel
   (file "../tour_world/hints/tour_world_info.milo")
   (hint_title "")
   (hint_text "")
   (hint_img "")
   (hint_name "")
   (load
      {if {!= [hint_img] ""}
         {$this add_tex [hint_img] "Image.mat"}
      }
   )
   (enter
      {Modifier_Text.grp set_showing FALSE}
      {title.lbl set_localized [hint_title]}
      {main_info.lbl set_localized [hint_text]}
      {if_else {== [hint_name] hint_rockden}
         {Nav_Info.grp set_showing TRUE}
         {Nav_Info.grp set_showing FALSE}
      }
   )
   (set_title
      ($title)
      {$this set hint_title $title}
   )
   (set_text
      ($text)
      {$this set hint_text $text}
   )
   (set_bg_type
      ($type)
      {$this set hint_type $type}
   )
   (set_img
      ($img)
      {$this set hint_img $img}
   )
   (set_hint_name
      ($name)
      {$this set hint_name $name}
   )
}
{new UIScreen
   hint_screen
   (panels hint_panel)
   (exit_screen '')
   (helpbar ((confirm helpbar_continue)))
   (BUTTON_DOWN_MSG
      {if {== $action kAction_Confirm}
         {do
            {synth play button_select}
            {if_else {== [exit_screen] ''}
               {ui pop_screen}
               {ui pop_screen [exit_screen]}
            }
         }
      }
      {$this set exit_screen ''}
   )
}
{func show_hint
   ($hint_name $exit_screen)
   {do
      ($band {tour band})
      {if_else {&& $band {! {$band have_seen_hint $hint_name}}}
         {do
            ($title {tour get_hint_title $hint_name})
            ($text {tour get_hint_text $hint_name})
            ($type {tour get_hint_type $hint_name})
            ($img {tour get_hint_img $hint_name})
            {hint_panel set_title $title}
            {hint_panel set_text $text}
            {hint_panel set_bg_type $type}
            {hint_panel set_img $img}
            {hint_panel set_hint_name $hint_name}
            {hint_screen set exit_screen $exit_screen}
            {ui push_screen hint_screen}
            {$band see_hint $hint_name}
            TRUE
         }
         FALSE
      }
   }
}
{func need_hint
   ($hint_name)
   {! {{tour band} have_seen_hint $hint_name}}
}