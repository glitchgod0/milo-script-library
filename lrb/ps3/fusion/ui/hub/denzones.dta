{new UIPanel
   denzones_panel
   (starting_focus "Lounge.btn")
   (file "denzones.milo")
   (enter
      {do
         ($focus {$this find [starting_focus]})
         {$this set_focus $focus}
         {denzones_focus $focus}
      }
   )
   (FOCUS_MSG
      {denzones_focus $new_focus}
      {synth play move_hub.cue}
   )
   (BUTTON_DOWN_MSG
      {switch $action
         (kAction_Confirm
            {synth play button_back}
            {ui goto_screen denslots_screen}
            {denslots_panel set from lounge}
         )
         (kAction_Cancel
            {synth play button_back}
            {ui goto_screen lounge_screen}
         )
         (kAction_Up
            {if_else {== {denzones_screen check_shot_ended} 1}
               kDataUnhandled
               FALSE
            }
         )
         (kAction_Down
            {if_else {== {denzones_screen check_shot_ended} 1}
               kDataUnhandled
               FALSE
            }
         )
         kDataUnhandled
      }
   )
}
{func denzones_focus
   ($focus)
   {do
      {Lounge.grp set_showing FALSE}
      {Garage.grp set_showing FALSE}
      {Office.grp set_showing FALSE}
      {Jukebox.grp set_showing FALSE}
      {Stage.grp set_showing FALSE}
      {Shop.grp set_showing FALSE}
      {Arcade.grp set_showing FALSE}
      {$focus set_showing TRUE}
      {denzones_panel set starting_focus {$focus name}}
      {switch $focus
         (Lounge.btn
            {denzones_setcam "lounge"}
            {Lounge.grp set_showing TRUE}
         )
         (Garage.btn
            {denzones_setcam "garage"}
            {Garage.grp set_showing TRUE}
         )
         (Office.btn
            {denzones_setcam "office"}
            {Office.grp set_showing TRUE}
         )
         (Jukebox.btn
            {denzones_setcam "jukebox"}
            {Jukebox.grp set_showing TRUE}
         )
         (Stage.btn
            {denzones_setcam "stage"}
            {Stage.grp set_showing TRUE}
         )
         (Shop.btn
            {denzones_setcam "shop"}
            {Shop.grp set_showing TRUE}
         )
         (Arcade.btn
            {denzones_setcam "arcade"}
            {Arcade.grp set_showing TRUE}
         )
      }
   }
}
{func denzones_setcam
   ($camname)
   {denzones_screen goto_camera $camname}
   #ifndef DAN_FAST
   {{{tour band} custom_den} set_new_zone $camname}
   #endif
}