#define kRewardCostume (0)
#define kRewardDenItem (1)
#define kRewardRockpet (2)
#define kRewardInstrument (3)
#define kRewardGigs (4)
#define kRewardEntourage (5)
{new UIPanel
   itemunlocked_panel
   (file "../tour_world/tour_world_rewards.milo")
   (unlock_active FALSE)
   (unlock_waiting FALSE)
   (exit_immediate FALSE)
   (enter
      {$this set_showing TRUE}
      {Delivered_RockDen.grp set_showing FALSE}
      {Unlocked_RockDen.grp set_showing FALSE}
      {Unlocked_RockShop.grp set_showing FALSE}
      {Unlocked_Entourage.grp set_showing FALSE}
   )
   (BUTTON_DOWN_MSG
      {switch $action
         (kAction_Confirm
            {do
               ($venue {{{tour band} venue} get_name})
               {if_else {&& {gamemode get is_tour} {tour has_unlocks_to_show}}
                  {$this do_unlock}
                  {do
                     ($gigcount {{tour band} total_gigs})
                     ($hint
                        {cond
                           ({== $gigcount 1} hint_firstsong)
                           ({== $gigcount 2} hint_secondsong)
                           ({&&
                                 {gamemode get is_tour}
                                 {gamemode get is_challenge}
                                 {!
                                    {challenge_mgr is_in_player}}
                                 {need_hint hint_arcade}}
                              hint_arcade
                           )
                           ({&&
                                 {== $venue ven_bill_ditt_construction_co}
                                 {need_hint hint_roadcrew_hired}}
                              hint_roadcrew_hired
                           )
                           ''
                        }
                     )
                     {do
                        ($specials {special_event_panel init})
                        {synth play button_select}
                        {taskmgr clear_tasks}
                        {cond
                           ({&&
                                 {{tour band}
                                    playing_special_gig}
                                 {==
                                    {{{tour band}
                                          special_gig}
                                       name}
                                    gig_ws_special_03}}
                              {special_event_panel set decline_exit_screen {$this exit_screen}}
                              {cutscene_branch_panel setup
                                 alien_signing
                                 {do
                                    {post_game_loading_panel load TRUE}
                                    {post_game_loading_panel set next_screen {$this exit_screen}}
                                    post_game_loading_screen
                                 }
                                 0
                                 FALSE
                              }
                              {if_else {!= $hint ''}
                                 {unless {show_hint $hint cutscene_branch_screen}
                                    {ui pop_screen cutscene_branch_screen}
                                 }
                                 {ui pop_screen cutscene_branch_screen}
                              }
                           )
                           ({&&
                                 {challenge_mgr has_challenge}
                                 {==
                                    {challenge_mgr get_name}
                                    challenge_asteroid}
                                 {!
                                    {{tour band}
                                       is_challenge_new
                                       {challenge_mgr get_name}}}}
                              {special_event_panel set decline_exit_screen {$this exit_screen}}
                              {cutscene_branch_panel setup
                                 story_outro
                                 {do
                                    {post_game_loading_panel load TRUE}
                                    {post_game_loading_panel set next_screen {$this exit_screen}}
                                    post_game_loading_screen
                                 }
                                 0
                                 FALSE
                              }
                              {if_else {!= $hint ''}
                                 {unless {show_hint $hint cutscene_branch_screen}
                                    {ui pop_screen cutscene_branch_screen}
                                 }
                                 {ui pop_screen cutscene_branch_screen}
                              }
                           )
                           ({&&
                                 {== $venue ven_stud_farm}
                                 {>=
                                    {{tour band}
                                       get_stars_at_venue
                                       ven_stud_farm}
                                    3}}
                              {special_event_panel set decline_exit_screen {$this exit_screen}}
                              {cutscene_branch_panel setup
                                 alien_abduction
                                 {if_else $specials
                                    special_event_screen
                                    {do
                                       {post_game_loading_panel load TRUE}
                                       {post_game_loading_panel set next_screen {$this exit_screen}}
                                       post_game_loading_screen
                                    }
                                 }
                                 0
                                 FALSE
                              }
                              {if_else {!= $hint ''}
                                 {unless {show_hint $hint cutscene_branch_screen}
                                    {ui pop_screen cutscene_branch_screen}
                                 }
                                 {ui pop_screen cutscene_branch_screen}
                              }
                           )
                           ({&&
                                 {== $venue ven_volcanic_rock}
                                 {>=
                                    {{tour band}
                                       get_stars_at_venue
                                       ven_volcanic_rock}
                                    3}}
                              {special_event_panel set decline_exit_screen {$this exit_screen}}
                              {cutscene_branch_panel setup
                                 dino_chase
                                 {if_else $specials
                                    special_event_screen
                                    {do
                                       {post_game_loading_panel load TRUE}
                                       {post_game_loading_panel set next_screen {$this exit_screen}}
                                       post_game_loading_screen
                                    }
                                 }
                                 0
                                 FALSE
                              }
                              {if_else {!= $hint ''}
                                 {unless {show_hint $hint cutscene_branch_screen}
                                    {ui pop_screen cutscene_branch_screen}
                                 }
                                 {ui pop_screen cutscene_branch_screen}
                              }
                           )
                           (TRUE
                              {if_else $specials
                                 {do
                                    {special_event_panel set decline_exit_screen {$this exit_screen}}
                                    {if_else {!= $hint ''}
                                       {unless {show_hint $hint special_event_screen}
                                          {ui pop_screen special_event_screen}
                                       }
                                       {ui pop_screen special_event_screen}
                                    }
                                 }
                                 {if_else {!= $hint ''}
                                    {do
                                       {if_else {show_hint $hint ''}
                                          {do
                                             {$this set_showing FALSE}
                                             {$this set exit_immediate TRUE}
                                          }
                                          {$this goto_hub}
                                       }
                                    }
                                    {$this goto_hub}
                                 }
                              }
                           )
                        }
                     }
                  }
               }
            }
         )
         kDataUnhandled
      }
   )
   (exit_screen
      {if_else {&& {gamemode get is_challenge} {challenge_mgr is_in_player}}
         arcade_screen
         {if_else {{tour band} vehicle}
            garage_gigscreen
            garage_vehiclescreen
         }
      }
   )
   (goto_hub
      {post_game_loading_panel load TRUE}
      {post_game_loading_panel set next_screen {$this exit_screen}}
      {ui pop_screen post_game_loading_screen}
      {fx fade_out}
   )
   (do_unlock
      {do
         ($unlock {tour pop_unlock})
         ($count {$unlock get_count})
         {if_else {$unlock has_art}
            {Rewards_Image.pic set tex_file {$unlock art_path}}
            {Rewards_Image.pic set tex_file "ui/image/blank_album_art_keep.png"}
         }
         {if_else {> $count 0}
            {InfoText.lbl set
               text_token
               {sprintf {localize {$unlock description}} $count}
            }
            {InfoText.lbl set text_token {$unlock description}}
         }
         {Delivered_RockDen.grp set_showing FALSE}
         {Unlocked_RockDen.grp set_showing FALSE}
         {Unlocked_RockShop.grp set_showing FALSE}
         {Unlocked_Entourage.grp set_showing FALSE}
         {switch {$unlock type}
            (
               (kRewardCostume kRewardInstrument)
               {Unlocked_RockShop.grp set_showing TRUE}
            )
            (kRewardRockpet {Delivered_RockDen.grp set_showing TRUE})
            (kRewardDenItem {Unlocked_RockDen.grp set_showing TRUE})
            (kRewardEntourage {Unlocked_Entourage.grp set_showing TRUE})
         }
         {if_else {tour has_unlocks_to_show}
            {helpbar set_config ((confirm helpbar_ok))}
            {helpbar set_config ((confirm tour_hb_returntomap))}
         }
         {synth play unlock_something.cue}
         {Rewards_Appear.trig trigger}
      }
   )
   (TRANSITION_COMPLETE_MSG
      {if {!= {ui bottom_screen} {ui current_screen}}
         {{ui bottom_screen} set_showing FALSE}
      }
      {if_else {tour has_unlocks_to_show}
         {$this do_unlock}
         {if [exit_immediate]
            {$this goto_hub}
            {$this set exit_immediate FALSE}
         }
      }
   )
}
{new BandScreen
   itemunlocked_screen
   (panels endgame_background_animated_panel itemunlocked_panel)
   (focus itemunlocked_panel)
   (helpbar ((confirm helpbar_ok)))
   (enter {endgame_background_animated_panel show})
}