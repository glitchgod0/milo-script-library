(EventTrigger
   (types
      (particle_trigger
         (editor
            (systems
               (array
                  object
                  (class Object)
                  (list {$this launcher_list})
               )
            )
         )
         (systems ())
         (launcher_list
            {do
               ($array {array 0})
               {{$this dir} iterate
                  Object
                  $obj
                  {if {== {$obj get_type} particle_launcher}
                     {push_back $array $obj}
                  }
               }
               $array
            }
         )
         (trigger
            {foreach $elem [systems]
               {$elem launch_particles}
            }
         )
      )
      (chars_unhook
         (editor
            (scene_parent
               object
               (class Object)
               (help "The new parent for the characters")
            )
         )
         (scene_parent {object $world})
         (trigger
            {do
               ($player_bass {find_obj $world player_bass})
               ($player_guitar {find_obj $world player_guitar})
               ($player_vocal {find_obj $world player_vocal})
               ($player_drum {find_obj $world player_drum})
               {$player_bass set_trans_parent [scene_parent]}
               {$player_guitar set_trans_parent [scene_parent]}
               {$player_vocal set_trans_parent [scene_parent]}
               {$player_drum set_trans_parent [scene_parent]}
            }
         )
      )
      (chars_rehook_item
         (editor
            (scene_parent
               object
               (class Object)
               (help "New parent for the item")
            )
            (scene_item
               object
               (class Object)
               (help "Item to reparent")
            )
            (track_env object (class Environ))
         )
         (scene_parent {object $world})
         (scene_item {object $world})
         (track_env '')
         (trigger
            {[scene_item] set_trans_parent [scene_parent]}
            {if {!= [track_env] ''}
               {[scene_item] set environ [track_env]}
            }
         )
      )
      (chars_hook
         (trigger
            {do
               ($player_bass_old {find_obj $world player_bass_old})
               ($player_guitar_old {find_obj $world player_guitar_old})
               ($player_vocal_old {find_obj $world player_vocal_old})
               ($player_drum_old {find_obj $world player_drum_old})
               ($player_bass {find_obj $world player_bass})
               ($player_guitar {find_obj $world player_guitar})
               ($player_vocal {find_obj $world player_vocal})
               ($player_drum {find_obj $world player_drum})
               {$player_bass set_trans_parent {$player_bass_old trans_parent}}
               {$player_guitar set_trans_parent {$player_guitar_old trans_parent}}
               {$player_vocal set_trans_parent {$player_vocal_old trans_parent}}
               {$player_drum set_trans_parent {$player_drum_old trans_parent}}
            }
         )
      )
   )
)