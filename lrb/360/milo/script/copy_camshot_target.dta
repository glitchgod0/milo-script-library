{milo dialog
   "Copy CamShot Target"
   (editor
      (cameras
         (array object (class BandCamShot))
         (help "Cameras to apply copied target")
      )
      (from
         object
         (class BandCamShot)
         (help "Cam Shot to copy the target from")
      )
      (index int (help "Index of the target on the camshot"))
      (Ok script (script {$this Ok}))
   )
   (modal FALSE)
   (from "")
   (cameras ())
   (index 0)
   (COPY_PROP_LIST
      (target
         anim
         teleport
         return
         freeze
         freeze_delay
         fast_forward
         forward_event
         self_shadow
         spot_cutout
         floor_shadow
         target_ik
         world_ik
         world_prop
         target_prop
         env_override
      )
   )
   (COPY_PROP_TRANSFORM (pitch roll yaw x_scale y_scale z_scale x y z))
   (enter
      {resize [cameras] 0}
      {milo iterate_lister_highlit $highlit {push_back [cameras] $highlit}}
   )
   (Ok
      {if {!= [from] ""}
         {do
            ($targets {[from] get_array targets})
            {if {&& {< [index] {size $targets}} {>= [index] 0}}
               {foreach $highlit [cameras]
                  {if {&& {== {$highlit class_name} BandCamShot} {!= $highlit [from]}}
                     {do
                        ($to_index {size {$highlit get_array targets}})
                        {$highlit insert (targets $to_index) {elem $targets [index]}}
                        {foreach $prop_name [COPY_PROP_LIST]
                           {$highlit set
                              (targets $to_index $prop_name)
                              {[from] get (targets [index] $prop_name)}
                           }
                        }
                        {foreach $prop_name [COPY_PROP_TRANSFORM]
                           {$highlit set
                              (targets $to_index to $prop_name)
                              {[from] get
                                 (targets [index] to $prop_name)
                              }
                           }
                        }
                     }
                  }
               }
            }
         }
         {milo update_objects}
      }
   )
}