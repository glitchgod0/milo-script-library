{milo dialog
   "Set CamShot Target Flags"
   (editor
      (cam_shots
         (array object (class BandCamShot))
         (help "List of the BandCamShots to apply flags to")
      )
      (teleport symbol (list (none ON OFF)))
      (teleport_hold symbol (list (none ON OFF)))
      (return symbol (list (none ON OFF)))
      (self_shadow symbol (list (none ON OFF)))
      (spot_cutout symbol (list (none ON OFF)))
      (floor_shadow symbol (list (none ON OFF)))
      (freeze symbol (list (none ON OFF)))
      (Ok script (script {$this Ok}))
   )
   (cam_shots ())
   (teleport none)
   (teleport_hold none)
   (return none)
   (self_shadow none)
   (spot_cutout none)
   (floor_shadow none)
   (freeze none)
   (modal FALSE)
   (enter
      {resize [cam_shots] 0}
      {milo iterate_lister_highlit
         $highlit
         {if {$highlit is_a BandCamShot}
            {push_back [cam_shots] $highlit}
         }
      }
   )
   (Ok
      {foreach $highlit [cam_shots]
         {do
            ($target_count {size {$highlit get_array targets}})
            {foreach_int $i 0 $target_count
               {$this set_property $highlit $i teleport}
               {$this set_property $highlit $i teleport_hold}
               {$this set_property $highlit $i return}
               {$this set_property $highlit $i self_shadow}
               {$this set_property $highlit $i spot_cutout}
               {$this set_property $highlit $i floor_shadow}
               {$this set_property $highlit $i freeze}
            }
         }
      }
      {milo update_objects}
   )
   (set_property
      ($obj $index $flag)
      {set $value {$this get $flag}}
      {if {!= $value none}
         {if_else {== $value ON}
            {$obj set (targets $index $flag) TRUE}
            {$obj set (targets $index $flag) FALSE}
         }
      }
   )
}