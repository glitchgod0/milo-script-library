#define FLAG_LIST ((none ON OFF))
{milo dialog
   "Filter and Rename Camshots"
   (editor
      (categories (array symbol (list CAMSHOT_CATEGORIES)))
      (BCSBDistNull symbol (list FLAG_LIST))
      (BCSBDistNear symbol (list FLAG_LIST))
      (BCSBDistFar symbol (list FLAG_LIST))
      (BCSBDistCloseup symbol (list FLAG_LIST))
      (BCSBDistBehind symbol (list FLAG_LIST))
      (BCSBFacingNull symbol (list FLAG_LIST))
      (BCSBFacingLeft symbol (list FLAG_LIST))
      (BCSBFacingRight symbol (list FLAG_LIST))
      (ValidMale symbol (list FLAG_LIST))
      (ValidFemale symbol (list FLAG_LIST))
      (ValidBanger symbol (list FLAG_LIST))
      (ValidDramatic symbol (list FLAG_LIST))
      (ValidRocker symbol (list FLAG_LIST))
      (ValidSpazz symbol (list FLAG_LIST))
      (bass_focus symbol (list FLAG_LIST))
      (drums_focus symbol (list FLAG_LIST))
      (guitar_focus symbol (list FLAG_LIST))
      (vocal_focus symbol (list FLAG_LIST))
      (filter script (script {$this filter}))
      (filtered_shots
         (array object (class BandCamShot))
         (help "List of the shots to rename.")
      )
      (prefix string)
      (rename script (script {$this rename}))
   )
   (filtered_shots ())
   (categories ())
   (BCSBDistNull none)
   (BCSBDistNear none)
   (BCSBDistFar none)
   (BCSBDistCloseup none)
   (BCSBDistBehind none)
   (BCSBFacingNull none)
   (BCSBFacingLeft none)
   (BCSBFacingRight none)
   (ValidMale none)
   (ValidFemale none)
   (ValidBanger none)
   (ValidDramatic none)
   (ValidRocker none)
   (ValidSpazz none)
   (bass_focus none)
   (drums_focus none)
   (guitar_focus none)
   (vocal_focus none)
   (prefix "")
   (modal FALSE)
   (enter)
   (filter
      {resize [filtered_shots] 0}
      {{milo cur_dir} iterate_self
         BandCamShot
         $cam
         {do
            ($match_cat {== {size [categories]} 0})
            ($match_flags TRUE)
            {foreach $cat [categories]
               {if {== {$cam get category} $cat}
                  {set $match_cat TRUE}
               }
            }
            {if $match_cat
               {&= $match_flags
                  {$this check_flag $cam kBCSBDistNull [BCSBDistNull]}
               }
               {&= $match_flags
                  {$this check_flag $cam kBCSBDistNear [BCSBDistNear]}
               }
               {&= $match_flags
                  {$this check_flag $cam kBCSBDistFar [BCSBDistFar]}
               }
               {&= $match_flags
                  {$this check_flag $cam kBCSBDistCloseup [BCSBDistCloseup]}
               }
               {&= $match_flags
                  {$this check_flag $cam kBCSBDistBehind [BCSBDistBehind]}
               }
               {&= $match_flags
                  {$this check_flag $cam kBCSBFacingNull [BCSBFacingNull]}
               }
               {&= $match_flags
                  {$this check_flag $cam kBCSBFacingLeft [BCSBFacingLeft]}
               }
               {&= $match_flags
                  {$this check_flag $cam kBCSBFacingRight [BCSBFacingRight]}
               }
               {&= $match_flags
                  {$this check_flag $cam kValidMale [ValidMale]}
               }
               {&= $match_flags
                  {$this check_flag $cam kValidFemale [ValidFemale]}
               }
               {&= $match_flags
                  {$this check_flag $cam kValidBanger [ValidBanger]}
               }
               {&= $match_flags
                  {$this check_flag $cam kValidDramatic [ValidDramatic]}
               }
               {&= $match_flags
                  {$this check_flag $cam kValidRocker [ValidRocker]}
               }
               {&= $match_flags
                  {$this check_flag $cam ValidSpazz [ValidSpazz]}
               }
               {&= $match_flags
                  {$this check_prop $cam bass_focus [bass_focus]}
               }
               {&= $match_flags
                  {$this check_prop $cam drums_focus [drums_focus]}
               }
               {&= $match_flags
                  {$this check_prop $cam guitar_focus [guitar_focus]}
               }
               {&= $match_flags
                  {$this check_prop $cam vocal_focus [vocal_focus]}
               }
               {if $match_flags
                  {push_back [filtered_shots] $cam}
               }
            }
         }
      }
      {milo update_objects}
   )
   (check_flag
      ($obj $mask $state)
      {if_else {== $state none}
         TRUE
         {do
            ($flag_state {== {& {$obj get flags} $mask} $mask})
            {if_else $flag_state
               {== $state ON}
               {== $state OFF}
            }
         }
      }
   )
   (check_prop
      ($obj $prop_name $state)
      {if_else {== $state none}
         TRUE
         {if_else {$obj get $prop_name}
            {== $state ON}
            {== $state OFF}
         }
      }
   )
   (rename
      {do
         ($i 0)
         ($new_name "")
         {foreach $cam [filtered_shots]
            {set $new_name
               {sprintf "%s%02d.shot" [prefix] $i}
            }
            {$cam set_name $new_name}
            {'++' $i}
         }
      }
   )
}