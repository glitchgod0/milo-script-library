{milo dialog
   "Copy Objects With Prefix"
   (editor
      (objects
         (array object (class Object))
         (help "Objects to copy and rename")
      )
      (prefix string)
      (Copy script (script {$this Copy}))
   )
   (modal FALSE)
   (objects ())
   (prefix "prefix_")
   (enter
      {milo iterate_lister_highlit $highlit {push_back [objects] $highlit}}
   )
   (Copy
      {do
         ($copied)
         ($name)
         ($preprefix)
         {foreach $elem [objects]
            {set $name {$elem name}}
            {set $preprefix ""}
            {if {$elem is_a BandCamShot}
               {if {startswith $name "coop_"}
                  {set $preprefix "coop_"}
                  {search_replace $name $preprefix "" $name}
               }
               {if {startswith $name "h2h_"}
                  {set $preprefix "h2h_"}
                  {search_replace {$elem name} $preprefix "" $name}
               }
            }
            {set $name
               {sprint $preprefix [prefix] $name}
            }
            {if {! {{$elem dir} exists $name}}
               {set $copied {new {$elem class_name} $name}}
               {$copied set_name $name {milo cur_dir}}
               {$copied copy $elem kCopyDeep}
            }
         }
      }
      {milo update_objects}
   )
}