#define kGameTimeDen (0)
#define kGameTimeShop (1)
#define kGameTimeOffice (2)
#define kGameTimeTour (3)
{new UIPanel
   hub_menu
   (focus_on_enter "BandButton_02Garage.btn")
   (file "hub_menu.milo")
   (show_help 1)
   (restart_cam 1)
   (load_img)
   (enter
      {do
         ($focus {$this find [focus_on_enter]})
         {if_else {== [restart_cam] 1}
            {hub_screen start_camera}
            {$this set restart_cam 1}
         }
         {$this set_focus $focus}
         {hub_focus $focus}
         #ifndef DAN_FAST
         {{{tour band} custom_den} show_slot_markers 0}
         {{jukebox_panel find jukebox.emit} set_occluded 0}
         #endif
         {gamemode set_practice 0}
         {input_mgr set_user ''}
         {input_mgr set_limit kLimitUILeader}
         {gamemode set is_tour_quickplay FALSE}
         {{tour band} start_game_timer kGameTimeDen}
         {if {> {{tour band} stars} 30}
            {$this set show_help 0}
         }
         {if {== [show_help] 1}
            {HelpBar_On.trig trigger}
         }
         #ifndef DAN_FAST
         {if {'||' {> {{tour band} fans} 0} {> {{tour band} stars} 0}}
            {BandButton_02Garage.btn set text_token tour_gigguide_continue}
         }
         {{mixer_panel find LightPulse.trig} trigger}
         #endif
      }
   )
   (exit {input_mgr set_limit kLimitUILeaderNoSwap})
   (FOCUS_MSG
      {hub_focus $new_focus}
      {synth play move_hub.cue}
   )
   (SELECT_MSG
      {do
         {$this set focus_on_enter {$this focus_name}}
         {switch $component
            (BandButton_01Lounge.btn
               {ui goto_screen tv_screen_avec_hub}
               {synth play button_select}
            )
            (BandButton_02Garage.btn
               {generic_loading_panel set
                  img
                  "fusion/ui/textures/load_screens/Office_Loader_Vehicle_nomip_keep.png"
               }
               {if_else {special_event_panel init}
                  {do
                     {special_event_panel set decline_exit_screen loading_garage_screen}
                     {ui goto_screen special_event_screen}
                  }
                  {do
                     {fx fade_out}
                     {ui goto_screen loading_garage_screen}
                     {garage_vehiclepanel set trigger_anims TRUE}
                  }
               }
               {synth play button_select}
               {{tour band} start_game_timer kGameTimeTour}
            )
            (BandButton_03Office.btn
               {generic_loading_panel set
                  img
                  "fusion/ui/textures/load_screens/Office_Loader_Manager_nomip_keep.png"
               }
               {ui goto_screen loading_office_screen}
               {synth play button_select}
               {{tour band} start_game_timer kGameTimeOffice}
               {fx fade_out}
            )
            (BandButton_06Jukebox.btn
               {gamemode set is_tour_quickplay TRUE}
               {ui goto_screen loading_jukebox_screen}
               {synth play button_select}
               {fx fade_out}
            )
            (BandButton_04Stage.btn
               {stage_panel set training_user {user_mgr get_band_user $player_num}}
               {ui goto_screen stage_screen}
               {synth play button_select}
            )
            (BandButton_05Shop.btn
               {do
                  ($user {user_mgr get_band_user $player_num})
                  {generic_loading_panel set
                     img
                     "fusion/ui/textures/load_screens/Office_Loader_Entourage_nomip_keep.png"
                  }
                  {input_mgr set_limit kLimitNone}
                  {input_mgr set_user $user}
                  {if_else
                     {&&
                        {== {{tour band} get_owner} {profile_mgr get_profile $user}}
                        {'||'
                           {{tour band} is_manager_hired}
                           {> {{tour band} get_num_entourage_hired 2} 0}
                        }
                     }
                     {do
                        {shop_char_panel set_edit_group 3}
                        {ui goto_screen loading_shopgroup_screen}
                     }
                     {do
                        {shop_char_panel set_edit_group 0}
                        {ui goto_screen loading_shop_screen}
                     }
                  }
                  {{tour band} start_game_timer kGameTimeShop}
                  {fx fade_out}
                  {synth play button_select}
               }
            )
            (BandButton_07Arcade.btn
               {do
                  {ui goto_screen arcade_screen_avec_hub}
                  {synth play button_select}
               }
            )
         }
      }
   )
   (BUTTON_DOWN_MSG
      {switch $action
         (kAction_ViewModify
            {if {== {hub_screen check_shot_ended} 1}
               {do
                  {synth play button_focus}
                  {if_else {== [show_help] 0}
                     {do
                        {HelpBar_On.trig trigger}
                        {$this set show_help 1}
                     }
                     {do
                        {HelpBar_Off.trig trigger}
                        {$this set show_help 0}
                     }
                  }
               }
            }
         )
         (
            #ifdef HX_WII
            (kAction_Option kAction_Option1)
            #else
            kAction_Option
            #endif
            {if {== {hub_screen check_shot_ended} 1}
               {do
                  {ui goto_screen denslots_screen}
                  {denslots_panel set from hub}
                  {synth play button_select}
                  {$this set focus_on_enter {$this focus_name}}
                  {hub_hide_menus}
                  {08Customisation.grp set_showing TRUE}
               }
            }
         )
         (
            (kAction_Up
               kAction_Down
               kAction_Left
               kAction_Right
               kAction_Confirm
               kAction_Cancel
            )
            {if_else {== {hub_screen check_shot_ended} 1}
               kDataUnhandled
               FALSE
            }
         )
         kDataUnhandled
      }
   )
}
{func hub_hide_menus
   {do
      {01Lounge.grp set_showing FALSE}
      {02Garage.grp set_showing FALSE}
      {03Office.grp set_showing FALSE}
      {06Jukebox.grp set_showing FALSE}
      {04Stage.grp set_showing FALSE}
      {05Shop.grp set_showing FALSE}
      {07Arcade.grp set_showing FALSE}
      {08Customisation.grp set_showing FALSE}
   }
}
{func hub_focus
   ($focus)
   {do
      {hub_hide_menus}
      {switch $focus
         (BandButton_01Lounge.btn
            {hub_setcam "lounge"}
            {01Lounge.grp set_showing TRUE}
         )
         (BandButton_02Garage.btn
            {hub_setcam "garage"}
            {02Garage.grp set_showing TRUE}
         )
         (BandButton_03Office.btn
            {hub_setcam "office"}
            {03Office.grp set_showing TRUE}
         )
         (BandButton_06Jukebox.btn
            {hub_setcam "jukebox"}
            {06Jukebox.grp set_showing TRUE}
         )
         (BandButton_04Stage.btn
            {hub_setcam "stage"}
            {04Stage.grp set_showing TRUE}
         )
         (BandButton_05Shop.btn
            {hub_setcam "shop"}
            {05Shop.grp set_showing TRUE}
         )
         (BandButton_07Arcade.btn
            {hub_setcam "arcade"}
            {07Arcade.grp set_showing TRUE}
         )
      }
   }
}
{func hub_setcam
   ($camname)
   {hub_screen goto_camera $camname}
   {denslots_panel set zone $camname}
   #ifndef DAN_FAST
   {{{tour band} custom_den} set_new_zone $camname}
   #endif
}