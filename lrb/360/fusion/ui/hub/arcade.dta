{new BandScreen
   nochallenges_screen
   (panels dialog_panel)
   (focus dialog_panel)
   (enter {dialog_panel set_ok {localize unlock_challenges}})
   (SELECT_MSG {ui pop_screen})
}
{new UIPanel
   arcade_panel
   (file "challenges_MainSel.milo")
   (any_set 0)
   (mapping
      (
         (01_WreckNRoll.btn
            ChallengeLocked_01.grp
            challenge_wrecknroll
            "fusion/ui/textures/Venue_Screenshots/Chal_WreckNRoll_nomip_keep.png"
         )
         (02_Ghostbusters.btn
            ChallengeLocked_02.grp
            challenge_ghostbusters
            "fusion/ui/textures/Venue_Screenshots/Chal_Ghostbusters_nomip_keep.png"
         )
         (03_OctopusAttack.btn
            ChallengeLocked_03.grp
            challenge_rocktopus
            "fusion/ui/textures/Venue_Screenshots/Chal_OctoAttack_nomip_keep.png"
         )
         (05_FarmRain.btn
            ChallengeLocked_05.grp
            challenge_farmrain
            "fusion/ui/textures/Venue_Screenshots/Chal_FarmRain_nomip_keep.png"
         )
         (06_DinoChase.btn
            ChallengeLocked_06.grp
            challenge_dinochase
            "fusion/ui/textures/Venue_Screenshots/Chal_DinoChase_nomip_keep.png"
         )
         (07_Icebreaker.btn
            ChallengeLocked_07.grp
            challenge_icebreaker
            "fusion/ui/textures/Venue_Screenshots/Chal_IceBreaker_nomip_keep.png"
         )
         (08_Castle.btn
            ChallengeLocked_08.grp
            challenge_castle
            "fusion/ui/textures/Venue_Screenshots/Chal_Castle_nomip_keep.png"
         )
         (09_UfoBreakdown.btn
            ChallengeLocked_09.grp
            challenge_ufobreakdown
            "fusion/ui/textures/Venue_Screenshots/Chal_UFOBreakdown_nomip_keep.png"
         )
         (10_Asteroid.btn
            ChallengeLocked_10.grp
            challenge_asteroid
            "fusion/ui/textures/Venue_Screenshots/Chal_Asteroid_nomip_keep.png"
         )
      )
   )
   (enter
      {$this set_status 01_WreckNRoll.btn}
      {$this set_status 02_Ghostbusters.btn}
      {$this set_status 03_OctopusAttack.btn}
      {$this set_status 05_FarmRain.btn}
      {$this set_status 06_DinoChase.btn}
      {$this set_status 07_Icebreaker.btn}
      {$this set_status 08_Castle.btn}
      {$this set_status 09_UfoBreakdown.btn}
      {$this set_status 10_Asteroid.btn}
      {$this set_focus 01_WreckNRoll.btn}
      {if {== [any_set] 0}
         {ui push_screen nochallenges_screen}
      }
   )
   (BUTTON_DOWN_MSG
      {switch $action
         (kAction_Confirm
            {do
               ($challenge_mapping {find_exists [mapping] {symbol {$this focus_name}}})
               ($selected {elem $challenge_mapping 2})
               {if_else {{tour band} is_challenge_unlocked $selected}
                  {do
                     {synth play button_select}
                     {challenge_mgr set_current_challenge $selected}
                     {challenge_mgr set_in_player TRUE}
                     {tour set_venue {{tour get_challenge_gig $selected} get_venue}}
                     {challenge_panel set quick TRUE}
                     {qp_difficulty_screen set cancel_screen loading_arcade_screen}
                     {ui goto_screen challenge_screen}
                  }
                  {synth play button_error}
               }
            }
         )
         (kAction_Cancel
            {synth play button_back}
            {if_else $hub_loaded
               {ui goto_screen hub_screen}
               {ui goto_screen loading_hub_screen}
            }
         )
         (
            (kAction_Up kAction_Down)
            {synth play button_focus}
            kDataUnhandled
         )
         (TRUE kDataUnhandled)
      }
   )
   (FOCUS_MSG
      {do
         {if {find_exists [mapping] {symbol {$new_focus name}}}
            {do
               ($challenge_mapping {find_exists [mapping] {symbol {$new_focus name}}})
               ($selected {elem $challenge_mapping 3})
               {if_else {== {{tour band} is_challenge_unlocked {elem $challenge_mapping 2}} TRUE}
                  {do
                     {Challenge_Texture.mesh set_showing TRUE}
                     {UIPicture.pic set tex_file $selected}
                  }
                  {Challenge_Texture.mesh set_showing FALSE}
               }
            }
         }
         kDataUnhandled
      }
   )
   (set_status
      ($button)
      {do
         ($challenge_mapping {find_exists [mapping] {symbol $button}})
         ($lockgrp {elem $challenge_mapping 1})
         ($name {elem $challenge_mapping 2})
         {if_else {== {{tour band} is_challenge_unlocked $name} TRUE}
            {do
               {$this set any_set 1}
               {$this enable $button}
               {$lockgrp set_showing FALSE}
            }
            {do
               {$this disable $button}
               {$lockgrp set_showing TRUE}
            }
         }
      }
   )
}
{new UIScreen
   arcade_screen
   (panels
      meta
      arcade_panel
      #ifndef DAN_FAST
      hubentourage_panel
      #endif
   )
   (focus arcade_panel)
   (helpbar
      (
         (cancel helpbar_back)
         (confirm helpbar_confirm)
      )
   )
}
{new UIScreen
   arcade_screen_avec_hub
   (panels
      meta
      #ifndef DAN_FAST
      mixer_panel
      hubentourage_panel
      #endif
      arcade_panel
   )
   (focus arcade_panel)
   (helpbar
      (
         (cancel helpbar_back)
         (confirm helpbar_confirm)
      )
   )
}