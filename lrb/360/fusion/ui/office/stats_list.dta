{new UIPanel
   stats_list_panel
   (file "stats_list.milo")
   (focus Career_Stats.lst)
   (enter {$this update_data})
   (FOCUS_MSG DO_FOCUSCLICK kDataUnhandled)
   (SCROLL_MSG {synth play button_focus} kDataUnhandled)
   (update_data
      {do
         ($band {tour band})
         {$this update_header}
         {{tour band} start_game_timer kGameTimeOffice}
         {{tour stats_provider} init_from_band $band}
         {Career_Stats.lst set_provider {tour stats_provider}}
      }
   )
   (update_header
      {do
         ($band {tour band})
         {bandname.btn set_localized {$band band_name}}
         {career_score.lbl set_localized {sprintf {fmt_score {$band studs}}}}
         {fans.lbl set_localized {sprintf {fmt_score {$band fans}}}}
         {do
            ($stars {$band stars})
            {if_else {== 0 $stars}
               {rating_stars.lbl set text_token "0"}
               {rating_stars.lbl set text_token {fmt_score $stars}}
            }
         }
         {rating.pdiff set num_players {$band roster_size}}
         {rating.pdiff set diff {$band difficulty_rating}}
      }
   )
   (BUTTON_DOWN_MSG
      {if_else {== $action kAction_Confirm}
         FALSE
         kDataUnhandled
      }
   )
}
{new MainScreen
   stats_list_screen
   (panels
      meta
      #ifndef DAN_FAST
      mixeroffice_panel
      hubentourage_panel
      #endif
      stats_list_panel
   )
   (focus stats_list_panel)
   (back
      {do
         {synth play button_back}
         office_screen
      }
   )
   (helpbar ((cancel helpbar_back)))
}