{new HamPanel
   leaderboards_panel
   (file "leaderboards.milo")
   (focus right_hand.hnl)
   (tertiary_token leaderboards_view_friends)
   (back_token back)
   (enter
      {start_loading.flow activate}
      {set [tertiary_token] leaderboards_view_friends}
      {leaderboards set_mode 2}
      {leaderboards download_scores {gamedata get song}}
      {right_hand.hnl set_provider leaderboards}
      {right_hand.hnl set_showing FALSE}
   )
   (exit)
   (leaderboards_loaded
      {finish_loading.flow activate}
      {right_hand.hnl set_showing TRUE}
      {right_hand.hnl refresh}
      {$this set_up {leaderboards num_scores} FALSE}
   )
   (leaderboards_failed
      {finish_loading.flow activate}
      {$this set_up 0 TRUE}
   )
   (set_focus
      ($focus_index)
      {if {!= $focus_index -1}
         {right_hand.hnl scroll_to_index 0 0}
         {right_hand.hnl scroll_to_index $focus_index -1}
      }
   )
   (set_up
      ($num_scores $failure)
      {if {== $num_scores 0}
         {do
            {if_else $failure
               {must_sign_in.lbl set_showing TRUE}
               {switch {leaderboards get_mode}
                  (0 {no_any_scores.lbl set_showing TRUE})
                  (2 {no_friend_scores.lbl set_showing TRUE})
                  (1 {no_self_score.lbl set_showing TRUE})
               }
            }
         }
      }
   )
   (NAV_SELECT_MSG
      {switch $component
         (right_hand.hnl
            {leaderboards show_gamercard $index {profile_mgr get_active_profile}}
            skip_select_anim
         )
         (left_hand.hnl
            {switch $name
               (back {ui go_back_screen {{ui current_screen} get back}})
               (leaderboards_view_top
                  {set [tertiary_token] leaderboards_view_friends}
                  {helpbar resync}
                  {leaderboards set_mode 2}
                  {$this update_sort}
               )
               (leaderboards_view_friends
                  {set [tertiary_token] leaderboards_view_self}
                  {helpbar resync}
                  {leaderboards set_mode 1}
                  {$this update_sort}
               )
               (leaderboards_view_self
                  {set [tertiary_token] leaderboards_view_top}
                  {helpbar resync}
                  {leaderboards set_mode 0}
                  {$this update_sort}
               )
            }
         )
      }
   )
   (update_sort
      {right_hand.hnl set_showing FALSE}
      {start_loading.flow activate}
      {leaderboards download_scores {gamedata get song}}
   )
}
{new HamScreen
   leaderboards_screen
   (panels meta background_panel leaderboards_panel)
   (focus leaderboards_panel)
   (back leaderboard_select_screen)
}