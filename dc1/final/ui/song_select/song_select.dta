{new SongSelectPanel
   song_select_panel
   (file "song_select.milo")
   (focus song_button_01.sbt)
   (skip_controller_focus_on_enter TRUE)
   (highlight_button "")
   (details_button "")
   (top_index 0)
   (focus_song none)
   (show_back_button TRUE)
   (storage_changed_error FALSE)
   (joypad
      (hold_ms 100)
      (repeat_ms 80)
   )
   (song_data_mounted
      ($song)
      {$this refresh_album_art $song}
   )
   (load
      {platform_mgr add_sink $this (storage_changed)}
      {ui_event_mgr add_sink $this (event_dialog_dismiss)}
   )
   (enter
      {song_offer_provider enter}
      {right_hand.hbs set_provider song_offer_provider}
      {right_hand.hbs scroll_to_index [top_index]}
   )
   (exit
      {meta music_start}
      {song_offer_provider stop_preview}
      {song_offer_provider exit}
      {set [top_index] {right_hand.hbs get_top_index}}
   )
   (unload
      {song_offer_provider unload}
      {ui_event_mgr remove_sink $this event_dialog_dismiss}
      {platform_mgr remove_sink $this storage_changed}
   )
   (TRANSITION_COMPLETE_MSG
      {do
         ($focus_index {song_offer_provider first_data_index [focus_song]})
         ($diff {- $focus_index [top_index]})
         ($current_button
            {switch $diff
               (0 song_button_01.sbt)
               (1 song_button_02.sbt)
               (2 song_button_03.sbt)
               (3 song_button_04.sbt)
               song_button_01.sbt
            }
         )
         {shell_input set_focus_if_controller_mode $current_button}
         {$this song_highlighted $current_button}
      }
   )
   (update_meta_performer)
   (SELECT_MSG
      {set [top_index] {right_hand.hbs get_top_index}}
      {switch $component
         (song_button_01.sbt {$this play_song $component})
         (song_button_02.sbt {$this play_song $component})
         (song_button_03.sbt {$this play_song $component})
         (song_button_04.sbt {$this play_song $component})
         (back.btn {$this move_back_quickplay})
      }
   )
   (BUTTON_DOWN_MSG
      {if
         {&&
            {== {{$this loaded_dir} get focus_component} song_button_01.sbt}
            {== $action kAction_Up}
         }
         {right_hand.hbs scroll_up}
      }
      {if
         {&&
            {== {{$this loaded_dir} get focus_component} song_button_04.sbt}
            {== $action kAction_Down}
         }
         {right_hand.hbs scroll_down}
      }
      kDataUnhandled
   )
   (FOCUS_MSG {$this highlight_changed $new_focus})
   (HIGHLIGHT_CHANGED_MSG
      {switch $component
         (song_button_01.sbt {$this song_highlighted $component})
         (song_button_02.sbt {$this song_highlighted $component})
         (song_button_03.sbt {$this song_highlighted $component})
         (song_button_04.sbt {$this song_highlighted $component})
         (song_button_up.sbt {$this song_highlighted $component})
         (song_button_down.sbt {$this song_highlighted $component})
         (back.btn)
         {$this song_highlighted none}
      }
   )
   (play_song
      ($button)
      {unless {ui in_transition}
         {do
            ($index {right_hand.hbs list_index_for_button $button})
            {$this play_song_preview $index}
            {do
               ($next_screen {song_offer_provider on_select $index})
               {if $next_screen
                  {ui goto_screen $next_screen}
               }
            }
         }
      }
   )
   (song_highlighted
      ($button)
      {$this set highlight_button $button}
      {if {!= $button none}
         {$this display_song_metadata $button}
      }
   )
   (display_song_metadata
      ($button)
      {unless {ui in_transition}
         {if
            {&&
               {== {ui current_screen} song_select_screen}
               {== [highlight_button] $button}
            }
            {if {&& {!= $button song_button_down.sbt} {!= $button song_button_up.sbt}}
               {do
                  ($index {right_hand.hbs list_index_for_button $button})
                  ($song {song_offer_provider get_token $index})
                  {song_offer_provider set_highlighted_ix $index}
                  {set [details_button] $button}
                  {$this refresh_details $song}
                  {set [focus_song] {song_offer_provider get_token $index}}
                  {if_else {right_hand.hbs is_scrolling_settled}
                     {do
                        {$this play_song_preview $index}
                        {$this refresh_album_art $song}
                     }
                     {do
                        {song_offer_provider stop_preview}
                        {{album_art find album.mesh} set showing FALSE}
                     }
                  }
               }
            }
         }
      }
   )
   (scrolling_settled
      {unless {ui in_transition}
         {if {== {ui current_screen} song_select_screen}
            {do
               ($index {right_hand.hbs list_index_for_button [details_button]})
               ($song {song_offer_provider get_token $index})
               {$this play_song_preview $index}
               {$this refresh_album_art $song}
            }
         }
      }
   )
   (play_song_preview
      ($index)
      {meta music_stop}
      {song_offer_provider start_preview $index}
   )
   (move_on_quickplay
      {unless {ui in_transition}
         {ui goto_screen {gamemode get ready_screen}}
      }
   )
   (move_back_quickplay
      {unless {ui in_transition}
         {ui go_back_screen {song_select_get_back_screen}}
      }
   )
   (storage_changed
      {if_else {! {ui_event_mgr has_active_dialog_event}}
         {ui_event_mgr trigger_event storage_changed}
         {set [storage_changed_error] TRUE}
      }
   )
   (event_dialog_dismiss
      {if [storage_changed_error]
         {ui_event_mgr trigger_event storage_changed}
         {set [storage_changed_error] FALSE}
      }
   )
   (refresh_details
      ($song)
      {$this refresh_song $song}
   )
   (refresh_album_art
      ($song)
      {do
         ($content_name {song_mgr content_name_from_sym $song})
         {if_else {'||' {== $content_name ""} {content_mgr is_mounted $content_name}}
            {do
               ($pic {album_art find album_art.pic})
               {$pic set tex_file {song_mgr album_art_path $song}}
            }
            {do
               ($mesh {album_art find album.mesh})
               {$mesh set showing FALSE}
            }
         }
      }
   )
   (refresh_song
      ($song)
      {do
         ($songid {song_mgr get_song_id $song})
         {album.lbl set_album_name $song}
         {artist.lbl set_artist_name $song}
         {intensity.sdd set_level {song_mgr rank_tier_for_song $song}}
         {dancer.lbl set_dancer $song}
         {best_score.lbl set_best_score $songid}
      }
   )
   (update_song_list {right_hand.hbs update_list})
}
{func song_select_get_back_screen
   main_screen
}
{new UIPanel
   lb_song_select_content_loading_panel
   (load
      {content_mgr start_refresh}
      {content_loading_panel allowed_to_show TRUE}
   )
   (finish_load {content_loading_panel allowed_to_show FALSE})
   (is_loaded {content_mgr refresh_done})
}
#define COMMON_SONG_SELECT_SCREEN (
   (focus song_select_panel)
   (back {song_select_get_back_screen})
   (background_view song_select)
)
{new HamScreen
   song_select_screen
   (panels meta background_panel song_select_panel)
   COMMON_SONG_SELECT_SCREEN
}
{new HamScreen
   invalid_version_screen
   (panels dialog_panel)
   (focus dialog_panel)
   (enter {dialog_panel set_ok invalid_version})
   (SELECT_MSG {ui pop_screen})
}