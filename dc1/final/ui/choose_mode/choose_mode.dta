{new HamPanel
   choose_mode_panel
   (file "choose_mode.milo")
   (focus practice.btn)
   (show_back_button TRUE)
   (enter
      {do
         ($songid {song_mgr get_song_id {meta_performer song}})
         {practice_diff.lbl set_best_practice_diff $songid}
         {practice_score.lbl set_best_practice_score $songid}
         {perform_stars.std set_song_never_multiplayer $songid}
         {multiplayer_stars.std set_song_multiplayer $songid}
      }
      {if {acc_mgr is_unlockable_asset {meta_performer song}}
         {$this disable practice.btn}
         {shell_input update_button_zones}
      }
      {if {profile_mgr is_playing_without_save_file}
         {$this disable leaderboards.btn}
         {shell_input update_button_zones}
      }
   )
   (SELECT_MSG
      {switch $component
         (perform.btn
            {gamemode set_mode perform}
            {ui goto_screen {gamemode get seldiff_screen}}
         )
         (practice.btn
            {gamemode set_mode practice}
            {ui goto_screen {gamemode get seldiff_screen}}
         )
         (multiplayer.btn
            {gamemode set_mode multiplayer}
            {ui goto_screen {gamemode get seldiff_screen}}
         )
         (leaderboards.btn {ui goto_screen leaderboards_screen})
         (back.btn {ui go_back_screen {{ui current_screen} get back}})
      }
   )
   (FOCUS_MSG {$this highlight_changed $new_focus})
   (highlight_changed
      ($component)
      {switch $component
         (perform.btn {description.lbl set text_token perform_desc})
         (practice.btn {description.lbl set text_token practice_desc})
         (multiplayer.btn {description.lbl set text_token multiplayer_desc})
         (leaderboards.btn {description.lbl set text_token leaderboards_desc})
      }
   )
}
{new HamScreen
   choose_mode_screen
   (panels meta background_panel choose_mode_panel song_info_panel)
   (focus choose_mode_panel)
   (back song_select_screen)
   (enter {song_info_panel set_mode_label {symbol ""}})
}