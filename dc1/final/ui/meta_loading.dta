{new UIPanel
   meta_loading
   (enter
      {game_screen set mic_removed_mid_setlist FALSE}
      {synth stop_all_sfx TRUE}
      {if $hamdirector
         {$hamdirector unload_all}
      }
   )
   (hide_helpbar TRUE)
   (show_hand_help FALSE)
}
#define kMetaLoading_Inactive (0)
#define kMetaLoading_Entered (1)
#define kMetaLoading_Leaving (2)
#define META_LOADING_SCREEN_HANDLERS (
   (panels meta_loading meta)
   (focus meta_loading)
   (block_event
      ($event)
      {!= [state] kMetaLoading_Leaving}
   )
   (state kMetaLoading_Inactive)
   (TRANSITION_COMPLETE_MSG
      {if {== [state] kMetaLoading_Inactive}
         {ui_event_mgr add_sink $this (event_dialog_dismiss)}
         {rnd set_in_game FALSE}
         {set [state] kMetaLoading_Entered}
      }
   )
   (exit
      {ui_event_mgr remove_sink $this event_dialog_dismiss}
      {set [state] kMetaLoading_Inactive}
   )
   (event_dialog_dismiss)
   (check_advance
      {if {! {ui in_transition}}
         {set [state] kMetaLoading_Leaving}
         {ui goto_screen [next_screen]}
      }
   )
   (poll
      {if {== [state] kMetaLoading_Entered}
         {saveload_mgr autosave}
      }
      {$this check_advance}
   )
)
{new HamScreen
   meta_loading_continue_screen
   META_LOADING_SCREEN_HANDLERS
   (next_screen song_select_screen)
}
{new HamScreen
   meta_loading_main_screen
   META_LOADING_SCREEN_HANDLERS
   (next_screen {gamemode get main_screen})
}
{new HamScreen
   meta_loading_pause_new_song_screen
   META_LOADING_SCREEN_HANDLERS
   (next_screen song_select_screen)
}
{new HamScreen
   meta_loading_signout_screen
   META_LOADING_SCREEN_HANDLERS
   (next_screen {gamemode get uisink_signout_screen})
}
{new HamScreen
   meta_loading_uisink_default_screen
   META_LOADING_SCREEN_HANDLERS
   (next_screen {gamemode get uisink_default_screen})
}
{new HamScreen
   meta_loading_uisink_lost_connection_screen
   META_LOADING_SCREEN_HANDLERS
   (next_screen {gamemode get uisink_lost_connection_screen})
}
{new HamScreen
   meta_loading_leaderboard_error_screen
   META_LOADING_SCREEN_HANDLERS
   (next_screen {leaderboards_netwatcher_panel get return_screen_in_progress})
}
#ifndef _SHIP
{new HamScreen
   meta_loading_return_to_frame_rate_screen
   META_LOADING_SCREEN_HANDLERS
   (next_screen frame_rate_screen)
}
#endif