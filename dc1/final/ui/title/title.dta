{new UIPanel
   title_panel
   (file "title.milo")
   (show_hand_help FALSE)
   (enter
      {meta music_start}
      {user_mgr clear_slot 0}
      {depth_buffer set_state kDepthBuffer_Tutorial}
   )
   (exit {depth_buffer set_state kDepthBuffer_Normal})
   (SELECT_MSG
      {switch $component
         (start.btn
            {if {&& {! {ui in_transition}} {== {ui current_screen} title_screen}}
               {depth_buffer end_tutorial}
               {ui goto_screen wait_check_signin_after_saveload_screen}
            }
         )
      }
   )
}
{new UIPanel empty_postproc_panel (file "empty_postproc.milo")}
{new HamScreen
   title_screen
   (panels meta background_panel title_panel)
   (focus title_panel)
}
{new SigninScreen
   check_signin_screen
   (panels meta background_panel empty_postproc_panel)
   (must_not_be_a_guest TRUE)
   (TRANSITION_COMPLETE_MSG
      #ifdef HX_PC
      {profile_mgr set_active_pad_num 0}
      {$this goto_next_screen}
      #else
      {do
         ($signin_mask {platform_mgr get_signin_mask})
         {cond
            ({== $signin_mask 0}
               {$this show_signin_ui}
            )
            ({'||'
                  {== $signin_mask 1}
                  {== $signin_mask 2}
                  {== $signin_mask 4}
                  {== $signin_mask 8}}
               {switch $signin_mask
                  (1 {profile_mgr set_active_pad_num 0})
                  (2 {profile_mgr set_active_pad_num 1})
                  (4 {profile_mgr set_active_pad_num 2})
                  (8 {profile_mgr set_active_pad_num 3})
               }
               {$this goto_next_screen}
            )
            (TRUE {ui goto_screen choose_profile_screen})
         }
      }
      #endif
   )
   (on_signed_in {$this goto_next_screen})
   (sign_in_dismissed {ui goto_screen recommend_signin_screen})
   (goto_next_screen {ui goto_screen wait_main_after_saveload_screen})
}
{new ChooseProfilePanel
   choose_profile_panel
   (file "choose_profile.milo")
   (focus 0.btn)
   (show_back_button FALSE)
   (SELECT_MSG
      {switch $component
         (0.btn
            {if {$this profile_selected 0}
               {$this goto_next_screen}
            }
         )
         (1.btn
            {if {$this profile_selected 1}
               {$this goto_next_screen}
            }
         )
         (2.btn
            {if {$this profile_selected 2}
               {$this goto_next_screen}
            }
         )
         (3.btn
            {if {$this profile_selected 3}
               {$this goto_next_screen}
            }
         )
         (4.btn
            {if {$this profile_selected 4}
               {$this goto_next_screen}
            }
         )
      }
   )
   (goto_next_screen {ui goto_screen wait_main_after_saveload_screen})
}
{new HamScreen
   choose_profile_screen
   (panels meta background_panel choose_profile_panel)
   (focus choose_profile_panel)
   (back title_screen)
}
{new SigninScreen
   recommend_signin_screen
   (panels meta background_panel dialog_panel)
   (focus dialog_panel)
   (enter
      {dialog_panel set_custom recommend_sign_in sign_in continue_no_save}
   )
   (SELECT_MSG
      {switch $component
         (opt1.btn {$this show_signin_ui {user_mgr get_active_user}})
         (opt2.btn
            {profile_mgr set_playing_without_save_file}
            {$this goto_next_screen}
         )
      }
   )
   (on_signed_in {$this goto_next_screen})
   (goto_next_screen
      {user_mgr set_slot {user_mgr get_active_user} 0}
      {ui goto_screen wait_main_after_saveload_screen}
   )
}
{new UIPanel
   wait_for_saveload_panel
   (is_waiting TRUE)
   (enter {set [is_waiting] TRUE})
   (poll
      {if {&& {== [is_waiting] TRUE} {! {ui in_transition}} {saveload_mgr is_idle}}
         {set [is_waiting] FALSE}
         {{ui current_screen} saveload_complete}
      }
   )
}
{new HamScreen
   wait_check_signin_after_saveload_screen
   (panels meta background_panel empty_postproc_panel wait_for_saveload_panel)
   (focus wait_for_saveload_panel)
   (saveload_complete {ui goto_screen check_signin_screen})
}
{new HamScreen
   wait_main_after_saveload_screen
   (panels meta background_panel empty_postproc_panel wait_for_saveload_panel)
   (focus wait_for_saveload_panel)
   (enter {saveload_mgr activate})
   (saveload_complete
      {user_mgr set_slot {user_mgr get_active_user} 0}
      {ui goto_screen main_screen}
   )
}
{new HamScreen
   unlock_all_screen
   (panels meta background_panel dialog_panel)
   (focus dialog_panel)
   (enter
      {dialog_panel set_ok unlock_all_description}
      {meta unlock_all}
   )
   (SELECT_MSG {ui goto_screen title_screen})
}