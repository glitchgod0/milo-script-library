(left
   (kPad_Tri
      "Game: Cycle Free Camera / Splash: Goto Framerate Test / Framerate Test: Restart"
      (filters game)
      {cond
         ({==
               {ui current_screen}
               splash_screen}
            {ui goto_screen frame_rate_screen}
         )
         ({==
               {ui current_screen}
               frame_rate_game_screen}
            {ui push_screen meta_loading_return_to_frame_rate_screen}
         )
         (TRUE {cycle_free_camera})
      }
   )
   (kPad_R2 "HAQ display all" {haq_display_all})
)
(right
   (kPad_Square
      "Print Current Screen"
      (filters meta)
      {handle ({ui current_screen} print)}
   )
)
(keyboard
   ('`' alt "Toggle fake file errors" {toggle_fake_file_errors})
   #ifdef HX_XBOX
   (KB_F2 "Fake a signout change on controller 1" {fake_signout 0})
   (KB_F3 "Fake a signout change on controller 2" {fake_signout 1})
   (KB_F4 "Fake a signout change on controller 3" {fake_signout 2})
   #endif
   (KB_F9
      "Check if gameplay timer is running"
      {cheat_display show_bool
         "Gameplay timer running"
         {meta_performer is_gameplay_timer_running}
      }
   )
   (KB_F10
      "Add 1 minute of played time"
      {cheat_display show "Adding 1 minute to gameplay timer"}
      {meta_performer jump_gameplay_timer_forward 60}
   )
   (KB_F11
      "Add 1 hour of played time"
      {cheat_display show "Adding 1 hour to gameplay timer"}
      {meta_performer jump_gameplay_timer_forward 3600}
   )
   (KB_F12
      "Add 1 day of played time"
      {cheat_display show "Adding 1 day to gameplay timer"}
      {meta_performer jump_gameplay_timer_forward 86400}
   )
   (0
      "Pause/unpause game"
      (filters game)
      {toggle_time_paused}
   )
   (5
      "Cycle through graphical character debugging in game"
      (filters game)
      {next_character_driver}
   )
   (&
      "Next Loading Tip entry"
      (filters game meta)
      {cheat_next_loading_tip_entry}
   )
   ('*' "EMPTY")
   ('['
      "Toggle in-game timer stats"
      (filters game)
      {rnd toggle_timers_verbose}
   )
   ('{'
      "Cycle Loading tip mode"
      (filters game meta)
      {cheat_next_loading_tip_mode}
   )
   (a
      "Toggle Auto-Play"
      (filters game)
      {cheat_display show_bool "Autoplay" {toggle_autoplay}}
   )
   (a
      alt
      "Cycle Auto-Play rating levels"
      (filters game)
      {cheat_display show {sprint "Autoplay :" {cycle_autoplay}}}
   )
   (a
      ctrl
      "Game: Load next character"
      (filters game)
      {if $hamwardrobe
         {{$hamwardrobe get_character 0} next_character}
      }
   )
   (A
      "Meta: unlock all; Game: toggle crowd audio"
      (filters meta game)
      {if_else {meta is_up}
         {do
            {cheat_display show_bool "Unlock All" {toggle_unlock_all}}
         }
         {toggle_crowd_audio}
      }
   )
   (A
      ctrl
      "Keep pressing the A button every 1/2 second"
      {ui set_button_spam {! {ui button_spam}}}
      {cheat_display show_bool "A button repeat " {ui button_spam}}
   )
   (b
      "Hide characters"
      (filters game)
      {if $hamdirector
         {do
            ($world {$hamdirector get cur_world})
            {-- $cheat.hide_char}
            {if {< $cheat.hide_char 0}
               {set $cheat.hide_char 7}
            }
            {do
               ($show_main_dancer {== 0 {& $cheat.hide_char 1}})
               ($show_backup_dancers {== 0 {& $cheat.hide_char 2}})
               ($show_crowd {== 0 {& $cheat.hide_char 4}})
               {$world iterate
                  Character
                  $c
                  {cond
                     ({has_substr
                           {$c name}
                           "player"}
                        {$c set_showing $show_main_dancer}
                     )
                     ({has_substr
                           {$c name}
                           "backup"}
                        {$c set_showing $show_backup_dancers}
                     )
                     {$c set_showing $show_crowd}
                  }
               }
               {$world iterate
                  RndDir
                  $r
                  {if
                     {'||'
                        {has_substr {$r name} "move_feedback"}
                        {has_substr {$r name} "phrase_meter"}
                     }
                     {$r set_showing $show_main_dancer}
                  }
               }
               {cheat_display show
                  {sprintf
                     "Showing Characters - main: %s, backup: %s, crowd: %s"
                     {if_else $show_main_dancer
                        "yes"
                        "no"
                     }
                     {if_else $show_backup_dancers
                        "yes"
                        "no"
                     }
                     {if_else $show_crowd
                        "yes"
                        "no"
                     }
                  }
               }
            }
         }
      }
   )
   (B
      "Game: Toggle Battle Meter"
      (filters game)
      {do
         ($battle_meter_group
            {find_obj $hud_panel multiplayer_meter battle_meter.grp}
         )
         {if $battle_meter_group
            {$battle_meter_group set_showing {! {$battle_meter_group showing}}}
            {cheat_display show_bool "Battle Meter " {$battle_meter_group showing}}
         }
      }
   )
   (c
      "Game: Cycle CamShots of matching Band Configuration"
      (filters game)
      {cond
         ({game_panel is_up}
            {if $hamdirector
               {$hamdirector cycle_shot}
            }
         )
      }
   )
   (c
      ctrl
      "Game: Display fitness mode (calories)"
      (filters game)
      {rnd toggle_overlay fitness}
      {rnd toggle_overlay fitness_meter}
   )
   (c
      alt
      "Isolate Main Character (there is no recovery)"
      (filters game)
      {do
         ($wrld {$hamdirector get cur_world})
         ($name "")
         {if $wrld
            {$wrld iterate
               Draw
               $draw
               {set $name {$draw name}}
               {if_else {'||' {== $name "player0.grp"} {== $name "player0"}}
                  {$draw set_showing TRUE}
                  {$draw set_showing FALSE}
               }
            }
         }
         {cheat_display show "Isolate Character... no recovery!"}
      }
   )
   (C
      "Toggle Current CamShot Info"
      (filters game)
      {rnd toggle_overlay camera}
      {if $hamdirector
         {handle ({$hamdirector get cur_shot} update_overlay)}
      }
   )
   (d
      "Game: Toggle Camera Picking, Meta: Toggle Difficulty"
      (filters game meta)
      {if_else {game_panel is_up}
         {if $p9director
            {do
               ($disable {! {$p9director get disable_picking}})
               {$p9director set disable_picking $disable}
               {cheat_display show_bool "Disable camera picking" $disable}
               {unless $disable
                  {set $composite_character_demand_load FALSE}
               }
            }
         }
         {cheat_toggle_difficulty}
      }
   )
   (d
      alt
      "Toggle use of procedural eye darts"
      {set $cheat.disable_eye_darts {! $cheat.disable_eye_darts}}
      {cheat_display show_bool
         "Disable character eye darts"
         $cheat.disable_eye_darts
      }
   )
   (D ctrl "Trigger Dirty Disc Error" {platform_mgr trigger_disk_error})
   (e
      "Toggle depth camera display"
      {do
         ($depth {cursor_panel find depth_buffer.grp})
         {$depth set_showing {! {$depth showing}}}
         {cheat_display show_bool "Depth display" {$depth showing}}
      }
   )
   (e
      ctrl
      "Toggles on/off visual debugging of eye interest object system"
      (filters game)
      {if $hamdirector
         {$hamdirector toggle_debug_interests}
         {cheat_display show "toggle visual debugging of eye interest system"}
      }
   )
   (E
      "Skip credits movie"
      (filters meta)
      {set $skip_movie_credits {! $skip_movie_credits}}
      {cheat_display show_bool "Skip credits movie" $skip_movie_credits}
   )
   (f
      "Fake cursor input with right stick"
      (filters natural_input)
      {cheat_display show_bool "Fake cursor" {ui toggle_fake_cursor_input}}
   )
   (f
      ctrl
      "Toggle move detection overlay"
      (filters natural_input)
      {cheat_display show {sprintf "Move overlay: %s" {toggle_move_overlay}}}
   )
   (f
      alt
      "Toggle per-limb feedback"
      (filters natural_input)
      {cheat_display show_bool "Per-limb feedback" {toggle_char_feedback}}
   )
   (F
      "Cycle Free Camera"
      (filters game)
      {cycle_free_camera}
   )
   (g
      "Draw cursor mode"
      (filters natural_input)
      {cheat_display show {sprint "Draw cursor mode " {ui cycle_draw_cursor}}}
   )
   (G "Print faders" {audio print_faders})
   (h
      "Toggle HUD"
      (filters game)
      (value_bool {&& $hud_panel {$hud_panel showing}})
      {if $hud_panel
         {$hud_panel set_showing {! {$hud_panel showing}}}
         {cheat_display show_bool "HUD" {$hud_panel showing}}
      }
   )
   (H "Toggle forced detection of calbert" {toggle_forced_autocal})
   (H
      ctrl
      "Reload hud/gameplay scripts and milo files"
      (filters game)
      {game_panel reload_data}
      {game_panel init}
   )
   (i
      "Force long intro cams"
      (filters game)
      {set $cheat.force_long_intro_cam {! $cheat.force_long_intro_cam}}
      {cheat_display show_bool
         "Force long intro cams"
         $cheat.force_long_intro_cam
      }
   )
   (I
      "Toggle Light Preset overlay"
      (filters game)
      {rnd toggle_overlay light_preset}
   )
   (i
      ctrl
      "Toggle eye debug overlay"
      (filters game)
      {rnd toggle_overlay eye_status}
   )
   (i
      alt
      "Show final pose effect"
      (filters game)
      {if $hud_panel
         {$hud_panel final_pose_snapshot}
      }
   )
   (j
      ctrl
      "Toggle use of eye jitter in lookats"
      {set $cheat.disable_eye_jitter {! $cheat.disable_eye_jitter}}
      {cheat_display show_bool
         "Disable character eye jitter"
         $cheat.disable_eye_jitter
      }
   )
   (k "Meta: Komplete Achievement" {fake_achievement})
   (K "Toggle HAQ Printouts" {toggle_haq_printouts})
   (l
      "Toggle single move looping"
      (filters game)
      {switch {game_panel get_type}
         (linear
            {do
               ($start)
               ($end)
               ($measure_beat {- $beat {mod $beat 4}})
               {if_else {audio get_loop_beats $start $end}
                  {do
                     {audio clear_loop}
                     {cheat_display show "Clearing loop"}
                     {game set_loop TRUE}
                  }
                  {do
                     {audio set_loop {- $measure_beat 4} {'+' $measure_beat 8}}
                     {audio get_loop_beats $start $end}
                     {if {game get move}
                        {cheat_display show {sprint "Looping on " {game get move}}}
                     }
                     {game set_loop TRUE}
                  }
               }
            }
         )
         (skillz
            {if {== {game_panel get state} learn}
               {game_panel set learn_mode_locked {! {game_panel get learn_mode_locked}}}
               {if_else {game_panel get learn_mode_locked}
                  {cheat_display show "Learn mode locked. Press l again to unlock!"}
                  {cheat_display show "Learn mode unlocked!"}
               }
            }
         )
      }
   )
   (L "Toggle audio levels" {synth toggle_hud})
   (l
      ctrl
      "Toggle force using longest VO"
      (filters game)
      {if $hud_panel
         {$hud_panel toggle_force_longest_sfx}
         {cheat_display show_bool
            "Force longest VO"
            {$hud_panel get force_longest_sfx}
         }
      }
   )
   (m
      "Game: Toggle midi audio"
      (filters game meta)
      {if {game_panel is_up}
         {do
            ($midi_cross_fader {synth find midi_music_cross_fade.fade FALSE})
            ($active {! {midi_player get active}})
            {midi_player set active $active}
            {if $midi_cross_fader
               {$midi_cross_fader set level 0}
            }
            {cheat_display show {sprint "Midi playing " {midi_player get active}}}
         }
      }
   )
   (m
      alt
      "Get latest strings from Magnu"
      (filters writer)
      {do
         ($num_strings {magnu_refresh_strings})
         {cheat_display show
            {sprintf "Found %i new strings from Magnu" $num_strings}
         }
      }
   )
   (m
      ctrl
      "Game: Toggle crowd meter/Meta: Mute shell music"
      (filters game meta)
      {if_else {game_panel is_up}
         {toggle_crowd_meter}
         {toggle_mute_shell_music}
      }
   )
   (M
      "Toggle mutes all"
      (value_bool $mute_cheat)
      {toggle_mute_global}
   )
   (n
      "Cycle notify level"
      (value {elem (disable overlay modal) $notify_level})
      {do
         ($names (disable overlay modal))
         {set $notify_level {mod {'++' $notify_level} {size $names}}}
         {cheat_display show {sprint "Notify level " {elem $names $notify_level}}}
      }
   )
   (N
      "Toggle Natal autoexposure"
      (filters natural_input)
      (value_bool {gesture_mgr using_autoexposure_tweak})
      {gesture_mgr toggle_autoexposure_tweak}
      {cheat_display show_bool
         "Camera night mode"
         {gesture_mgr using_autoexposure_tweak}
      }
   )
   (n
      ctrl
      "Force Finale sequence at the end of the last song"
      {cheat_display show_bool "Finale" {meta_performer cheat_toggle_finale}}
   )
   (N
      ctrl
      "Dump Natal Camera properties and exposure settings"
      {if {exists gesture_mgr}
         {gesture_mgr dump_camera_properties}
         {cheat_display show "Settings dumped to log."}
      }
   )
   (o "Toggle Post Proc overlay" {rnd toggle_overlay postproc})
   (O "Print the symbol table" {print_symbol_table syms})
   (o
      alt
      "Lower depth alpha threshold"
      {do
         ($depth {cursor_panel find depth_buffer.mat})
         ($alpha_threshold {$depth get alpha_threshold})
         {$depth set alpha_threshold {- $alpha_threshold 1}}
         {cheat_display show
            {sprint "Alpha threshold: " {$depth get alpha_threshold}}
         }
      }
   )
   (o
      ctrl
      "Raise depth alpha threshold"
      {do
         ($depth {cursor_panel find depth_buffer.mat})
         ($alpha_threshold {$depth get alpha_threshold})
         {$depth set alpha_threshold {'+' $alpha_threshold 1}}
         {cheat_display show
            {sprint "Alpha threshold: " {$depth get alpha_threshold}}
         }
      }
   )
   (p
      ctrl
      "Display current blended PostProcs"
      (filters game)
      {display_postprocs}
   )
   (P
      "Shell: Toggle Random Song Debug"
      (filters meta)
      {cheat_display show_bool
         "Random Song Debug"
         {song_mgr toggle_random_song_debug}
      }
   )
   (p
      alt
      "Toggle all postprocess effects"
      (filters game meta)
      {cheat_display show
         {sprint
            "PostProcs: "
            {if_else {rnd toggle_all_postprocs}
               "off"
               "on"
            }
         }
      }
   )
   (q
      "Print Current Screen"
      (filters meta)
      {handle ({ui current_screen} print)}
   )
   (q
      ctrl
      "Toggle RandomGroupSequence Randomness"
      {game force_serial_sequences {! {game using_serial_sequences}}}
      {cheat_display show_bool
         "RandomGroupSequence randomness: "
         {! {game using_serial_sequences}}
      }
   )
   (Q
      "Toggle Credits Autoscroll"
      {if {== {ui current_screen} text_credits_screen}
         {text_credits_panel debug_toggle_autoscroll}
         {cheat_display show "Credits Autoscroll toggled"}
      }
   )
   (r
      "Toggle Shell Automation"
      (value {ui auto_script})
      {cheat_display show {sprint "Shell Automation: " {ui toggle_auto}}}
   )
   (R
      "Toggle Shell Recording"
      (value {ui record_script})
      {cheat_display show {sprint "Shell Recording: " {ui toggle_record}}}
   )
   (r
      ctrl
      "Game: Reset Detection"
      (filters game)
      {if $hamdirector
         {$hamdirector reset_detection}
      }
   )
   (r
      alt
      "Game: Toggle Song Recording"
      (filters game)
      {cheat_display show_bool "Song recording" {toggle_song_record}}
   )
   (R
      ctrl
      "Use Restart as the default option in the end game screens"
      {set $cheat_focus_restart {! $cheat_focus_restart}}
      {cheat_display show
         {sprint
            "Default end game screen option: "
            {if_else $cheat_focus_restart
               "Restart"
               "Continue"
            }
         }
      }
   )
   (s
      "Toggle Skeleton Drawing"
      (filters natural_input)
      {cheat_display show_bool "Skeleton drawing" {ui toggle_draw_skeletons}}
   )
   (s
      ctrl
      "Toggle joint smoothing"
      (filters natural_input)
      (value_bool {gesture_mgr using_smoothing})
      {gesture_mgr toggle_smoothing}
      {cheat_display show_bool "Joint smoothing" {gesture_mgr using_smoothing}}
   )
   (s
      alt
      "Set Active Skeleton to First Tracked"
      (filters natural_input)
      {gesture_mgr set_first_tracked_active}
   )
   (S
      "Toggle scoring info"
      (filters game)
      {if {game_panel is_up}
         {rnd toggle_overlay score}
      }
   )
   (t
      "Toggle time overlay"
      (filters game)
      {rnd toggle_overlay time}
   )
   (t
      ctrl
      "Toggle camera tilt correction"
      (filters natural_input)
      (value_bool {gesture_mgr using_tilt_correction})
      {gesture_mgr toggle_tilt_correction}
      {cheat_display show_bool
         "Camera tilt correction"
         {gesture_mgr using_tilt_correction}
      }
   )
   (t alt "Print the particle pool size" {print_particle_pool_size})
   (T
      "Toggle profiling timers"
      (filters game)
      {rnd toggle_timers}
      {rnd toggle_overlay rate}
   )
   (u
      "Toggle fullscreen camera/skeleton debug drawing"
      (filters natural_input)
      {cheat_display show_bool
         "Fullscreen camera/skeleton"
         {ui toggle_full_screen_draw}
      }
   )
   (U
      "Toggle UI load times"
      (filters meta)
      (value_bool {ui showing_load_times})
      {ui toggle_load_times}
   )
   (v
      "Song select: Go to venue select"
      (filters meta)
      {if {song_select_panel is_up}
         {ui goto_screen selvenue_screen}
      }
   )
   (V
      "Turn on verb barks"
      (filters game)
      {if {game_panel is_up}
         {if_else {action_bark_player get active}
            {action_bark_player set_active FALSE}
            {$hud_panel add_all_barks}
         }
         {cheat_display show_bool "actions barks " {action_bark_player get active}}
      }
   )
   (v
      ctrl
      "toggle VO spew"
      {if {game_panel is_up}
         {do
            ($new_state {! {game_panel get vo_spew}})
            {game_panel set vo_spew $new_state}
            {cheat_display show_bool "VO spew" $new_state}
         }
      }
   )
   (v
      alt
      "show freestyle feedback text"
      (filters game)
      {do
         ($trig
            {find_obj
               $hud_panel
               freestyle_text_feedback
               {if_else $freestyle_cheat_toggle
                  "freestyle_rating_awesome.trig"
                  "freestyle_rating_perfect.trig"
               }
            }
         )
         {if $trig
            {$trig trigger}
         }
         {set $freestyle_cheat_toggle {! $freestyle_cheat_toggle}}
      }
   )
   (w
      "Win the game"
      (filters game)
      {if {game_panel is_up}
         {game_panel win}
      }
   )
   (w
      ctrl
      "Toggle world game events output"
      (filters game)
      {if {exists game}
         {set $gameplay_event_spew {! $gameplay_event_spew}}
         {cheat_display show_bool "World Game events output" $gameplay_event_spew}
      }
   )
   (x
      "Show skeleton identification debug"
      (filters gesture)
      {cheat_display show_bool
         "Skeleton identification debug"
         {skeleton_identifier toggle_draw_debug}
      }
   )
   (x
      ctrl
      "Show skeleton chooser debug"
      (filters gesture)
      {cheat_display show_bool
         "Skeleton chooser debug"
         {skeleton_chooser toggle_draw_debug}
      }
   )
   (x
      alt
      "Toggle Kinect autoexposure"
      (filters game meta)
      {cheat_display show
         {sprint
            "Autoexposure: "
            {if_else {gesture_mgr toggle_autoexposure}
               "on"
               "off"
            }
         }
      }
   )
   (X
      "Toggle shuttle"
      (filters game)
      {toggle_shuttle}
   )
   (y
      "Toggle fake leaderboard upload failure"
      {toggle_fake_leaderboard_upload_failure}
   )
   (y
      ctrl
      "Toggle on character feedback"
      (filters game)
      {set $hide_character_feedback {! $hide_character_feedback}}
      {do
         ($wrld {$hamdirector get cur_world})
         ($feedback_list ())
         {resize $feedback_list 0}
         {push_back
            $feedback_list
            {find_obj $wrld phrase_meter draw.grp}
         }
         {push_back
            $feedback_list
            {find_obj $wrld text_feedback draw.grp}
         }
         {push_back $feedback_list {find_obj $wrld move_feedback}}
         {foreach $elem $feedback_list
            {if $elem
               {$elem set_showing {! $hide_character_feedback}}
            }
         }
      }
      {cheat_display show_bool
         "On character feedback "
         {! $hide_character_feedback}
      }
   )
   (Y
      "Game: Toggle mute master fader"
      (filters game)
      {if {game_panel is_up}
         {audio toggle_mute_master}
      }
   )
   (z
      "Toggle showing world"
      (filters game)
      (value_bool
         {&&
            $hamdirector
            {$hamdirector get cur_world}
            {{$hamdirector get cur_world} showing}
         }
      )
      {if {&& $hamdirector {$hamdirector get cur_world}}
         {{$hamdirector get cur_world} set_showing
            {! {{$hamdirector get cur_world} showing}}
         }
         {cheat_display show_bool "VENUE" {{$hamdirector get cur_world} showing}}
      }
   )
   (z
      ctrl
      "Toggle pause on skeleton loss"
      (filters natural_input)
      {cheat_display show_bool
         "Pause on skeleton loss"
         {gesture_mgr toggle_pause_on_skeleton_loss}
      }
      {if {pause_panel is_up}
         {pause_panel update_skeleton_status}
      }
   )
   (Z
      "Hi-reZ screen shot"
      (filters game meta)
      {rnd hi_res_screen 5}
   )
   (')' "Win with 0 stars" {fake_win 0})
   (! "Win with 1 star" {fake_win 1})
   ('@' "Win with 2 stars" {fake_win 2})
   (# "Win with 3 stars" {fake_win 3})
   ('$' "Win with 4 stars" {fake_win 4})
   ('%' "Win with 5 stars" {fake_win 5})
   ('^' "Win with 6 stars" {fake_win 6})
   (! ctrl "Break It Down win, all perfect moves" {fake_perfect_skills_win})
   (<
      "Jump backward a bit in time"
      (filters game)
      {if {&& {exists game} {game_panel is_up}}
         {game jump {- {game get_song_ms} 1000.0}}
      }
   )
   ('|'
      "Toggles practice mode playing all stems."
      {practice_panel set_play_all_tracks
         {! {practice_panel get_play_all_tracks}}
      }
   )
   (KB_UP
      "Double song speed / Paused: increase vocal pitch window size"
      (filters game)
      {if {game_panel is_up}
         {change_speed 2.0}
      }
   )
   (KB_DOWN
      "Halve song speed / Paused: decrease vocal pitch window size"
      (filters game)
      {if {game_panel is_up}
         {change_speed 0.5}
      }
   )
   (KB_RIGHT
      "Rotate Debug Skeleton drawing by 90 degrees"
      (filters natural_input)
      {cheat_display show
         {sprintf "Skeleton rotation: %f degrees" {ui next_skeleton_draw_rot}}
      }
   )
   (KB_ENTER
      "Skills - skip to next section/ Multiplayer - skip to end of section"
      (filters game)
      {switch {game_panel get_type}
         (skillz {game_panel cheat_skip_section})
         (multiplayer {game_panel cheat_skip_phase})
      }
   )
   (':'
      "Toggle pink error shaders"
      (filters game meta)
      {cheat_display show
         {sprint
            "Pink error shaders (for uncached shaders): "
            {if_else {rnd toggle_error_shaders}
               "on"
               "off"
            }
         }
      }
   )
   ('['
      alt
      "Previous performance level"
      (filters game)
      {if {== {game_panel get_type} linear}
         {game_panel force_performance_previous}
      }
   )
   (']'
      alt
      "Next performance level"
      (filters game)
      {if {== {game_panel get_type} linear}
         {game_panel force_performance_next}
      }
   )
   (>
      "Toggle Language"
      (filters writer)
      (value {system_language})
      {do
         ($old_notify_level $notify_level)
         {set $notify_level 0}
         {switch_system_language}
         {cheat_display show {sprint "System language set to " {system_language}}}
         {set $notify_level $old_notify_level}
      }
   )
   ('\\'
      "Sync and reload locale files"
      (filters writer)
      {sync_reload_locale}
      {cheat_display show "Locale files synced and reloaded"}
   )
   (' '
      "Show next camera output (RGB/depth/off)"
      (filters natural_input)
      {cheat_display show
         {sprintf "Camera output: %s" {ui display_next_camera_output}}
      }
   )
   (' '
      alt
      "Toggle player visualizer"
      (filters natural_input)
      {if $hamdirector
         {do
            ($vis {$hamdirector get player_freestyle})
            {if_else $vis
               {do
                  {$hamdirector force_shot ""}
                  {$hamdirector set player_freestyle FALSE}
               }
               {do
                  ($fshot {{$hamdirector get cur_world} find_shot DC_PLAYER_FREESTYLE})
                  {if $fshot
                     {$hamdirector force_shot {$fshot name}}
                  }
               }
            }
            {cheat_display show_bool "Player Visualizer: " {! $vis}}
         }
      }
   )
   #ifndef HX_PC
   ('}'
      #ifdef HX_XBOX
      "Check locale achievements vs. xlast"
      #else
      "Check locale trophies vs. trophy file"
      #endif
      (filters writer)
      {achievements check_strings}
   )
   #endif
)