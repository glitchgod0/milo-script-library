#define SELDIFF_PANEL_COMMON (
   (show_back_button 1)
   (old_difficulty none)
   (enter
      {unless {ui went_back}
         {$this set old_difficulty {meta_performer get_difficulty}}
      }
      {platform_mgr enable_xmp}
      {song.lbl set_song_name {meta_performer song}}
      {artist.lbl set_artist_name {meta_performer song}}
      {intensity.sdd set_level
         {song_mgr rank_tier_for_song {meta_performer song}}
      }
      {do
         ($pic {album_art find album_art.pic})
         {$pic set tex_file {song_mgr album_art_path {meta_performer song}}}
      }
      {handle ($this set_up)}
   )
   (restore_difficulty {meta_performer set_difficulty [old_difficulty]})
   (SELECT_MSG
      {switch $component
         (easy.btn {{ui current_screen} difficulty_selected easy})
         (medium.btn {{ui current_screen} difficulty_selected medium})
         (expert.btn {{ui current_screen} difficulty_selected expert})
         (back.btn {ui go_back_screen {{ui current_screen} get back}})
      }
   )
)
{new HamPanel
   seldiff_panel
   (file "seldiff.milo")
   (start_controller_focus easy.btn)
   SELDIFF_PANEL_COMMON
   (set_up
      {do
         ($songid {song_mgr get_song_id {meta_performer song}})
         {last_stars.std set_song_last_played $songid}
         {last_played.lbl set_last_played_time $songid}
         {easy_stars.std set_song_with_diff $songid 0}
         {medium_stars.std set_song_with_diff $songid 1}
         {expert_stars.std set_song_with_diff $songid 2}
         {medium_locked.lbl set_locked {! {$this is_difficulty_unlocked medium}}}
         {expert_locked.lbl set_locked {! {$this is_difficulty_unlocked expert}}}
      }
   )
   (is_difficulty_unlocked
      ($diff)
      {do
         ($profile {profile_mgr get_profile {user_mgr get_active_user}})
         {$profile is_difficulty_unlocked {meta_performer song} $diff}
      }
   )
}
{new HamScreen
   seldiff_screen
   (panels meta background_panel seldiff_panel)
   (focus seldiff_panel)
   (back choose_mode_screen)
   (difficulty_selected
      ($diff)
      {if_else {seldiff_panel is_difficulty_unlocked $diff}
         {do
            {meta_performer set_difficulty $diff}
            {ui goto_screen {gamemode get startgame_screen}}
         }
         {do
            {locked_content_panel set_up_difficulty_locked {meta_performer song} $diff}
            {ui push_screen locked_content_screen}
         }
      }
   )
}
{new HamScreen
   ingame_seldiff_screen
   (panels ingame_background_panel seldiff_panel)
   (focus seldiff_panel)
   (back_screen none)
   (back
      {do
         {seldiff_panel restore_difficulty}
         {ingame_seldiff_screen get back_screen}
      }
   )
   (difficulty_selected
      ($diff)
      {meta_performer set_difficulty $diff}
      {if_else {game_panel is_game_over}
         {do
            {meta_performer restart}
            {game_restart}
         }
         {game_panel end_game 0}
      }
   )
}
{new HamPanel
   seldiff_practice_panel
   (file "seldiff_practice.milo")
   (start_controller_focus easy.btn)
   SELDIFF_PANEL_COMMON
   (set_up)
}
{new HamScreen
   seldiff_practice_screen
   (panels meta background_panel seldiff_practice_panel)
   (focus seldiff_practice_panel)
   (back choose_mode_screen)
   (difficulty_selected
      ($diff)
      {meta_performer set_difficulty $diff}
      {ui goto_screen {gamemode get startgame_screen}}
   )
}
{new HamScreen
   ingame_seldiff_practice_screen
   (panels ingame_background_panel seldiff_practice_panel)
   (focus seldiff_practice_panel)
   (back_screen none)
   (back
      {do
         {seldiff_practice_panel restore_difficulty}
         {ingame_seldiff_practice_screen get back_screen}
      }
   )
   (difficulty_selected
      ($diff)
      {meta_performer set_difficulty $diff}
      {if_else {game_panel is_game_over}
         {do
            {meta_performer restart}
            {game_restart}
         }
         {game_panel end_game 0}
      }
   )
}
{new HamPanel
   seldiff_multiplayer_panel
   (file "seldiff_multiplayer.milo")
   (start_controller_focus easy.btn)
   SELDIFF_PANEL_COMMON
   (set_player_header
      ($player)
      {switch $player
         (0
            {player_1_header.lbl set_showing 1}
            {player_2_header.lbl set_showing 0}
         )
         (1
            {player_1_header.lbl set_showing 0}
            {player_2_header.lbl set_showing 1}
         )
      }
   )
}
{new HamScreen
   seldiff_multiplayer_player1_screen
   (panels meta background_panel seldiff_multiplayer_panel)
   (focus seldiff_multiplayer_panel)
   (back choose_mode_screen)
   (enter
      {skeleton_identifier reset_for_new_game}
      {seldiff_multiplayer_panel set_player_header 0}
   )
   (difficulty_selected
      ($diff)
      {skeleton_identifier assign_current_skeleton_to_slot 0}
      {meta_performer set_difficulty $diff}
      {ui goto_screen seldiff_multiplayer_player2_screen}
   )
}
{new HamScreen
   seldiff_multiplayer_player2_screen
   (panels meta background_panel seldiff_multiplayer_panel)
   (focus seldiff_multiplayer_panel)
   (back choose_mode_screen)
   (enter {seldiff_multiplayer_panel set_player_header 1})
   (difficulty_selected
      ($diff)
      {skeleton_identifier assign_current_skeleton_to_slot 1}
      {meta_performer set_secondary_difficulty $diff}
      {ui goto_screen {gamemode get startgame_screen}}
   )
}