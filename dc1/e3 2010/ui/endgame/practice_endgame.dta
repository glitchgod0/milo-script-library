#include endgame_helpers.dta
{new HamPanel
   practice_endgame_panel
   (file "practice_endgame.milo")
   (start_controller_focus continue.btn)
   ENDGAME_PANEL_HANDLERS
   (enter
      {unless {ui went_back}
         {meta_performer trigger_song_completion {$this get_score} 0}
         {meta_performer advance_song {{game main_performer} num_stars}}
      }
      {platform_mgr set_notify_ui_location 1}
      {$this set_results}
      ENDGAME_PANEL_COMMON_ENTER
      {saveload_mgr autosave}
      {meta_performer add_sink $this}
   )
   (exit
      {platform_mgr set_notify_ui_location 0}
      {meta_performer remove_sink $this}
   )
   (get_score {int {game_panel get score}})
   (set_results {song.lbl set_song_name {meta_performer get_completed_song}})
   (SELECT_MSG
      {switch $component
         (restart.btn
            {meta_performer restart}
            {game_restart}
         )
         (perform.btn
            {gamemode set_mode perform}
            {meta_performer restart}
            {game_restart}
         )
         (change_difficulty.btn
            {{gamemode get ingame_seldiff_screen} set
               back_screen
               practice_endgame_screen
            }
            {ui goto_screen {gamemode get ingame_seldiff_screen}}
         )
         (continue.btn {ui pop_screen meta_loading_continue_screen})
      }
   )
}
{new HamScreen
   practice_endgame_screen
   (panels background_panel practice_endgame_panel)
   (focus practice_endgame_panel)
   ENDGAME_SCREEN_HANDLERS
}