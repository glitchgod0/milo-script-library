{new HamPanel
   choose_mode_panel
   (file "choose_mode.milo")
   (start_controller_focus practice.btn)
   (show_back_button 1)
   (enter
      {platform_mgr enable_xmp}
      {song.lbl set_song_name {meta_performer song}}
      {artist.lbl set_artist_name {meta_performer song}}
      {do
         ($pic {album_art find album_art.pic})
         {$pic set tex_file {song_mgr album_art_path {meta_performer song}}}
      }
      {do
         ($songid {song_mgr get_song_id {meta_performer song}})
         {practice_diff.lbl set_practice_diff $songid}
         {practice_score.lbl set_practice_score $songid}
         {perform_stars.std set_song $songid}
         {best_score.lbl set_best_score $songid}
         {intensity.sdd set_level
            {song_mgr rank_tier_for_song {meta_performer song}}
         }
      }
      {if {acc_mgr is_unlockable_asset {meta_performer song}}
         {$this disable practice.btn}
         {shell_input update_button_zones}
      }
   )
   (SELECT_MSG
      {switch $component
         (perform.btn
            {gamemode set_mode perform}
            {ui goto_screen {gamemode get seldiff_screen}}
         )
         (practice.btn
            {gamemode set_mode practice}
            {ui goto_screen {gamemode get seldiff_screen}}
         )
         (multiplayer.btn
            {gamemode set_mode multiplayer}
            {ui goto_screen {gamemode get seldiff_screen}}
         )
         (leaderboards.btn {ui goto_screen leaderboards_screen})
         (back.btn {ui go_back_screen {{ui current_screen} get back}})
      }
   )
}
{new HamScreen
   choose_mode_screen
   (panels meta background_panel choose_mode_panel)
   (focus choose_mode_panel)
   (back song_select_screen)
}