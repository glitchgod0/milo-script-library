#ifndef HX_XBOX
{new UIScreen
   tour_movie_screen
   (TRANSITION_COMPLETE_MSG {ui goto_screen {gamemode get tour_event_screen}})
}
{new UIPanel transition_overlay_panel (file "world/transition_video.milo")}
{new UIPanel
   tour_band_lower3rd_panel
   #ifdef HX_XBOX
   (file "tour_band_lower3rd.milo")
   #else
   (file "tour_band_lower3rd_og.milo")
   #endif
}
{new UIPanel
   tour_worldmap_lower3rd_panel
   (file "tour_worldmap_band_lower3rd_og.milo")
}
{new UIPanel
   transition_movie_panel
   (file "world/transition_video.milo")
   (moviefinished FALSE)
   (enter
      {set $moviefinished FALSE}
      {if_else {!= $new_town ''}
         {do
            ($tp {tour performer})
            ($new_town {$tp new_town})
            {{meta meta_music} stop}
            {$tp add_seen_town $new_town}
            {switch $new_town
               (boston
                  {video_file.mov set movie_file {sprintf "videos/placeholder_movie_act1"}}
               )
               (newyork {video_file.mov set movie_file {sprintf "videos/cube"}})
               (chicago {video_file.mov set movie_file {sprintf "videos/cube"}})
               (losangeles {video_file.mov set movie_file {sprintf "videos/cube"}})
               (seattle {video_file.mov set movie_file {sprintf "videos/cube"}})
               (berlin {video_file.mov set movie_file {sprintf "videos/cube"}})
               (london {video_file.mov set movie_file {sprintf "videos/cube"}})
               (stockholm {video_file.mov set movie_file {sprintf "videos/cube"}})
               (rome {video_file.mov set movie_file {sprintf "videos/cube"}})
               {video_file.mov set movie_file {sprintf "videos/cube"}}
            }
            {video_file.mov animate}
            {film_grain.filt animate (loop 0 101)}
         }
         {ui goto_screen {gamemode get tour_event_screen}}
      }
   )
   (poll
      {if {&& {! $moviefinished} {video_file.mov is_movie_done}}
         {do
            {set $moviefinished TRUE}
            {ui goto_screen {gamemode get tour_event_screen}}
         }
      }
   )
   (exit {{meta meta_music} start})
   (BUTTON_DOWN_MSG
      {if {&& {'||' {== $button kPad_Start} {== $button kPad_X}} {! $moviefinished}}
         {set $moviefinished TRUE}
         {ui goto_screen {gamemode get tour_event_screen}}
      }
   )
}
{new UIScreen
   transition_movie_screen
   (panels meta transition_movie_panel helpbar)
   (focus transition_movie_panel)
   (helpbar (helpbar_select))
}
{new UIPanel
   transition_movie1_panel
   (file "world/transition_video.milo")
   (moviefinished FALSE)
   (enter
      {set $moviefinished FALSE}
      {{meta meta_music} stop}
      {video_file.mov set movie_file {sprintf "videos/cube"}}
      {video_file.mov animate}
      {film_grain.filt animate (loop 0 101)}
   )
   (exit {{meta meta_music} start})
   (poll
      {if {&& {! $moviefinished} {video_file.mov is_movie_done}}
         {do
            {set $moviefinished TRUE}
            {ui goto_screen {gamemode get tour_event_screen}}
         }
      }
   )
   (BUTTON_DOWN_MSG
      {if {&& {'||' {== $button kPad_Start} {== $button kPad_X}} {! $moviefinished}}
         {set $moviefinished TRUE}
         {ui goto_screen {gamemode get tour_event_screen}}
      }
   )
}
{new UIScreen
   transition_movie1_screen
   (panels meta transition_movie1_panel)
   (focus transition_movie1_panel)
}
#endif
{new WorldMapPanel
   tour_world_bg_panel
   (file world/tour_worldmap.milo)
   (zoomed FALSE)
   (town boston)
   (venue big_club)
   (enter
      {set [zoomed] FALSE}
      {set [town] {{tour performer} town}}
      {set [venue] {{tour performer} venue}}
   )
   (update {$this sync [town] [venue] [zoomed]})
   (zoom_in
      {set [zoomed] TRUE}
      {$this update}
   )
   (zoom_out
      {set [zoomed] FALSE}
      {set [venue] ''}
      {$this update}
   )
   (zoom_in_impl {zoom.grp animate (dest {zoom.grp end_frame})})
   (zoom_out_impl {zoom.grp animate (dest 0)})
}
#define TOUR_HINT_PANEL_HANDLERS (
   (force_exit TRUE)
   (focus continue.btn)
   (SELECT_START_MSG
      {{tour hint} advance}
      {ui goto_screen
         {cond
            ({tour hint}
               {{tour hint} get screen}
            )
            ({gamemode is_solo} tour_solo_event_screen)
            #ifdef HX_EE
            ({{tour performer} can_leave_hometown} tour_solo_event_screen)
            ({{tour performer} can_leave_home_venue} tour_solo_event_screen)
            (TRUE tour_chooseevent_screen)
            #else
            ({{tour performer} can_leave_hometown} tour_band_town_screen)
            ({{tour performer} can_leave_home_venue} tour_band_venue_screen)
            (TRUE tour_band_event_screen)
            #endif
         }
         TRUE
      }
      kDataUnhandled
   )
)
{func log_tour
   ($system $data)
}
#define TOUR_EVENT_PANEL_HANDLERS (
   (confirm_event
      {log_tour "EVENT " {{tour event} event_token}}
      {{tour performer} set_random_songs}
      {game set_song {{tour performer} next_song}}
      {game set_venue {tour venue_variant}}
   )
   (is_updown
      ($button)
      {'||'
         {== $button kPad_DDown}
         {== $button kPad_DUp}
         {== $button kPad_Circle}
         {== $button kPad_Square}
      }
   )
   (is_updownpad
      ($button)
      {'||' {== $button kPad_DDown} {== $button kPad_DUp}}
   )
   (update_selection
      {do
         {set $provider {tour solo_event_provider}}
         {set $pos {event.lst selected_pos}}
         {set $town {$provider town $pos}}
         {set $venue {$provider venue $pos}}
         {{tour performer} set_town $town}
         {{tour performer} set_venue $venue}
      }
      {{tour performer} set_event {event.lst selected_sym} {game get leader_num}}
      {if {!= $old_town $town}
         {do
            {switch $old_town
               (boston {fadeoutact.mat set diffuse_tex act10.tex})
               (newyork {fadeoutact.mat set diffuse_tex act2.tex})
               (chicago {fadeoutact.mat set diffuse_tex act3.tex})
               (losangeles {fadeoutact.mat set diffuse_tex act4.tex})
               (seattle {fadeoutact.mat set diffuse_tex act5.tex})
               (berlin {fadeoutact.mat set diffuse_tex act6.tex})
               (london {fadeoutact.mat set diffuse_tex act7.tex})
               (stockholm {fadeoutact.mat set diffuse_tex act8.tex})
               (rome {fadeoutact.mat set diffuse_tex act9.tex})
               (sanfrancisco {fadeoutact.mat set diffuse_tex act1.tex})
               (paris {fadeoutact.mat set diffuse_tex act11.tex})
               (amsterdam {fadeoutact.mat set diffuse_tex act2.tex})
            }
            {if {!= $old_town -1}
               {fadeoutact.mnm animate}
            }
            {switch $town
               (boston {fadeinact.mat set diffuse_tex act10.tex})
               (newyork {fadeinact.mat set diffuse_tex act2.tex})
               (chicago {fadeinact.mat set diffuse_tex act3.tex})
               (losangeles {fadeinact.mat set diffuse_tex act4.tex})
               (seattle {fadeinact.mat set diffuse_tex act5.tex})
               (berlin {fadeinact.mat set diffuse_tex act6.tex})
               (london {fadeinact.mat set diffuse_tex act7.tex})
               (stockholm {fadeinact.mat set diffuse_tex act8.tex})
               (rome {fadeinact.mat set diffuse_tex act9.tex})
               (sanfrancisco {fadeinact.mat set diffuse_tex act1.tex})
               (paris {fadeinact.mat set diffuse_tex act11.tex})
               (amsterdam {fadeinact.mat set diffuse_tex act2.tex})
            }
            {fadeinact.mnm animate}
            {set $old_town $town}
         }
      }
      #ifdef HX_EE
      {meta music_stop_stream}
      #else
      {song_preview ''}
      #endif
   )
   (set_eventlist_position
      {set $max {{tour performer} total_world_fame}}
      {if {< {event.lst num_total} {'+' 1 $max}}
         {$this update_selection}
         {event.lst set_selected 0}
         {foreach_int $i 0 {event.lst num_total}
            {if_else {> {{tour performer} fame {tour event}} 0}
               {event.lst set_selected {'+' {event.lst selected_pos} 1}}
               {do
                  {set $i {- {event.lst num_total} 1}}
               }
            }
            {do
               {set $provider {tour solo_event_provider}}
               {set $pos {event.lst selected_pos}}
               {set $town {$provider town $pos}}
               {set $venue {$provider venue $pos}}
               {{tour performer} set_town $town}
               {{tour performer} set_venue $venue}
            }
            {{tour performer} set_event {event.lst selected_sym} {game get leader_num}}
         }
      }
      {if {<= {event.lst num_total} {'+' 1 $max}}
         {if {tour event}
            {do
               {event.lst set_selected {{tour event} get event_name}}
            }
         }
         {$this update_selection}
      }
   )
   (last_selection -1)
   (last_preview_play 0.0)
   (preview_playing FALSE)
   (BUTTON_UP_MSG
      {if
         {&&
            {$this is_updown $button}
            {==
               {gamecfg get_controller_type_from_controller $player_num}
               kControllerDrum
            }
            {gamecfg is_player_participating $player_num}
            {!= {event.lst selected_pos} [last_selection]}
         }
         {set [last_selection] {event.lst selected_pos}}
         {set [last_preview_play] {taskmgr ui_seconds}}
         {set [preview_playing] FALSE}
      }
      {if
         {&&
            {$this is_updownpad $button}
            {!=
               {gamecfg get_controller_type_from_controller $player_num}
               kControllerDrum
            }
            {gamecfg is_player_participating $player_num}
            {!= {event.lst selected_pos} [last_selection]}
         }
         {set [last_selection] {event.lst selected_pos}}
         {set [last_preview_play] {taskmgr ui_seconds}}
         {set [preview_playing] FALSE}
      }
      kDataUnhandled
   )
   (play_preview
      #ifdef HX_EE
      {meta music_stop_stream}
      #else
      {song_preview ''}
      #endif
      {do
         ($song_to_set {{tour performer} next_song})
         {if
            {&&
               {ui current_screen}
               {!= {ui current_screen} tier_unlock_screen}
               {!= {ui current_screen} sponsor_unlock_screen}
               {!= $song_to_set ""}
            }
            #ifdef HX_EE
            {meta music_start_stream {song_mgr song_preview_clip $song_to_set}}
            #else
            {song_preview $song_to_set}
            #endif
         }
      }
   )
   (exiting_play_preview
      #ifdef HX_EE
      {meta music_stop_stream}
      #else
      {song_preview ''}
      #endif
      {do
         ($song_to_set {{tour performer} next_song})
         {if {!= $song_to_set ""}
            #ifdef HX_EE
            {meta music_start_stream {song_mgr song_preview_clip $song_to_set}}
            #else
            {song_preview $song_to_set}
            #endif
         }
      }
   )
   (poll
      {do
         ($last_play_delta {- {taskmgr ui_seconds} [last_preview_play]})
         {if {&& {> $last_play_delta 0.25} {! [preview_playing]} {! {ui in_transition}}}
            {do
               {$this play_preview}
               {set [preview_playing] TRUE}
            }
         }
      }
   )
   (exit
      #ifdef HX_EE
      {meta music_stop_stream}
      #else
      {song_preview ''}
      #endif
      {set [last_preview_play] 0.0}
      {set [preview_playing] FALSE}
   )
   (BUTTON_DOWN_MSG
      {if {== $button kPad_Tri}
         {script_task kTaskSeconds
            (delay {'+' {{meta meta_music} fade_time} 0.1})
            (script {meta music_start})
         }
      }
      {if {== $button kPad_X}
         {synth play shell_crowd.cue}
      }
      kDataUnhandled
   )
)
#ifndef HX_XBOX
{new BandCreatePanel
   tour_band_create_panel
   (file selname_og.milo)
   (focus name.lbl)
   (reset_focus TRUE)
   (player_num {game get leader_num})
   (back_button_exits TRUE)
   (name_label name.lbl)
   (name_active TRUE)
   (randomize_button helpbar_random)
   (back_button helpbar_back)
   (helpbar ())
   NAMING_PANEL_HANDLERS
   (enter {$this init_name})
   (accept_name
      {{tour performer} set_char_name {$this get_name}}
      {if_else {== {{tour performer} get_char_name} ""}
         {do
            {set [kill] FALSE}
            {ui push_screen name_error_screen}
         }
         {if_else
            {!
               {profile_mgr is_char_name_unique
                  {gamecfg get leader_num}
                  {{tour performer} get_char_name}
               }
            }
            {do
               {set [kill] FALSE}
               {ui push_screen tour_band_create_name_not_unique_screen}
            }
            {do
               {$this stop_naming}
               {charcreator_panel add_char_to_profile}
               {if_else {== {gamemode get max_players} 1}
                  {autosave_push_goto tour_solo_diff_screen}
                  {do
                     {session clear}
                     {game set leader_num -1}
                     {autosave_push_goto {gamemode get matchmaking_screen}}
                  }
               }
            }
         }
      }
   )
   (extra_function {$this random_name})
   (cancel_name
      {if_else {== {gamemode get max_players} 1}
         {ui goto_screen {gamemode get matchmaking_screen}}
         {ui goto_screen tour_band_char_screen}
      }
   )
}
{new UIScreen
   tour_band_create_name_not_unique_screen
   (panels dialog_panel helpbar_dialog)
   (helpbar ('' helpbar_continue))
   (focus dialog_panel)
   LIMIT_TO_LEADER_SESSION
   (enter
      {helpbar_dialog set_config ("" helpbar_continue)}
      {helpbar_dialog set_use_all_controllers TRUE}
      {dialog_panel set_ok {localize_token tour_band_name_not_unique}}
   )
   (SELECT_MSG {ui pop_screen})
}
{new UIScreen
   tour_credits_goto_hxmovie_screen
   (TRANSITION_COMPLETE_MSG {ui goto_screen tour_credits_text_screen})
}
{new UIPanel
   tour_credits_hxmovie_panel
   (file "world/intro_video.milo")
   (moviefinished FALSE)
   (enter
      {16x9.mesh set_showing FALSE}
      {4x3.mesh set_showing FALSE}
      {16x9zoom.mesh set_showing FALSE}
      {4x3std.mesh set_showing TRUE}
      #ifdef HX_EE
      {intro_video.mov set movie_file "videos/rb_endcredits"}
      #else
      {intro_video.mov set movie_file "videos/hmx_endcredits.bik"}
      #endif
      {intro_video.mov animate}
      {set $moviefinished FALSE}
   )
   (poll
      {if {&& {! $moviefinished} {intro_video.mov is_movie_done}}
         {do
            {set $moviefinished TRUE}
            {ui goto_screen tour_credits_goto_pimovie_screen}
         }
      }
   )
   (BUTTON_DOWN_MSG
      {if {&& {'||' {== $button kPad_Start} {== $button kPad_X}} {! $moviefinished}}
         {do
            {set $moviefinished TRUE}
            #ifdef HX_WII
            {intro_video.mov set_movie_paused TRUE}
            #endif
            {ui goto_screen tour_credits_goto_pimovie_screen}
         }
      }
   )
}
{new UIScreen
   tour_credits_hxmovie_screen
   (panels tour_credits_hxmovie_panel)
   (focus tour_credits_hxmovie_panel)
}
{new UIScreen
   tour_credits_goto_pimovie_screen
   (TRANSITION_COMPLETE_MSG {ui goto_screen tour_credits_pimovie_screen})
}
{new UIPanel
   tour_credits_pimovie_panel
   (file "world/intro_video.milo")
   (moviefinished FALSE)
   (enter
      {16x9.mesh set_showing FALSE}
      {4x3.mesh set_showing FALSE}
      {16x9zoom.mesh set_showing FALSE}
      {4x3std.mesh set_showing TRUE}
      #ifdef HX_EE
      {intro_video.mov set movie_file "videos/pi_endcredits"}
      #else
      {intro_video.mov set movie_file "videos/pi_endcredits.bik"}
      #endif
      {intro_video.mov animate}
      {set $moviefinished FALSE}
   )
   (poll
      {if {&& {! $moviefinished} {intro_video.mov is_movie_done}}
         {do
            {set $moviefinished TRUE}
            {ui goto_screen tour_credits_text_screen}
         }
      }
   )
   (BUTTON_DOWN_MSG
      {if {&& {'||' {== $button kPad_Start} {== $button kPad_X}} {! $moviefinished}}
         {do
            {set $moviefinished TRUE}
            #ifdef HX_WII
            {intro_video.mov set_movie_paused TRUE}
            #endif
            {ui goto_screen tour_credits_text_screen}
         }
      }
   )
}
{new UIScreen
   tour_credits_pimovie_screen
   (panels tour_credits_pimovie_panel)
   (focus tour_credits_pimovie_panel)
}
{new CreditsPanel
   tour_credits_text_panel
   (file "credits.milo")
   (BUTTON_DOWN_MSG
      {if {== $button kPad_Tri}
         {ui goto_screen {gamemode get sendoff_screen}}
      }
   )
   (poll
      {if {! {credits.lst is_scrolling}}
         {if {! {ui in_transition}}
            {handle (ui credits_done)}
         }
      }
   )
}
{new UIScreen
   tour_credits_text_screen
   (panels tour_credits_text_panel helpbar)
   (helpbar (helpbar_back))
   (scroll_sfx FALSE)
   (focus tour_credits_text_panel)
   (enter
      {helpbar set_limit_to_player_num -1}
      {helpbar set_use_all_controllers TRUE}
      {helpbar set_config (helpbar_back)}
   )
   (exit {helpbar set_use_all_controllers FALSE})
   (credits_done
      {helpbar set_use_all_controllers FALSE}
      {ui goto_screen {gamemode get sendoff_screen}}
   )
}
#endif